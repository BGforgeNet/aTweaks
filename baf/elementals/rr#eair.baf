IF
    Global("RR#Active","LOCALS",0)
    !Allegiance(Myself,GOODCUTOFF)
    OR(4)
      See(NearestEnemyOf(Myself))
      AttackedBy([ANYONE],DEFAULT)
      TookDamage()
      Help([EVILCUTOFF.0.ELEMENTAL])
THEN
    RESPONSE #100
        SetGlobal("RR#Active","LOCALS",1)
        SetGlobalTimer("RR#Rest","LOCALS",2400) // 8 hours
        Enemy()
        Help()
        Continue()
END

IF
    Global("RR#Active","LOCALS",0)
    Allegiance(Myself,GOODCUTOFF)
THEN
    RESPONSE #100
        SetGlobal("RR#Active","LOCALS",1)
        Continue()
END

IF
    OR(3)
      Global("RR#Active","LOCALS",0)
      StateCheck(Myself,STATE_IMMOBILE) // using this instead of STATE_DISABLED because the latter includes STATE_CHARMED
      StateCheck(Myself,STATE_REALLY_DEAD) // prevents contingencies and other ReallyForceSpell() stuff from popping up on dead creatures
THEN
    RESPONSE #100
        NoAction()
END

IF
    StateCheck(Myself,STATE_PANIC)  // needed because the AI can perform script actions (i.e. drink potions) while panicked, unlike the player
THEN
    RESPONSE #100
        RandomWalkContinuous() // using this instead of NoAction() because otherwise panicked characters would stand in place
END

IF
    Global("RR#Active","LOCALS",1)
    !InActiveArea(Myself)
    !See(NearestEnemyOf(Myself))
THEN
    RESPONSE #100
        NoAction()
END

IF
    !Allegiance(Myself,GOODCUTOFF)
    !GlobalTimerNotExpired("RR#Rest","LOCALS") // re-initialize the rest timer
THEN
    RESPONSE #100
        SetGlobalTimer("RR#Rest","LOCALS",2400) // 8 hours
        SetGlobal("RR#Gust","LOCALS",0) // reset Gust of Wind variable
        ApplySpell(Myself,RESTORE_FULL_HEALTH) // heal up
        Rest()
END

IF
    !Allegiance(Myself,GOODCUTOFF)
    !GlobalTimerNotExpired("RR#Help","LOCALS") // shout for help every two rounds while in combat, other mephits may hear it and join the battle
    See(NearestEnemyOf(Myself))
THEN
    RESPONSE #100
        SetGlobalTimer("RR#Help","LOCALS",12)
        Help()
        Continue()
END

IF
    ActionListEmpty()
    !GlobalTimerNotExpired("RR#Cast","LOCALS")
    CheckStatGT(Myself,7,LEVEL) // 8 HD Air Elemental
    OR(3)
      CheckStatLT(ThirdNearestEnemyOf(Myself),3,LEVEL)
      CheckStatLT(SecondNearestEnemyOf(Myself),3,LEVEL)
      CheckStatLT(NearestEnemyOf(Myself),3,LEVEL)
THEN
    RESPONSE #100
        SetInterrupt(FALSE)
        SetGlobalTimer("RR#Cast","LOCALS",6)
        ForceSpellRES("rr#eairw",Myself) // Air Elemental Whirlwind
        RemoveSpellRES("rr#eairw") // remove one instance of the spell upon casting
        SetInterrupt(TRUE)
END

IF
    ActionListEmpty()
    !GlobalTimerNotExpired("RR#Cast","LOCALS")
    CheckStatGT(Myself,11,LEVEL) // 12 HD Air Elemental
    OR(3)
      CheckStatLT(ThirdNearestEnemyOf(Myself),4,LEVEL)
      CheckStatLT(SecondNearestEnemyOf(Myself),4,LEVEL)
      CheckStatLT(NearestEnemyOf(Myself),4,LEVEL)
THEN
    RESPONSE #100
        SetInterrupt(FALSE)
        SetGlobalTimer("RR#Cast","LOCALS",6)
        ForceSpellRES("rr#eairw",Myself) // Air Elemental Whirlwind
        RemoveSpellRES("rr#eairw") // remove one instance of the spell upon casting
        SetInterrupt(TRUE)
END

IF
    ActionListEmpty()
    !GlobalTimerNotExpired("RR#Cast","LOCALS")
    CheckStatGT(Myself,11,LEVEL) // 12 HD Air Elemental
    OR(3)
      CheckStatLT(ThirdNearestEnemyOf(Myself),4,LEVEL)
      CheckStatLT(SecondNearestEnemyOf(Myself),4,LEVEL)
      CheckStatLT(NearestEnemyOf(Myself),4,LEVEL)
THEN
    RESPONSE #100
        SetInterrupt(FALSE)
        SetGlobalTimer("RR#Cast","LOCALS",6)
        ForceSpellRES("rr#eairw",Myself) // Air Elemental Whirlwind
        RemoveSpellRES("rr#eairw") // remove one instance of the spell upon casting
        SetInterrupt(TRUE)
END

IF
    ActionListEmpty()
    !GlobalTimerNotExpired("RR#Cast","LOCALS")
    CheckStatGT(Myself,15,LEVEL) // 16 HD Air Elemental
    OR(3)
      CheckStatLT(ThirdNearestEnemyOf(Myself),5,LEVEL)
      CheckStatLT(SecondNearestEnemyOf(Myself),5,LEVEL)
      CheckStatLT(NearestEnemyOf(Myself),5,LEVEL)
THEN
    RESPONSE #100
        SetInterrupt(FALSE)
        SetGlobalTimer("RR#Cast","LOCALS",6)
        ForceSpellRES("rr#eairw",Myself) // Air Elemental Whirlwind
        RemoveSpellRES("rr#eairw") // remove one instance of the spell upon casting
        SetInterrupt(TRUE)
END

IF
    ActionListEmpty()
    !GlobalTimerNotExpired("RR#Cast","LOCALS")
    CheckStatGT(Myself,19,LEVEL) // 20 HD Air Elemental
    OR(3)
      CheckStatLT(ThirdNearestEnemyOf(Myself),6,LEVEL)
      CheckStatLT(SecondNearestEnemyOf(Myself),6,LEVEL)
      CheckStatLT(NearestEnemyOf(Myself),6,LEVEL)
THEN
    RESPONSE #100
        SetInterrupt(FALSE)
        SetGlobalTimer("RR#Cast","LOCALS",6)
        ForceSpellRES("rr#eairw",Myself) // Air Elemental Whirlwind
        RemoveSpellRES("rr#eairw") // remove one instance of the spell upon casting
        SetInterrupt(TRUE)
END

IF
    ActionListEmpty()
    !GlobalTimerNotExpired("RR#Cast","LOCALS")
    CheckStatGT(Myself,23,LEVEL) // 24 HD Air Elemental
    OR(3)
      CheckStatLT(ThirdNearestEnemyOf(Myself),7,LEVEL)
      CheckStatLT(SecondNearestEnemyOf(Myself),7,LEVEL)
      CheckStatLT(NearestEnemyOf(Myself),7,LEVEL)
THEN
    RESPONSE #100
        SetInterrupt(FALSE)
        SetGlobalTimer("RR#Cast","LOCALS",6)
        ForceSpellRES("rr#eairw",Myself) // Air Elemental Whirlwind
        RemoveSpellRES("rr#eairw") // remove one instance of the spell upon casting
        SetInterrupt(TRUE)
END

IF
    ActionListEmpty()
    Allegiance(Myself,GOODCUTOFF)
    HaveSpellRES("rr#eairw") // Air Elemental Whirlwind
    !GlobalTimerNotExpired("RR#Cast","LOCALS")
    NumCreatureGT([EVILCUTOFF],2) // at least three opponents are nearby (note: the NumCreatureGT() trigger doesn't work with NearestEnemyOf(Myself))
    Range([EVILCUTOFF],12)
    Range(SecondNearest([EVILCUTOFF]),12)
    Range(ThirdNearest([EVILCUTOFF]),12)
THEN
    RESPONSE #100
        SetInterrupt(FALSE)
        SetGlobalTimer("RR#Cast","LOCALS",6)
        ForceSpellRES("rr#eairw",Myself) // Air Elemental Whirlwind
        RemoveSpellRES("rr#eairw") // remove one instance of the spell upon casting
        SetInterrupt(TRUE)
END

IF
    Allegiance(Myself,EVILCUTOFF)
    HaveSpellRES("rr#eairw") // Air Elemental Whirlwind
    !GlobalTimerNotExpired("RR#Cast","LOCALS")
    NumCreatureGT([GOODCUTOFF],2) // at least three opponents are nearby (note: the NumCreatureGT() trigger doesn't work with NearestEnemyOf(Myself))
    Range([GOODCUTOFF],12)
    Range(SecondNearest([GOODCUTOFF]),12)
    Range(ThirdNearest([GOODCUTOFF]),12)
THEN
    RESPONSE #100
        SetInterrupt(FALSE)
        SetGlobalTimer("RR#Cast","LOCALS",6)
        ForceSpellRES("rr#eairw",Myself) // Air Elemental Whirlwind
        RemoveSpellRES("rr#eairw") // remove one instance of the spell upon casting
        SetInterrupt(TRUE)
END

IF
    ActionListEmpty()
    Allegiance(Myself,GOODCUTOFF)
    HaveSpellRES("rr#eairw") // Air Elemental Whirlwind
    !GlobalTimerNotExpired("RR#Cast","LOCALS")
    !GlobalTimerNotExpired("rr#eairw","LOCALS") // whirlwind timer
    NumCreatureGT([EVILCUTOFF],1) // at least two opponents are nearby (note: the NumCreatureGT() trigger doesn't work with NearestEnemyOf(Myself))
    Range([EVILCUTOFF],12)
    Range(SecondNearest([EVILCUTOFF]),12)
    Range(ThirdNearest([EVILCUTOFF]),12)
THEN
    RESPONSE #70
        SetGlobalTimer("rr#eairw","LOCALS",12) // whirlwind timer
    RESPONSE #30
        SetInterrupt(FALSE)
        SetGlobalTimer("RR#Cast","LOCALS",6)
        ForceSpellRES("rr#eairw",Myself) // Air Elemental Whirlwind
        RemoveSpellRES("rr#eairw") // remove one instance of the spell upon casting
        SetInterrupt(TRUE)
END

IF
    Allegiance(Myself,EVILCUTOFF)
    HaveSpellRES("rr#eairw") // Air Elemental Whirlwind
    !GlobalTimerNotExpired("RR#Cast","LOCALS")
    !GlobalTimerNotExpired("rr#eairw","LOCALS") // whirlwind timer
    NumCreatureGT([GOODCUTOFF],1) // at least two opponents are nearby (note: the NumCreatureGT() trigger doesn't work with NearestEnemyOf(Myself))
    Range([GOODCUTOFF],12)
    Range(SecondNearest([GOODCUTOFF]),12)
    Range(ThirdNearest([GOODCUTOFF]),12)
THEN
    RESPONSE #70
        SetGlobalTimer("rr#eairw","LOCALS",12) // whirlwind timer
    RESPONSE #30
        SetInterrupt(FALSE)
        SetGlobalTimer("RR#Cast","LOCALS",6)
        ForceSpellRES("rr#eairw",Myself) // Air Elemental Whirlwind
        RemoveSpellRES("rr#eairw") // remove one instance of the spell upon casting
        SetInterrupt(TRUE)
END

IF
    ActionListEmpty()
    HaveSpellRES("rr#eairw") // Air Elemental Whirlwind
    !GlobalTimerNotExpired("RR#Cast","LOCALS")
    !GlobalTimerNotExpired("rr#eairw","LOCALS") // whirlwind timer
    Range(NearestEnemyOf(Myself),12) // at least one opponent is nearby
THEN
    RESPONSE #85
        SetGlobalTimer("rr#eairw","LOCALS",12) // whirlwind timer
    RESPONSE #15
        SetInterrupt(FALSE)
        SetGlobalTimer("RR#Cast","LOCALS",6)
        ForceSpellRES("rr#eairw",Myself) // Air Elemental Whirlwind
        RemoveSpellRES("rr#eairw") // remove one instance of the spell upon casting
        SetInterrupt(TRUE)
END

IF
    !GlobalTimerNotExpired("RR#Cast","LOCALS")
    HaveSpellRES("rr#ecncl") // Elemental Concealment
    !StateCheck(Myself,STATE_INVISIBLE)
    !Global("dvgldust","LOCALS",1)
    !CheckStatGT(Player1,0,TRUE_SIGHT)
    !CheckStatGT(Player2,0,TRUE_SIGHT)
    !CheckStatGT(Player3,0,TRUE_SIGHT)
    !CheckStatGT(Player4,0,TRUE_SIGHT)
    !CheckStatGT(Player5,0,TRUE_SIGHT)
    !CheckStatGT(Player6,0,TRUE_SIGHT)
    !CheckStatGT([GOODCUTOFF.0.PLANATAR],0,TRUE_SIGHT)
    !CheckStatGT([GOODCUTOFF.0.DARKPLANATAR],0,TRUE_SIGHT)
THEN
    RESPONSE #100
        SetInterrupt(FALSE)
        SetGlobalTimer("RR#Cast","LOCALS",6)
        ForceSpellRES("rr#ecncl",Myself) // Air Elemental Whirlwind
        RemoveSpellRES("rr#ecncl") // remove one instance of the spell upon casting
        SetInterrupt(TRUE)
END

IF
    !GlobalTimerNotExpired("rr#clear","LOCALS") // prevent the creature from getting stuck in an attack pattern because ActionListEmpty() is used above
    See(NearestEnemyOf(Myself))
THEN
    RESPONSE #100
        SetGlobalTimer("rr#clear","LOCALS",6)
        ClearActions(Myself)
END

IF
    !CheckStatGT(SixthNearestEnemyOf(Myself),0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle of higher
    CheckStatLT(SixthNearestEnemyOf(Myself),100,RESISTCRUSHING)
    See(NearestEnemyOf(Myself))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    !CheckStatGT(SixthNearestEnemyOf(Myself),0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle of higher
    !StateCheck(SixthNearestEnemyOf(Myself),STATE_PANIC)
    CheckStatLT(SixthNearestEnemyOf(Myself),100,RESISTCRUSHING)
    CheckStatLT(SixthNearestEnemyOf(Myself),1,STONESKINS)
    !StateCheck(SixthNearestEnemyOf(Myself),STATE_MIRRORIMAGE)
    See(SixthNearestEnemyOf(Myself))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    !CheckStatGT(FifthNearestEnemyOf(Myself),0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle of higher
    !StateCheck(FifthNearestEnemyOf(Myself),STATE_PANIC)
    CheckStatLT(FifthNearestEnemyOf(Myself),100,RESISTCRUSHING)
    CheckStatLT(FifthNearestEnemyOf(Myself),1,STONESKINS)
    !StateCheck(FifthNearestEnemyOf(Myself),STATE_MIRRORIMAGE)
    See(FifthNearestEnemyOf(Myself))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    !CheckStatGT(FourthNearestEnemyOf(Myself),0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle of higher
    !StateCheck(FourthNearestEnemyOf(Myself),STATE_PANIC)
    CheckStatLT(FourthNearestEnemyOf(Myself),100,RESISTCRUSHING)
    CheckStatLT(FourthNearestEnemyOf(Myself),1,STONESKINS)
    !StateCheck(FourthNearestEnemyOf(Myself),STATE_MIRRORIMAGE)
    See(FourthNearestEnemyOf(Myself))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    !CheckStatGT(ThirdNearestEnemyOf(Myself),0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle of higher
    !StateCheck(ThirdNearestEnemyOf(Myself),STATE_PANIC)
    CheckStatLT(ThirdNearestEnemyOf(Myself),100,RESISTCRUSHING)
    CheckStatLT(ThirdNearestEnemyOf(Myself),1,STONESKINS)
    !StateCheck(ThirdNearestEnemyOf(Myself),STATE_MIRRORIMAGE)
    See(ThirdNearestEnemyOf(Myself))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    !CheckStatGT(SecondNearestEnemyOf(Myself),0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle of higher
    !StateCheck(SecondNearestEnemyOf(Myself),STATE_PANIC)
    CheckStatLT(SecondNearestEnemyOf(Myself),100,RESISTCRUSHING)
    CheckStatLT(SecondNearestEnemyOf(Myself),1,STONESKINS)
    !StateCheck(SecondNearestEnemyOf(Myself),STATE_MIRRORIMAGE)
    See(SecondNearestEnemyOf(Myself))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    !CheckStatGT(NearestEnemyOf(Myself),0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle of higher
    !StateCheck(NearestEnemyOf(Myself),STATE_PANIC)
    CheckStatLT(NearestEnemyOf(Myself),100,RESISTCRUSHING)
    CheckStatLT(NearestEnemyOf(Myself),1,STONESKINS)
    !StateCheck(NearestEnemyOf(Myself),STATE_MIRRORIMAGE)
    See(NearestEnemyOf(Myself))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    Allegiance(Myself,EVILCUTOFF)
    !StateCheck(WorstAC([PC]),STATE_PANIC)
    !StateCheck(WorstAC([PC]),STATE_MIRRORIMAGE)
    !CheckStatGT(WorstAC([PC]),0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle of higher
    CheckStatLT(WorstAC([PC]),100,RESISTCRUSHING)
    CheckStatLT(WorstAC([PC]),1,STONESKINS)
    See(WorstAC([PC]))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    AttackedBy(NearestEnemyOf(Myself),DEFAULT)
    !CheckStatGT(LastAttackerOf(Myself),0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle of higher
    !StateCheck(LastAttackerOf(Myself),STATE_PANIC)
    CheckStatLT(LastAttackerOf(Myself),100,RESISTCRUSHING)
    !StateCheck(LastAttackerOf(Myself),STATE_MIRRORIMAGE)
    CheckStatLT(LastAttackerOf(Myself),1,STONESKINS)
    Range(LastAttackerOf(Myself),8)
    RandomNum(3,1)
    See(LastAttackerOf(Myself))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    AttackedBy(NearestEnemyOf(Myself),DEFAULT)
    !CheckStatGT(LastAttackerOf(Myself),0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle of higher
    !StateCheck(LastAttackerOf(Myself),STATE_PANIC)
    CheckStatLT(LastAttackerOf(Myself),100,RESISTCRUSHING)
    !StateCheck(LastAttackerOf(Myself),STATE_MIRRORIMAGE)
    CheckStatLT(LastAttackerOf(Myself),1,STONESKINS)
    Range(LastAttackerOf(Myself),4)
    See(LastAttackerOf(Myself))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    !CheckStatGT(ThirdNearestEnemyOfType([0.0.0.BARD_ALL]),0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle of higher
    !StateCheck(ThirdNearestEnemyOfType([0.0.0.BARD_ALL]),STATE_PANIC)
    CheckStatLT(ThirdNearestEnemyOfType([0.0.0.BARD_ALL]),100,RESISTCRUSHING)
    !StateCheck(ThirdNearestEnemyOfType([0.0.0.BARD_ALL]),STATE_MIRRORIMAGE)
    CheckStatLT(ThirdNearestEnemyOfType([0.0.0.BARD_ALL]),1,STONESKINS)
    See(ThirdNearestEnemyOfType([0.0.0.BARD_ALL])) // these blocks are optimized for targeting low save vs. breath characters such as single-class thieves and bards
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    !CheckStatGT(SecondNearestEnemyOfType([0.0.0.BARD_ALL]),0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle of higher
    !StateCheck(SecondNearestEnemyOfType([0.0.0.BARD_ALL]),STATE_PANIC)
    CheckStatLT(SecondNearestEnemyOfType([0.0.0.BARD_ALL]),100,RESISTCRUSHING)
    !StateCheck(SecondNearestEnemyOfType([0.0.0.BARD_ALL]),STATE_MIRRORIMAGE)
    CheckStatLT(SecondNearestEnemyOfType([0.0.0.BARD_ALL]),1,STONESKINS)
    See(SecondNearestEnemyOfType([0.0.0.BARD_ALL])) // these blocks are optimized for targeting low save vs. breath characters such as single-class thieves and bards
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    !CheckStatGT(NearestEnemyOfType([0.0.0.BARD_ALL]),0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle of higher
    !StateCheck(NearestEnemyOfType([0.0.0.BARD_ALL]),STATE_PANIC)
    CheckStatLT(NearestEnemyOfType([0.0.0.BARD_ALL]),100,RESISTCRUSHING)
    !StateCheck(NearestEnemyOfType([0.0.0.BARD_ALL]),STATE_MIRRORIMAGE)
    CheckStatLT(NearestEnemyOfType([0.0.0.BARD_ALL]),1,STONESKINS)
    See(NearestEnemyOfType([0.0.0.BARD_ALL])) // these blocks are optimized for targeting low save vs. breath characters such as single-class thieves and bards
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    !CheckStatGT(ThirdNearestEnemyOfType([0.0.0.THIEF]),0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle of higher
    !StateCheck(ThirdNearestEnemyOfType([0.0.0.THIEF]),STATE_PANIC)
    CheckStatLT(ThirdNearestEnemyOfType([0.0.0.THIEF]),100,RESISTCRUSHING)
    !StateCheck(ThirdNearestEnemyOfType([0.0.0.THIEF]),STATE_MIRRORIMAGE)
    CheckStatLT(ThirdNearestEnemyOfType([0.0.0.THIEF]),1,STONESKINS)
    See(ThirdNearestEnemyOfType([0.0.0.THIEF])) // these blocks are optimized for targeting low save vs. breath characters such as single-class thieves and bards
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    !CheckStatGT(SecondNearestEnemyOfType([0.0.0.THIEF]),0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle of higher
    !StateCheck(SecondNearestEnemyOfType([0.0.0.THIEF]),STATE_PANIC)
    CheckStatLT(SecondNearestEnemyOfType([0.0.0.THIEF]),100,RESISTCRUSHING)
    !StateCheck(SecondNearestEnemyOfType([0.0.0.THIEF]),STATE_MIRRORIMAGE)
    CheckStatLT(SecondNearestEnemyOfType([0.0.0.THIEF]),1,STONESKINS)
    See(SecondNearestEnemyOfType([0.0.0.THIEF])) // these blocks are optimized for targeting low save vs. breath characters such as single-class thieves and bards
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    !CheckStatGT(NearestEnemyOfType([0.0.0.THIEF]),0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle of higher
    !StateCheck(NearestEnemyOfType([0.0.0.THIEF]),STATE_PANIC)
    CheckStatLT(NearestEnemyOfType([0.0.0.THIEF]),100,RESISTCRUSHING)
    !StateCheck(NearestEnemyOfType([0.0.0.THIEF]),STATE_MIRRORIMAGE)
    CheckStatLT(NearestEnemyOfType([0.0.0.THIEF]),1,STONESKINS)
    See(NearestEnemyOfType([0.0.0.THIEF])) // these blocks are optimized for targeting low save vs. breath characters such as single-class thieves and bards
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    !CheckStatGT(SixthNearestEnemyOf(Myself),0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle of higher
    StateCheck(SixthNearestEnemyOf(Myself),STATE_IMMOBILE)
    CheckStatLT(SixthNearestEnemyOf(Myself),100,RESISTCRUSHING)
    See(SixthNearestEnemyOf(Myself))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    !CheckStatGT(FifthNearestEnemyOf(Myself),0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle of higher
    StateCheck(FifthNearestEnemyOf(Myself),STATE_IMMOBILE)
    CheckStatLT(FifthNearestEnemyOf(Myself),100,RESISTCRUSHING)
    See(FifthNearestEnemyOf(Myself))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    !CheckStatGT(FourthNearestEnemyOf(Myself),0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle of higher
    StateCheck(FourthNearestEnemyOf(Myself),STATE_IMMOBILE)
    CheckStatLT(FourthNearestEnemyOf(Myself),100,RESISTCRUSHING)
    See(FourthNearestEnemyOf(Myself))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    !CheckStatGT(ThirdNearestEnemyOf(Myself),0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle of higher
    StateCheck(ThirdNearestEnemyOf(Myself),STATE_IMMOBILE)
    CheckStatLT(ThirdNearestEnemyOf(Myself),100,RESISTCRUSHING)
    See(ThirdNearestEnemyOf(Myself))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    !CheckStatGT(SecondNearestEnemyOf(Myself),0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle of higher
    StateCheck(SecondNearestEnemyOf(Myself),STATE_IMMOBILE)
    CheckStatLT(SecondNearestEnemyOf(Myself),100,RESISTCRUSHING)
    See(SecondNearestEnemyOf(Myself))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    !CheckStatGT(NearestEnemyOf(Myself),0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle of higher
    StateCheck(NearestEnemyOf(Myself),STATE_IMMOBILE)
    CheckStatLT(NearestEnemyOf(Myself),100,RESISTCRUSHING)
    See(NearestEnemyOf(Myself))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    Allegiance(Myself,EVILCUTOFF)
    Global("Elementalcontrol","LOCALS",2) // summoned elemental out of control
    See(LastSummonerOf(Myself))
    False()
THEN
    RESPONSE #100
        NoAction()
END

IF
    Allegiance(Myself,EVILCUTOFF)
    !StateCheck(LastSeenBy(Myself),STATE_REALLY_DEAD)
    !StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)    
    CheckStatLT(LastSeenBy(Myself),100,RESISTCRUSHING)
THEN
    RESPONSE #100
        AttackReevaluate(LastSeenBy(Myself),15)
END

IF
    Allegiance(Myself,GOODCUTOFF)
    ActionListEmpty()
    !InParty(LastSeenBy(Myself))
    !Allegiance(LastSeenBy(Myself),GOODCUTOFF)
    !StateCheck(LastSeenBy(Myself),STATE_REALLY_DEAD)
    !StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)
    CheckStatLT(LastSeenBy(Myself),100,RESISTCRUSHING)
THEN
    RESPONSE #100
        AttackReevaluate(LastSeenBy(Myself),15)
END

IF
    Allegiance(Myself,EVILCUTOFF)
    !GlobalTimerNotExpired("RR#Move","LOCALS") // help -> move timer
    Help([EVILCUTOFF.0.ELEMENTAL])
    InMyArea(LastHelp(Myself))
    !StateCheck(LastHelp(Myself),STATE_REALLY_DEAD)
THEN
    RESPONSE #100
        SetGlobalTimer("RR#Move","LOCALS",6)
        MoveToObject(LastHelp(Myself))
END

IF
    Allegiance(Myself,EVILCUTOFF) // only RandomWalk if hostile
    StateCheck(Myself,STATE_INVISIBLE) // if Natural Invisibility is active and there is nothing better to do
    !GlobalTimerNotExpired("RR#Move","LOCALS")
THEN
    RESPONSE #100
        RandomWalkContinuous()
END
