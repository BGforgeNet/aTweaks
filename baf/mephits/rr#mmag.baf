IF
	Global("RR#Active","LOCALS",0)
	OR(5)
	  AttackedBy([ANYONE],DEFAULT)
	  TookDamage()
	  Help([EVILCUTOFF.0.MEPHIT])
	  See([GOODCUTOFF])
	  Gender(Myself,3) // gated mephit
THEN
	RESPONSE #100
		SetGlobal("RR#Active","LOCALS",1)
		SetGlobalTimer("RR#Rest","LOCALS",2400) // 8 hours
		Enemy()
		Help()
		Continue()
END

IF
	OR(4)
	  Global("RR#Active","LOCALS",0)
          !Allegiance(Myself,EVILCUTOFF) // better way to check for charm because STATE_CHARMED persists when the creature is attacked and becomes red circled again
	  StateCheck(Myself,STATE_IMMOBILE) // using this instead of STATE_DISABLED because the latter includes STATE_CHARMED
	  StateCheck(Myself,STATE_REALLY_DEAD) // prevents contingencies and other ReallyForceSpell() stuff from popping up on dead creatures
THEN
	RESPONSE #100
		NoAction()
END

IF

	StateCheck(Myself,STATE_PANIC)  // needed because the AI can perform script actions (i.e. drink potions) while panicked, unlike the player
THEN
	RESPONSE #100
		RandomWalkContinuous() // using this instead of NoAction() because otherwise panicked characters would stand in place
END

IF
	Global("RR#Active","LOCALS",1)
	!InActiveArea(Myself)
	!See([GOODCUTOFF])
THEN
	RESPONSE #100
		NoAction()
END

IF
	!GlobalTimerNotExpired("RR#Rest","LOCALS") // re-initialize the rest timer
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Rest","LOCALS",2400) // 8 hours
		SetGlobal("RR#MBall","LOCALS",0) // reset Magma Ball variable
                ApplySpell(Myself,RESTORE_FULL_HEALTH) // heal up
                Rest()
END

IF
	!GlobalTimerNotExpired("RR#Hems","LOCALS")
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Hems","LOCALS",6)
		ForceSpellRES("RR#MMAHE",Myself) // Heat Emission
                CreateVisualEffectObject("ICFIRSDI",Myself)
                Continue()
END

IF
	!GlobalTimerNotExpired("RR#Help","LOCALS") // shout for help every two rounds while in combat, other mephits may hear it and join the battle
	See([GOODCUTOFF])
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Help","LOCALS",12)
		Help()
		Continue()
END

IF
	!Gender(Myself,3) // the mephit has not been gated
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	!GlobalTimerNotExpired("RR#Gate","LOCALS")
        OR(3)
          RandomNum(7,1)
	  HPPercentLT(Myself,31)
	  !See([GOODCUTOFF])
THEN
	RESPONSE #25
		SetGlobalTimer("RR#Cast","LOCALS",6)
		SetGlobalTimer("RR#Gate","LOCALS",300) // a gate can be opened only once per hour
                DisplayStringHead(Myself,@1800) // attempts to open an interplanar gate
		ForceSpellRES("RR#MMAGT",Myself) // Gate (Magma Mephit version)
	RESPONSE #75
		SetGlobalTimer("RR#Cast","LOCALS",6)
		SetGlobalTimer("RR#Gate","LOCALS",300) // a gate can be opened only once per hour
                DisplayStringHead(Myself,@1801) // fails to open an interplanar gate
		SpellNoDec(Myself,DO_NOTHING)
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	!GlobalTimerNotExpired("RR#Bwpn","LOCALS")
	GlobalLT("RR#MBall","LOCALS",8) // 8x per day
	See([GOODCUTOFF.0.0.MAGE_ALL])
	CheckStatLT(LastSeenBy(Myself),50,RESISTFIRE)
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Cast","LOCALS",6)
		SetGlobalTimer("RR#Bwpn","LOCALS",18) // breath weapon may be used once per 3 rounds
                IncrementGlobal("RR#MBall","LOCALS",1)
		ForceSpellRES("RR#MMAMB",LastSeenBy(Myself)) // Magma Ball
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	!GlobalTimerNotExpired("RR#Bwpn","LOCALS")
	GlobalLT("RR#MBall","LOCALS",8) // 8x per day
	See(SecondNearest([GOODCUTOFF.0.0.MAGE_ALL]))
	CheckStatLT(LastSeenBy(Myself),50,RESISTFIRE)
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Cast","LOCALS",6)
                IncrementGlobal("RR#MBall","LOCALS",1)
		ForceSpellRES("RR#MMAMB",LastSeenBy(Myself)) // Magma Ball
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	!GlobalTimerNotExpired("RR#Bwpn","LOCALS")
	GlobalLT("RR#MBall","LOCALS",8) // 8x per day
	See([GOODCUTOFF.0.0.BARD_ALL])
	CheckStatLT(LastSeenBy(Myself),50,RESISTFIRE)
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Cast","LOCALS",6)
                IncrementGlobal("RR#MBall","LOCALS",1)
		ForceSpellRES("RR#MMAMB",LastSeenBy(Myself)) // Magma Ball
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	!GlobalTimerNotExpired("RR#Bwpn","LOCALS")
	GlobalLT("RR#MBall","LOCALS",8) // 8x per day
	See(SecondNearest([GOODCUTOFF.0.0.BARD_ALL]))
	CheckStatLT(LastSeenBy(Myself),50,RESISTFIRE)
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Cast","LOCALS",6)
                IncrementGlobal("RR#MBall","LOCALS",1)
		ForceSpellRES("RR#MMAMB",LastSeenBy(Myself)) // Magma Ball
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	!GlobalTimerNotExpired("RR#Bwpn","LOCALS")
	GlobalLT("RR#MBall","LOCALS",8) // 8x per day
	See([GOODCUTOFF.0.0.CLERIC_ALL])
	CheckStatLT(LastSeenBy(Myself),50,RESISTFIRE)
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Cast","LOCALS",6)
                IncrementGlobal("RR#MBall","LOCALS",1)
		ForceSpellRES("RR#MMAMB",LastSeenBy(Myself)) // Magma Ball
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	!GlobalTimerNotExpired("RR#Bwpn","LOCALS")
	GlobalLT("RR#MBall","LOCALS",8) // 8x per day
	See(SecondNearest([GOODCUTOFF.0.0.CLERIC_ALL]))
	CheckStatLT(LastSeenBy(Myself),50,RESISTFIRE)
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Cast","LOCALS",6)
                IncrementGlobal("RR#MBall","LOCALS",1)
		ForceSpellRES("RR#MMAMB",LastSeenBy(Myself)) // Magma Ball
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	!GlobalTimerNotExpired("RR#Bwpn","LOCALS")
	GlobalLT("RR#MBall","LOCALS",8) // 8x per day
	See([GOODCUTOFF.0.0.DRUID_ALL])
	CheckStatLT(LastSeenBy(Myself),50,RESISTFIRE)
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Cast","LOCALS",6)
                IncrementGlobal("RR#MBall","LOCALS",1)
		ForceSpellRES("RR#MMAMB",LastSeenBy(Myself)) // Magma Ball
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	!GlobalTimerNotExpired("RR#Bwpn","LOCALS")
	GlobalLT("RR#MBall","LOCALS",8) // 8x per day
	See(SecondNearest([GOODCUTOFF.0.0.DRUID_ALL]))
	CheckStatLT(LastSeenBy(Myself),50,RESISTFIRE)
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Cast","LOCALS",6)
                IncrementGlobal("RR#MBall","LOCALS",1)
		ForceSpellRES("RR#MMAMB",LastSeenBy(Myself)) // Magma Ball
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	!GlobalTimerNotExpired("RR#Bwpn","LOCALS")
	GlobalLT("RR#MBall","LOCALS",8) // 8x per day
	See(MostDamagedOf([PC]))
	CheckStatLT(LastSeenBy(Myself),50,RESISTFIRE)
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Cast","LOCALS",6)
                IncrementGlobal("RR#MBall","LOCALS",1)
		ForceSpellRES("RR#MMAMB",LastSeenBy(Myself)) // Magma Ball
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	!GlobalTimerNotExpired("RR#Bwpn","LOCALS")
	GlobalLT("RR#MBall","LOCALS",8) // 8x per day
	See([GOODCUTOFF])
	CheckStatLT(LastSeenBy(Myself),50,RESISTFIRE)
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Cast","LOCALS",6)
                IncrementGlobal("RR#MBall","LOCALS",1)
		ForceSpellRES("RR#MMAMB",LastSeenBy(Myself)) // Magma Ball
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	!GlobalTimerNotExpired("RR#Bwpn","LOCALS")
	GlobalLT("RR#MBall","LOCALS",8) // 8x per day
	See(SecondNearest([GOODCUTOFF]))
	CheckStatLT(LastSeenBy(Myself),50,RESISTFIRE)
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Cast","LOCALS",6)
                IncrementGlobal("RR#MBall","LOCALS",1)
		ForceSpellRES("RR#MMAMB",LastSeenBy(Myself)) // Magma Ball
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	!GlobalTimerNotExpired("RR#Bwpn","LOCALS")
	GlobalLT("RR#MBall","LOCALS",8) // 8x per day
	See(ThirdNearest([GOODCUTOFF]))
	CheckStatLT(LastSeenBy(Myself),50,RESISTFIRE)
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Cast","LOCALS",6)
                IncrementGlobal("RR#MBall","LOCALS",1)
		ForceSpellRES("RR#MMAMB",LastSeenBy(Myself)) // Magma Ball
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	!GlobalTimerNotExpired("RR#Bwpn","LOCALS")
	GlobalLT("RR#MBall","LOCALS",8) // 8x per day
	See(FourthNearest([GOODCUTOFF]))
	CheckStatLT(LastSeenBy(Myself),50,RESISTFIRE)
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Cast","LOCALS",6)
                IncrementGlobal("RR#MBall","LOCALS",1)
		ForceSpellRES("RR#MMAMB",LastSeenBy(Myself)) // Magma Ball
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	!GlobalTimerNotExpired("RR#Bwpn","LOCALS")
	GlobalLT("RR#MBall","LOCALS",8) // 8x per day
	See(FifthNearest([GOODCUTOFF]))
	CheckStatLT(LastSeenBy(Myself),50,RESISTFIRE)
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Cast","LOCALS",6)
                IncrementGlobal("RR#MBall","LOCALS",1)
		ForceSpellRES("RR#MMAMB",LastSeenBy(Myself)) // Magma Ball
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	!GlobalTimerNotExpired("RR#Bwpn","LOCALS")
	GlobalLT("RR#MBall","LOCALS",8) // 8x per day
	See(SixthNearest([GOODCUTOFF]))
	CheckStatLT(LastSeenBy(Myself),50,RESISTFIRE)
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Cast","LOCALS",6)
                IncrementGlobal("RR#MBall","LOCALS",1)
		ForceSpellRES("RR#MMAMB",LastSeenBy(Myself)) // Magma Ball
END

IF
	!Gender(Myself,3) // the mephit has not been gated
	!GlobalTimerNotExpired("RR#Cast","LOCALS") // open a gate if all other abilities are unavailable
	!GlobalTimerNotExpired("RR#Gate","LOCALS")
THEN
	RESPONSE #25
		SetGlobalTimer("RR#Cast","LOCALS",6)
		SetGlobalTimer("RR#Gate","LOCALS",300) // a gate can be opened only once per hour
                DisplayStringHead(Myself,@1800) // attempts to open an interplanar gate
		ForceSpellRES("RR#MMAGT",Myself) // Gate (Magma Mephit version)
	RESPONSE #75
		SetGlobalTimer("RR#Cast","LOCALS",6)
		SetGlobalTimer("RR#Gate","LOCALS",300) // a gate can be opened only once per hour
                DisplayStringHead(Myself,@1801) // fails to open an interplanar gate
		SpellNoDec(Myself,DO_NOTHING)
END

IF
	OR(6)  // note: this block specifically checks for Players in order to target unconscious opponents (if any) since they can't be seen otherwise
	  See(Player1)
	  See(Player2)
	  See(Player3)
	  See(Player4)
	  See(Player5)
	  See(Player6)
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT([GOODCUTOFF],0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle of higher
        CheckStatLT([GOODCUTOFF],100,RESISTFIRE)
	See([GOODCUTOFF]) // note: this block is intended to check for opponents who may be panicked
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT(SixthNearest([GOODCUTOFF]),0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle of higher
	!StateCheck(SixthNearest([GOODCUTOFF]),STATE_PANIC)
        CheckStatLT(SixthNearest([GOODCUTOFF]),100,RESISTFIRE)
	!StateCheck(SixthNearest([GOODCUTOFF]),STATE_MIRRORIMAGE)
	See(SixthNearest([GOODCUTOFF]))
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT(FifthNearest([GOODCUTOFF]),0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle of higher
	!StateCheck(FifthNearest([GOODCUTOFF]),STATE_PANIC)
        CheckStatLT(FifthNearest([GOODCUTOFF]),100,RESISTFIRE)
	!StateCheck(FifthNearest([GOODCUTOFF]),STATE_MIRRORIMAGE)
	See(FifthNearest([GOODCUTOFF]))
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT(FourthNearest([GOODCUTOFF]),0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle of higher
	!StateCheck(FourthNearest([GOODCUTOFF]),STATE_PANIC)
        CheckStatLT(FourthNearest([GOODCUTOFF]),100,RESISTFIRE)
	!StateCheck(FourthNearest([GOODCUTOFF]),STATE_MIRRORIMAGE)
	See(FourthNearest([GOODCUTOFF]))
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT(ThirdNearest([GOODCUTOFF]),0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle of higher
	!StateCheck(ThirdNearest([GOODCUTOFF]),STATE_PANIC)
        CheckStatLT(ThirdNearest([GOODCUTOFF]),100,RESISTFIRE)
	!StateCheck(ThirdNearest([GOODCUTOFF]),STATE_MIRRORIMAGE)
	See(ThirdNearest([GOODCUTOFF]))
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT(SecondNearest([GOODCUTOFF]),0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle of higher
	!StateCheck(SecondNearest([GOODCUTOFF]),STATE_PANIC)
        CheckStatLT(SecondNearest([GOODCUTOFF]),100,RESISTFIRE)
	!StateCheck(SecondNearest([GOODCUTOFF]),STATE_MIRRORIMAGE)
	See(SecondNearest([GOODCUTOFF]))
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT([GOODCUTOFF],0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle of higher
	!StateCheck([GOODCUTOFF],STATE_PANIC)
        CheckStatLT([GOODCUTOFF],100,RESISTFIRE)
	!StateCheck([GOODCUTOFF],STATE_MIRRORIMAGE)
	See([GOODCUTOFF])
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT(SecondNearest([PC.0.0.MAGE]),0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle of higher
	!StateCheck(SecondNearest([PC.0.0.MAGE]),STATE_PANIC)
        CheckStatLT(SecondNearest([PC.0.0.MAGE]),100,RESISTFIRE)
	!StateCheck(SecondNearest([PC.0.0.MAGE]),STATE_MIRRORIMAGE)
	See(SecondNearest([PC.0.0.MAGE])) // these blocks are optimized for targeting low AC characters such as single-class mages and mage/thieves
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT([PC.0.0.MAGE],0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle of higher
	!StateCheck([PC.0.0.MAGE],STATE_PANIC)
        CheckStatLT([PC.0.0.MAGE],100,RESISTFIRE)
	!StateCheck([PC.0.0.MAGE],STATE_MIRRORIMAGE)
	See([PC.0.0.MAGE]) // these blocks are optimized for targeting low AC characters such as single-class mages and mage/thieves
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT(SecondNearest([PC.0.0.MAGE_THIEF]),0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle of higher
	!StateCheck(SecondNearest([PC.0.0.MAGE_THIEF]),STATE_PANIC)
        CheckStatLT(SecondNearest([PC.0.0.MAGE_THIEF]),100,RESISTFIRE)
	!StateCheck(SecondNearest([PC.0.0.MAGE_THIEF]),STATE_MIRRORIMAGE)
	See(SecondNearest([PC.0.0.MAGE_THIEF])) // these blocks are optimized for targeting low AC characters such as single-class mages and mage/thieves
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT([PC.0.0.MAGE_THIEF],0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle of higher
	!StateCheck([PC.0.0.MAGE_THIEF],STATE_PANIC)
        CheckStatLT([PC.0.0.MAGE_THIEF],100,RESISTFIRE)
	!StateCheck([PC.0.0.MAGE_THIEF],STATE_MIRRORIMAGE)
	See([PC.0.0.MAGE_THIEF]) // these blocks are optimized for targeting low AC characters such as single-class mages and mage/thieves
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT(WorstAC([PC]),0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle of higher
	!StateCheck(WorstAC([PC]),STATE_PANIC)
        CheckStatLT(WorstAC([PC]),100,RESISTFIRE)
	!StateCheck(WorstAC([PC]),STATE_MIRRORIMAGE)
	See(WorstAC([PC])) // these blocks are optimized for targeting low AC characters such as single-class mages and mage/thieves
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	AttackedBy([GOODCUTOFF],DEFAULT)
	!CheckStatGT(LastAttackerOf(Myself),0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle of higher
        !StateCheck(LastAttackerOf(Myself),STATE_PANIC)
        CheckStatLT(LastAttackerOf(Myself),100,RESISTFIRE)
	Range(LastAttackerOf(Myself),8)
	RandomNum(3,1)
	See(LastAttackerOf(Myself))
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	AttackedBy([GOODCUTOFF],DEFAULT)
	!CheckStatGT(LastAttackerOf(Myself),0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle of higher
        !StateCheck(LastAttackerOf(Myself),STATE_PANIC)
        CheckStatLT(LastAttackerOf(Myself),100,RESISTFIRE)
	Range(LastAttackerOf(Myself),4)
	See(LastAttackerOf(Myself))
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT(SixthNearest([GOODCUTOFF]),0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle of higher
	StateCheck(SixthNearest([GOODCUTOFF]),STATE_IMMOBILE)
        CheckStatLT(SixthNearest([GOODCUTOFF]),100,RESISTFIRE)
	See(SixthNearest([GOODCUTOFF]))
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT(FifthNearest([GOODCUTOFF]),0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle of higher
	StateCheck(FifthNearest([GOODCUTOFF]),STATE_IMMOBILE)
        CheckStatLT(FifthNearest([GOODCUTOFF]),100,RESISTFIRE)
	See(FifthNearest([GOODCUTOFF]))
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT(FourthNearest([GOODCUTOFF]),0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle of higher
	StateCheck(FourthNearest([GOODCUTOFF]),STATE_IMMOBILE)
        CheckStatLT(FourthNearest([GOODCUTOFF]),100,RESISTFIRE)
	See(FourthNearest([GOODCUTOFF]))
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT(ThirdNearest([GOODCUTOFF]),0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle of higher
	StateCheck(ThirdNearest([GOODCUTOFF]),STATE_IMMOBILE)
        CheckStatLT(ThirdNearest([GOODCUTOFF]),100,RESISTFIRE)
	See(ThirdNearest([GOODCUTOFF]))
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT(SecondNearest([GOODCUTOFF]),0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle of higher
	StateCheck(SecondNearest([GOODCUTOFF]),STATE_IMMOBILE)
        CheckStatLT(SecondNearest([GOODCUTOFF]),100,RESISTFIRE)
	See(SecondNearest([GOODCUTOFF]))
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT([GOODCUTOFF],0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle of higher
	StateCheck([GOODCUTOFF],STATE_IMMOBILE)
        CheckStatLT([GOODCUTOFF],100,RESISTFIRE)
	See([GOODCUTOFF])
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT(Player6,0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle of higher
	StateCheck(Player6,STATE_IMMOBILE)
        CheckStatLT(Player6,100,RESISTFIRE)
	See(Player6)
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT(Player5,0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle of higher
	StateCheck(Player5,STATE_IMMOBILE)
        CheckStatLT(Player5,100,RESISTFIRE)
	See(Player5)
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT(Player4,0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle of higher
	StateCheck(Player4,STATE_IMMOBILE)
        CheckStatLT(Player4,100,RESISTFIRE)
	See(Player4)
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT(Player3,0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle of higher
	StateCheck(Player3,STATE_IMMOBILE)
        CheckStatLT(Player3,100,RESISTFIRE)
	See(Player3)
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT(Player2,0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle of higher
	StateCheck(Player2,STATE_IMMOBILE)
        CheckStatLT(Player2,100,RESISTFIRE)
	See(Player2)
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT(Player1,0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle of higher
	StateCheck(Player1,STATE_IMMOBILE)
        CheckStatLT(Player1,100,RESISTFIRE)
	See(Player1)
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!StateCheck(LastSeenBy(Myself),STATE_REALLY_DEAD)
        !StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)
        OR(2)
	  !StateCheck(Myself,STATE_BLIND) // don't attack when blinded
	  Range(LastSeenBy(Myself),2) // unless the target is extremely close by
	!CheckStatGT(LastSeenBy(Myself),0,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Mantle or higher
        CheckStatLT(LastSeenBy(Myself),100,RESISTFIRE)
THEN
	RESPONSE #100
		AttackReevaluate(LastSeenBy(Myself),30)
END

IF
	!GlobalTimerNotExpired("RR#Move","LOCALS") // help -> move timer
	Help([EVILCUTOFF.0.MEPHIT])
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Move","LOCALS",6)
		MoveToObject(LastHelp(Myself))
END

IF
	!GlobalTimerNotExpired("RR#Move","LOCALS")
THEN
	RESPONSE #100
		RandomWalkContinuous()
END