
COMPILE ~atweaks/dlg/evil_rewards.d~ USING ~atweaks/tra/english/evil_rewards.tra~ ~atweaks/tra/%LANGUAGE%/evil_rewards.tra~

//Trademeet
COPY_EXISTING cefald01.cre ~override/fl#fald.cre~ //todo: check what SCSII does here (wrt. scripts) and accommodate
  WRITE_ASCII 0x248 shoutdlg #8
  WRITE_ASCII 0x250 trevil #8
  WRITE_ASCII 0x258 cefald03 #8
  WRITE_ASCII 0x2cc cefald04 #8
          
COPY_EXISTING ar1901.bcs override
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY EXACT_MATCH ~CreateCreatureObject("CEFALD04",Myself,0,0,0)~ ~CreateCreatureObject("fl#fald",Myself,0,0,0)~
  END
BUT_ONLY


//Ust Natha
COPY_EXISTING udimp.cre ~override/fl#udimp.cre~
  WRITE_ASCIIL 0x248 ~~ ~~ ~~ ~~ ~~ 
  WRITE_ASCII  0x280 fl#udimp #20
  WRITE_ASCII  0x2cc fl#udimp #8

EXTEND_BOTTOM udsoltri.bcs "atweaks/baf/fake_eggs.baf"
//todo: erase the journal entries when DrowCityHostile or when you complete the quest, in either manner


//Saradush
ACTION_IF GAME_IS tob BEGIN
  COPY_EXISTING xplist.2da override //Change quest XP reward for Kiser's quest to 7000
    REPLACE_TEXTUALLY CASE_INSENSITIVE ~\(PLOT8B[%TAB% ]+Saved_Ardic_Bad_Sarson01\)[%TAB% ]+5000~
                                       ~\1 7000~
  BUT_ONLY

  COPY_EXISTING sarvie01.cre override
    WRITE_LONG 0x14 9000 //make the Viekang pi√±ata hold 9k xp instead of 6.3k
    WRITE_ASCII 0x248 ~~ #8 //Vampire courtesans use their own shout code, so why should Viekang shout the same as the commoners?
  BUT_ONLY
END
