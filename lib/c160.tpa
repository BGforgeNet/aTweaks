
INCLUDE "atweaks/lib/dsandstuff.tpa"

COPY "atweaks/itm/fl#dksw1.itm" override
     "atweaks/itm/fl#dksw2.itm" override
     "atweaks/itm/fl#dksw3.itm" override
     "atweaks/itm/fl#dksw4.itm" override
     "atweaks/itm/fl#dksw5.itm" override
     "atweaks/itm/fl#dksw6.itm" override
     "atweaks/itm/fl#dk5a.itm"  override
     "atweaks/cre/fl#dksw5.cre" override


// Death Knights

ACTION_FOR_EACH file IN
               deathk                                                              //Death Knight from Durlag's Tower
               _deathk                                                             //Tutu
               deathkni                                                            //Plain Death Knight used in ToB
               deck615                                                             //DoMT hitman
               uddeath                                                             //Undeadark Death Knights
               uddeath2                                                            //Underdark Death Knight leader
               gooddeat                                                            //Durlag's Tower DK mirror fiend
               _ooddeat                                                            //Tutu
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME "%file%.cre" BEGIN
    COPY_EXISTING "%file%.cre" override
      PATCH_INCLUDE "atweaks/lib/fj_cre_validity.tpp"
      PATCH_IF valid BEGIN
        PATCH_INCLUDE "atweaks/lib/fj_cre_reindex.tpp"
        PATCH_IF "%file%" STRING_MATCHES_REGEXP "_?g?ooddeat$" BEGIN
          SAY 0x8 @1020                                                            //Death Knight
          SAY 0xc @1020
        END
        REPLACE_CRE_ITEM helmnoan #0 #0 #0 none helmet                             //Helmet
        REPLACE_CRE_ITEM ring95 #0 #0 #0 none lring                                //Standard undead immunities
        REPLACE_CRE_ITEM sw2h01 #0 #0 #0 none weapon2 EQUIP TWOHANDED
        LPF ADD_CRE_EFFECT INT_VAR
                          opcode = 297                                             //Immunity to Turn Undead
                          target = 1 
                          timing = 9
                          paramater2 = 1 END
        LPF DELETE_CRE_EFFECT INT_VAR opcode_to_delete = 193 END                   //Invisibility detection by script
        LPF DELETE_CRE_ITEM STR_VAR item_to_delete = "sw2h02\|sw2hdeat\|_?sw2h05" END
        LPF RR#MDCRE
          INT_VAR
            newxpv = 10000
            newhp = 90 //9d10
            newac = 0
            newth = 8
            newapr = 1
            newsvd = 6
            newsvw = 8
            newsvp = 7
            newsvb = 7
            newsvs = 9
            newmr = 75
            newlvl1 = 9 //Fighter
            newlvl2 = 20 //Mage
            newlvl3 = 8 //Cleric
            newfr = 0
            newcr = 0
            newer = 0
            newar = 0
            newrs = 0
            newrc = 0
            newrp = 0
            newrm = 0
            newstr = 18
            newstrx = 100
            newint = 18
            newwis = 16
            newdex = 18
            newcon = 11
            newchr = 10
            newmor = 17
            newgen = 4 //Undead
            newrace = 115 //Skeleton
            newclass = 17 //F_M_C
            newalign = 51
          STR_VAR
            enfc01 = deathk
            enfc02 = _deathk
            enfc03 = deathkni
            enfc04 = deck615
            enfc05 = uddeath
            enfc06 = uddeath2
            enfc07 = gooddeat
            enfc08 = _ooddeat
        END
        PATCH_IF "%file%" STRING_MATCHES_REGEXP "_?g?ooddeat$" BEGIN
          PATCH_IF "%file%" STRING_MATCHES_REGEXP "_?deathk$" = 0 BEGIN
            SPRINT script "fl#bg1kn"
          END ELSE BEGIN
            SPRINT script "fl#death"
          END
          LPF RR#MDCSC
            STR_VAR
              newscript = EVAL "%script%"
              script01 = deathkni
              script02 = wtasight
              script03 = _eathkni
              script04 = _tasight
              script05 = uddeath
              script06 = bpwtsigt
              script07 = bpmag18a
              script08 = bpdemon
          END
        END
      END
    BUT_ONLY
  END
END


//Mummies

ACTION_FOR_EACH file IN
               mummy
               mummy01
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME "%file%.cre" BEGIN
    COPY_EXISTING "%file%.cre" override
      PATCH_INCLUDE "atweaks/lib/fj_cre_validity.tpp"
      PATCH_IF valid BEGIN
        PATCH_INCLUDE "atweaks/lib/fj_cre_reindex.tpp"
        REPLACE_CRE_ITEM fl#mum #0 #0 #0 none weapon1 EQUIP
        LPF RR#MDCRE
          INT_VAR
            newxpv = 3000
            newhp = 51 //6d8+3
            newac = 3
            newth = 13
            newapr = 1
            newsvd = 10
            newsvw = 12
            newsvp = 11
            newsvb = 12
            newsvs = 13
            newmr = 0
            newlvl1 = 6
            newfr = "-30"
            newcr = 100
            newer = 0
            newar = 0
            newrs = 50
            newrc = 50
            newrp = 50
            newrm = 50
            newstr = 18
            newstrx = 99
            newint = 7
            newwis = 7
            newdex = 9
            newcon = 14
            newchr = 7
            newalign = 19 //LE 
        END
        LPF RR#MDCSC
          STR_VAR
            newscript = fl#mum
            script01 = wtasight
            script02 = dw#melee //what to do? I don't want to override SCSII just to add some aura
        END
      END
    BUT_ONLY
  END
END

/*
// Greater Mummies

ACTION_FOR_EACH file IN
               mumgre01
               riftcr03
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME "%file%.cre" BEGIN
    COPY_EXISTING "%file%.cre" override
      PATCH_INCLUDE "atweaks/lib/fj_cre_validity.tpp"
      PATCH_IF valid BEGIN
        PATCH_INCLUDE "atweaks/lib/fj_cre_reindex.tpp"
        LPF RR#MDCRE
          INT_VAR
            newxpv = 8000
            newhp = 67 //8d8+3
            newac = 2
            newth = 11
            newapr = 1
            newsvd = 7
            newsvw = 11
            newsvp = 10
            newsvb = 13
            newsvs = 12
            newmr = 15
            newlvl1 = 19
            newfr = 0
            newcr = 100
            newer = "-50"
            newar = 0
            newrs = 50
            newrc = 50
            newrp = 50
            newrm = 50
            newstr = 18
            newstrx = 99
            newint = 7
            newwis = 7
            newdex = 9
            newcon = 14
            newchr = 7
            newalign = 19 //LE 
        END
*/

ACTION_IF GAME_IS ~tutu tutu_totsc~ BEGIN
  OUTER_SPRINT DurlagArea fw0516
  OUTER_SPRINT TutuJournal
  "
    EraseJournalEntry(84201)
    EraseJournalEntry(84195)
    EraseJournalEntry(83499)
    EraseJournalEntry(83502)
    EraseJournalEntry(83506)
    EraseJournalEntry(83508)
    EraseJournalEntry(83510)
    EraseJournalEntry(83512)
    EraseJournalEntry(83522)
    EraseJournalEntry(83710)
    EraseJournalEntry(83561)
    EraseJournalEntry(83738)
    EraseJournalEntry(83894)
    EraseJournalEntry(83899)
    EraseJournalEntry(83905)
    EraseJournalEntry(83989)
    EraseJournalEntry(83993)
    EraseJournalEntry(83995)
"
  OUTER_SPRINT GGhoul _houllor
  OUTER_SPRINT SkeletC _kelet_c
  OUTER_SPRINT killgood _illgood
  OUTER_SPRINT gooddeat _ooddeat
END ELSE ACTION_IF GAME_IS bgt BEGIN
  OUTER_SPRINT DurlagArea ard016
  OUTER_SPRINT TutuJournal ""
  OUTER_SPRINT GGhoul ghoullor
  OUTER_SPRINT SkeletC skelet_c
  OUTER_SPRINT killgood killgood
  OUTER_SPRINT gooddeat gooddeat
END

ACTION_IF BG1 BEGIN
  COPY - "atweaks/baf/killgood.baf" "...atweaks/fl-inlined/%killgood%.baf"
         "atweaks/baf/gooddeat.baf" "...atweaks/fl-inlined/%gooddeat%.baf"
  COMPILE ~atweaks/baf/fl#bg1kn.baf~ "...atweaks/fl-inlined/%killgood%.baf" "...atweaks/fl-inlined/%gooddeat%.baf" EVAL
END ELSE BEGIN
  COMPILE ~atweaks/baf/fl#death.baf~
END

COPY "atweaks/spl/fl#mumfa.spl" override                                           // Mummy Fear Aura (area effect)
  PATCH_FOR_EACH race IN 153 2 3 4 5 6 7 BEGIN                                     // non-humans
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 2 parameter1 = race parameter2 = 4 savingthrow = 1 STR_VAR resource = fl#mumfa END
  END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 2 parameter1 = 1 parameter2 = 4 savingthrow = 1 savebonus = 2 STR_VAR resource = fl#mumfa END

COPY ~atweaks/spl/fldkaura.spl~ override                                           // Death Knight Fear Aura
     "atweaks/spl/fl#dksw.spl" override                                            // Death Knight sword-making spell
     "atweaks/spl/fl#mumfe.spl" override                                           // Mummy Fear Aura (fear itself)
     "atweaks/eff/fl#mumfa.eff" override                                           // Mummy Fear Aura (cast fl#mumfe)

ADD_PROJECTILE ~atweaks/pro/fldkicew.pro~                                          //Single-round Ice Storm (cue imagination stretch)
COPY ~atweaks/spl/fldkicew.spl~ override                                           //Death Knight Ice Wall (sheet of falling ice)
  SAY 0x8 @1024
  READ_LONG  0x64 ab_off
  READ_SHORT 0x68 ab_num
  FOR(i=0; i<ab_num; i+=1) BEGIN
    WRITE_SHORT (ab_off+i*0x28+0x26) fldkicew
  END
