
INCLUDE "atweaks/lib/dsandstuff.tpa"
INCLUDE "atweaks/lib/undead.tpa"

LAF ret_from_eval STR_VAR f_Entry = fl#Disease RET DiseaseSecType = f_Value END
LAF ret_from_eval STR_VAR f_Entry = fl#Fear RET FearSecType = f_Value END
LAF ret_from_eval STR_VAR f_Entry = fl#sight RET fl#sight = f_Value END


//Banshee

ACTION_FOR_EACH file IN
                banshe01 //ToB Banshee
                firmon01 //Unused critter from Firkraag's
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME "%file%.cre" BEGIN
    COPY_EXISTING "%file%.cre" override
      PATCH_INCLUDE "atweaks/lib/fj_cre_validity.tpp"
      PATCH_IF valid BEGIN
        PATCH_INCLUDE "atweaks/lib/fj_cre_reindex.tpp"
        REPLACE_CRE_ITEM b1-8m2 #0 #0 #0 none weapon1 EQUIP
        REPLACE_CRE_ITEM immune1 #0 #0 #0 none lring
        REPLACE_CRE_ITEM ring95 #0 #0 #0 none rring
        REMOVE_CRE_ITEM immchs
        LPF RR#MDCRE
          INT_VAR
            newxpv = 4000
            newhp = 56 //7d8
            newac = 0
            newth = 13
            newapr = 1
            newsvd = 9
            newsvw = 11
            newsvp = 10
            newsvb = 12
            newsvs = 12
            newlvl1 = 17 //To approximate their "turned as special undead" from PnP
            newlvl2 = 0
            newlvl3 = 0
            newmr = 50
            newfr = 0
            newcr = 100
            newer = 100
            newar = 0
            newrs = 0
            newrc = 0
            newrp = 0
            newrm = 0
            newstr = 16
            newstrx = 0
            newint = 16
            newwis = 16
            newdex = 15
            newcon = 9
            newchr = 9
            newmor = 13
            newalign = 51 //CE
            
            enforce = 1
        END
        LPF RR#MDCSC
          STR_VAR
            newscript = fl#bansh
            script01 = banshe01
            script02 = wtasight
        END
        LPF cold_spell_immunities END
        LPF electrical_spell_immunities END
      END
    BUT_ONLY
  END
END

COPY "atweaks/spl/undead/fl#banwa.spl" override //Banshee's wail
  FOR(i=0;i<SHORT_AT 0x68;++i) BEGIN
    WRITE_SHORT LONG_AT 0x64+i*0x28+0x26 fl#sight
  END

COPY "atweaks/spl/base.spl" "override/fl#banfa.spl" //Fear aura
  WRITE_BYTE 0x27 11 //disabling
  READ_LONG 0x64 ao
  FOR(i=0;i<SHORT_AT 0x68;++i) BEGIN
    WRITE_BYTE  ao+i*0x28+0xc 4 //any point
    WRITE_SHORT ao+i*0x28+0x26 fl#sight
  END
  DEFINE_ARRAY banfa_opcode BEGIN 24 139 142 END
  DEFINE_ARRAY banfa_p1 BEGIN 0 14007 0 END
  DEFINE_ARRAY banfa_p2 BEGIN 0 0 36 END
  DEFINE_ARRAY banfa_timing BEGIN 0 1 0 END
  DEFINE_ARRAY banfa_duration BEGIN 60 0 60 END
  PHP_EACH banfa_opcode AS int => opcode BEGIN
    LPF ADD_SPELL_EFFECT
      INT_VAR
        opcode
        target = 2
        parameter1 = $banfa_p1("%int%")
        parameter2 = $banfa_p2("%int%")
        timing = $banfa_timing("%int%")
        duration = $banfa_duration("%int%")
        savingthrow = BIT0 //spell
    END
  END


// Death Knights
ACTION_FOR_EACH file IN
               deathk //Death Knight from Durlag's Tower
               _deathk
               deathkni //Plain Death Knight used in ToB
               deck615 //DoMT hitman
               uddeath //Undeadark Death Knights
               uddeath2 //Underdark Death Knight leader
               gooddeat //Durlag's Tower DK mirror fiend
               _ooddeat
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME "%file%.cre" BEGIN
    COPY_EXISTING "%file%.cre" override
      PATCH_INCLUDE "atweaks/lib/fj_cre_validity.tpp"
      PATCH_IF valid BEGIN
        PATCH_INCLUDE "atweaks/lib/fj_cre_reindex.tpp"
        PATCH_IF "%file%" STRING_MATCHES_REGEXP "_?g?ooddeat$" BEGIN
          SAY 0x8 @1020 //Death Knight
          SAY 0xc @1020
        END
        REPLACE_CRE_ITEM helmnoan #0 #0 #0 none helmet //Helmet
        REPLACE_CRE_ITEM ring95 #0 #0 #0 none lring //Standard undead immunities
        LPM unturnable
        LPF DELETE_CRE_EFFECT INT_VAR opcode_to_delete = 193 END //Invisibility detection by script
        LPF DELETE_CRE_ITEM STR_VAR item_to_delete = "sw2h02\|sw2hdeat\|_?sw2h05\|ringdemn" END
        LPF RR#MDCRE
          INT_VAR
            newxpv = 10000
            newhp = 90 //9d10
            newac = 0
            newth = 8
            newapr = 1
            newsvd = 6
            newsvw = 8
            newsvp = 7
            newsvb = 7
            newsvs = 9
            newmr = 75
            newlvl1 = 9 //Fighter
            newlvl2 = 20 //Mage
            newlvl3 = 8 //Cleric
            newfr = 0
            newcr = 0
            newer = 0
            newar = 0
            newrs = 0
            newrc = 0
            newrp = 0
            newrm = 0
            newstr = 18
            newstrx = 100
            newint = 18
            newwis = 16
            newdex = 18
            newcon = 11
            newchr = 10
            newmor = 17
            newgen = 4 //Undead
            newrace = 115 //Skeleton
            newclass = 17 //F_M_C
            newalign = 51

            enforce = 1
        END
        PATCH_IF "%file%" STRING_MATCHES_REGEXP "_?g?ooddeat$" BEGIN
          PATCH_IF "%file%" STRING_MATCHES_REGEXP "_?deathk$" = 0 BEGIN
            SPRINT script "fl#bg1kn"
          END ELSE BEGIN
            SPRINT script "fl#death"
          END
          LPF RR#MDCSC
            STR_VAR
              newscript = EVAL "%script%"
              script01 = deathkni
              script02 = wtasight
              script03 = _eathkni
              script04 = _tasight
              script05 = uddeath
              script06 = bpwtsigt
              script07 = bpmag18a
              script08 = bpdemon
          END
        END
        LPF add_skeletal_traits END
      END
    BUT_ONLY
  END
END

COPY_EXISTING misc56.itm "override/fl#brsw.itm" //Broken sword
  SAY 0x8 @1031
  SAY 0xc @1031
  SAY 0x50 @1032
  SAY 0x54 @1032

COPY ~atweaks/spl/undead/fl#dkfa.spl~ override //Death Knight Fear Aura
     "atweaks/itm/undead/fl#dksw3.itm" override //Two-handed sword +4

COPY_EXISTING sw1h02.itm "override/fl#dksw4.itm" //Bastard sword +2, +1 APR
              sw1h02.itm "override/fl#dksw5.itm" //Bastard sword +2, of dancing
              sw1h02.itm "override/fl#dksw6.itm" //Bastard sword +2, of life stealing
  WRITE_LONG 0x60 2 //enchantment
  LPF configure_header
    INT_VAR
      f_ToHit = 2
      f_Damage = 2
  END
  PATCH_MATCH "%DEST_RES%"
  WITH
    fl#dksw4 BEGIN
      LPF ADD_ITEM_EQEFFECT
        INT_VAR
          opcode = 1 //APR mod
          target = 1
          parameter1 = 1
          timing = 2
      END
    END
    fl#dksw5 BEGIN
      LPF fill_headers
        INT_VAR
          f_FillLimit = 2
      END
      LPF configure_header
        INT_VAR
          f_Header = 1
          f_Type = 3
          f_Identify = 1
          f_Slot = 3
          f_Target = 5
          f_Speed = 0
          f_Drained = 3
          f_Flags = BIT11
      END
      LPF ADD_ITEM_EFFECT
        INT_VAR
          type = 3
          opcode = 112 //Remove item
          target = 1
          timing = 1
        STR_VAR
          resource = fl#dksw5
      END
      LPF ADD_ITEM_EFFECT
        INT_VAR
          type = 3
          opcode = 122 //Create item
          target = 1
          timing = 4
          duration = 24
        STR_VAR
          resource = fl#dksw5
      END
      LPF ADD_ITEM_EFFECT
        INT_VAR
          type = 3
          opcode = 67 //Summon creature
          target = 1
          duration = 24
        STR_VAR
          resource = fl#dksw5
      END
      LPF ADD_ITEM_EFFECT
        INT_VAR
          type = 3
          opcode = 215 //Play visual
          target = 1
          timing = 1
        STR_VAR
          resource = spcrtwpn
      END
    END
    fl#dksw6 BEGIN
      LPF ADD_ITEM_EFFECT
        INT_VAR
          type = 1
          opcode = 216 //Level drain
          target = 2
          parameter1 = 1
          timing = 1
      END
    END
    DEFAULT
  END

COPY_EXISTING sword02.cre "override/fl#dksw5.cre" //Dancing sword
  SAY 0x8 @1030
  SAY 0xc @1030
  REPLACE_CRE_ITEM fl#dksw5 #0 #0 #0 "unstealable&undroppable" weapon1 EQUIP
  WRITE_ASCII SCRIPT_OVERRIDE fl#dksw5 #8

COPY "atweaks/spl/base.spl" "override/fl#dkeds.spl"
  LPF ADD_SPELL_EFFECT
    INT_VAR
      opcode = 143 //Create item in slot
      target = 1
      parameter1 = 35 //WEAPON0
      timing = 1
    STR_VAR
      resource = fl#dksw5
  END
  LPF ADD_SPELL_EFFECT
    INT_VAR
      opcode = 123 //Remove inventory item
      target = 1
      timing = 1
    STR_VAR
      resource = fl#dksw5
  END
 
OUTER_SPRINT $dksw(1) sw1h42
OUTER_SPRINT $dksw(2) sw2h20
OUTER_SPRINT $dksw(3) fl#dksw3
OUTER_SPRINT $dksw(4) fl#dksw4
OUTER_SPRINT $dksw(5) fl#dksw5
OUTER_SPRINT $dksw(6) fl#dksw6


OUTER_SET r = RANDOM(1 6)
OUTER_SET c = RANDOM(1 100)
ACTION_FOR_EACH file IN
                deathk
                gooddeat
                _deathk
                _ooddeat
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME "%file%.cre" BEGIN
    COPY_EXISTING "%file%.cre" override
      PATCH_INCLUDE "atweaks/lib/fj_cre_validity.tpp"
      PATCH_IF valid BEGIN
        PATCH_IF c <= 80 BEGIN
          SPRINT sword $dksw("%r%")
          PATCH_IF r = 1 BEGIN
            ADD_CRE_ITEM "%sword%" #0 #0 #0 unstealable weapon1 EQUIP
          END ELSE PATCH_IF r = 2 BEGIN
            ADD_CRE_ITEM "%sword%" #0 #0 #0 unstealable weapon1 EQUIP TWOHANDED
          END ELSE PATCH_IF r = 3 BEGIN
            ADD_CRE_ITEM "%sword%" #0 #0 #0 "unstealable&undroppable" weapon1 EQUIP TWOHANDED
            ADD_CRE_ITEM fl#brsw #0 #0 #0 unstealable inv1
          END ELSE PATCH_IF r = 4 OR r = 6 BEGIN
            ADD_CRE_ITEM "%sword%" #0 #0 #0 "unstealable&undroppable" weapon1 EQUIP
            ADD_CRE_ITEM fl#brsw #0 #0 #0 unstealable inv1
          END ELSE BEGIN
            ADD_CRE_ITEM fl#dksw5 #0 #1 #0 "unstealable&undroppable" weapon1 EQUIP
            ADD_CRE_ITEM sw2h01 #0 #0 #0 unstealable weapon2 TWOHANDED
            ADD_CRE_ITEM fl#brsw #0 #0 #0 unstealable inv1
          END
        END ELSE BEGIN
          REPLACE_CRE_ITEM sw2h01 #0 #0 #0 unstealable weapon1 EQUIP TWOHANDED
        END
      END
    BUT_ONLY
  END
END

ACTION_IF !BG1 OR BGT BEGIN
  COPY_EXISTING uddeath2.cre override
                deck615.cre  override
                uddeath.cre  "override/fl#udkn1.cre"
                uddeath.cre  "override/fl#udkn2.cre"
                uddeath.cre  "override/fl#udkn3.cre"
                deathkni.cre "override/fl#wkkn1.cre"
                deathkni.cre "override/fl#wkkn2.cre"
                deathkni.cre "override/fl#wkkn3.cre"
		uddeath.cre  override
		deathkni.cre override
    PATCH_INCLUDE "atweaks/lib/fj_cre_validity.tpp"
    PATCH_IF valid BEGIN
      r = RANDOM(1 6)
      SPRINT sword $dksw("%r%")
      PATCH_IF r = 1 BEGIN
        REPLACE_CRE_ITEM "%sword%" #0 #0 #0 unstealable weapon1 EQUIP
      END ELSE PATCH_IF r = 2 BEGIN
        REPLACE_CRE_ITEM "%sword%" #0 #0 #0 unstealable weapon1 EQUIP TWOHANDED
      END ELSE PATCH_IF r = 3 BEGIN
        REPLACE_CRE_ITEM "%sword%" #0 #0 #0 "unstealable&undroppable" weapon1 EQUIP TWOHANDED
        ADD_CRE_ITEM fl#brsw #0 #0 #0 unstealable inv1
      END ELSE PATCH_IF r = 4 OR r = 6 BEGIN
        REPLACE_CRE_ITEM "%sword%" #0 #0 #0 "unstealable&undroppable" weapon1 EQUIP
        ADD_CRE_ITEM fl#brsw #0 #0 #0 unstealable inv1
      END ELSE BEGIN
        REPLACE_CRE_ITEM fl#dksw5 #0 #1 #0 "unstealable&undroppable" weapon1 EQUIP
        ADD_CRE_ITEM sw2h01 #0 #0 #0 unstealable weapon2 TWOHANDED
        ADD_CRE_ITEM fl#brsw #0 #0 #0 unstealable inv1
      END
    END
  BUT_ONLY
  
  COPY_EXISTING udsac.bcs override
    DECOMPILE_BCS_TO_BAF
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~("uddeath",[2929.1562],12)~ ~("fl#udkn1",[2929.1562],12)~
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~("uddeath",[2724.1143],12)~ ~("fl#udkn2",[2724.1143],12)~
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~("uddeath",[3280.1348],12)~ ~("fl#udkn3",[3280.1348],12)~
    COMPILE_BAF_TO_BCS
  BUT_ONLY
  
  COPY_EXISTING ar3007.bcs override
    DECOMPILE_BCS_TO_BAF
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~("deathkni",[550.390],0)~ ~("fl#wkkn1",[550.390],0)~
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~("deathkni",[383.505],12)~ ~("fl#wkkn2",[383.505],12)~
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~("deathkni",[574.683],10)~ ~("fl#wkkn3",[574.683],10)~
    COMPILE_BAF_TO_BCS
  BUT_ONLY
END



//Ghasts
ACTION_FOR_EACH file IN
		ghast //BG1 standard Ghast
		ghastd //Durlag's Tower Ghast Trap Ghast
		_ghast
		_ghastd
		ghasts //Tiax' summoned ghast
		_ghasts
		ghast01 //BG2 standard Ghast
		nevm3 //Nev's undead trap Ghast
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME "%file%.cre" BEGIN
    COPY_EXISTING "%file%.cre" override
      PATCH_INCLUDE "atweaks/lib/fj_cre_validity.tpp"
      PATCH_IF valid BEGIN
        PATCH_INCLUDE "atweaks/lib/fj_cre_reindex.tpp"
        REPLACE_CRE_ITEM fl#ghas1 #0 #0 #0 none weapon1 EQUIP
        REPLACE_CRE_ITEM fl#ghas2 #0 #0 #0 none shield
        LPF RR#MDCRE
          INT_VAR
            newxpv = 650
            newhp = 32 //4d8
            newac = 4
            newth = 17
            newapr = 2 //2 APR with main hand, plus teeth in the off hand
            newsvd = 9
            newsvw = 11
            newsvp = 10
            newsvb = 12
            newsvs = 12
            newlvl1 = 4
            newlvl2 = 0
            newlvl3 = 0
            newmr = 0
            newfr = 0
            newcr = 0
            newer = 0
            newar = 0
            newrs = 0
            newrc = 0
            newrp = 0
            newrm = 0
            newstr = 18
            newdex = 9
            newcon = 9
            newint = 12
            newwis = 9
            newchr = 9
            newmor = 14
            newclass = 118
            newalign = 51

            enforce = 1
          STR_VAR
        END
        LPF RR#MDCSC
          STR_VAR
            newscript = fl#ghast
            script01 = ghast
            script02 = wtasight
            script03 = ghastd
            script04 = movep1
            script05 = _ghast
            script06 = _tasight
            script07 = _ghastd
            script08 = _movep1
            script09 = dw#melee
            skipfile02 = ghasts
            skipfile03 = _ghasts
        END
      END
    BUT_ONLY
  END
END

COPY //"atweaks/itm/undead/fl#ghast.itm" override
     "atweaks/spl/undead/fl#ghaau.spl" override

COPY_EXISTING s1-4.itm "override/fl#ghas1.itm"
              s1-8.itm "override/fl#ghas2.itm"
  WRITE_LONG 0x60 1 //enchantment
  DEFINE_ARRAY ghast_opcode BEGIN 175 141 141 50 174 174 END
  DEFINE_ARRAY ghast_p1 BEGIN 0 0 0 0x455700 0 0 END
  DEFINE_ARRAY ghast_p2 BEGIN 2 1 33 0x190000 0 0 END
  DEFINE_ARRAY ghast_timing BEGIN 0 1 1 0 1 4 END
  DEFINE_ARRAY ghast_duration BEGIN 42 0 0 2 0 42 END
  DEFINE_ARRAY ghast_resref BEGIN "" "" "" "" eff_p11 eff_e05 END
  PHP_EACH ghast_opcode AS int => opcode BEGIN
    LPF ADD_ITEM_EFFECT
      INT_VAR
        type = 1
        opcode
        target = 2
        parameter1 = $ghast_p1("%int%")
        parameter2 = $ghast_p2("%int%")
        timing = $ghast_timing("%int%")
        duration = $ghast_duration("%int%")
        savingthrow = BIT2 //Paralysation
      STR_VAR
        resource = EVAL $ghast_resref("%int%")
    END
  END


// Ghouls and Lacedons
ACTION_FOR_EACH file IN
               ghoul //BG1 Ghoul
               korax //Korax the Ghoul
               _ghoul
               _korax
               lacedo01 //Lacedon
               sahlace //Lacedon
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME "%file%.cre" BEGIN
    COPY_EXISTING "%file%.cre" override
      PATCH_INCLUDE "atweaks/lib/fj_cre_validity.tpp"
      PATCH_IF valid BEGIN
        PATCH_INCLUDE "atweaks/lib/fj_cre_reindex.tpp"
        REPLACE_CRE_ITEM fl#ghou1 #0 #0 #0 none weapon1 EQUIP
        REPLACE_CRE_ITEM fl#ghou2 #0 #0 #0 none shield
        LPF RR#MDCRE
          INT_VAR
            newxpv = 175
            newhp = 16 //2d8
            newac = 6
            newth = 19
            newapr = 2 //2 with main, plus one chomp with the off hand
            newsvd = 12
            newsvw = 14
            newsvp = 13
            newsvb = 15
            newsvs = 13
            newlvl1 = 2
            newlvl2 = 0
            newlvl3 = 0
            newmr = 0
            newfr = 0
            newcr = 0
            newer = 0
            newar = 0
            newrs = 0
            newrc = 0
            newrp = 0
            newrm = 0
            newstr = 16
            newdex = 9
            newcon = 9
            newint = 7
            newwis = 7
            newchr = 9
            newmor = 12
            newalign = 51 //CE

            enforce = 1
        END
        LPF RR#MDCSC
          STR_VAR
            newscript = fl#ghoul
            script01 = wdasight
            script02 = _dasight
            script04 = _tasight
            script05 = wtasight
            skipfile01 = korax
            skipfile02 = _korax
        END
      END
    BUT_ONLY
  END
END

COPY_EXISTING s1-3.itm "override/fl#ghou1.itm"
              s1-6.itm "override/fl#ghou2.itm"
  LPF ADD_ITEM_EFFECT INT_VAR type = 1 opcode = 177 target = 2 duration = 1 parameter1 = 2 parameter2 = 4 STR_VAR resource = fl#ghoul END //Protection from fl#ghoul.spl for elves
  LPF ADD_ITEM_EFFECT INT_VAR type = 1 opcode = 146 target = 2 timing = 1 parameter1 = 1 parameter2 = 1 STR_VAR resource = fl#ghoul END //Cast fl#ghoul.spl

COPY "atweaks/eff/base.eff" "override/fl#ghoul.eff"
  WRITE_LONG 0x10 206
  WRITE_LONG 0x14 2
  WRITE_LONG 0x28 1
  WRITE_ASCII 0x30 fl#ghoul #8

COPY "atweaks/spl/undead/fl#ghoul.spl" override



// Ghoul lords
ACTION_FOR_EACH file IN
                ghogr01
                gholor01
                riftcr01
                ghoullor
                _houllor
                lacedo02 //Sahuagin Greater Lacedon
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME "%file%.cre" BEGIN
    COPY_EXISTING "%file%.cre" override
      PATCH_INCLUDE "atweaks/lib/fj_cre_validity.tpp"
      PATCH_IF valid BEGIN
        PATCH_INCLUDE "atweaks/lib/fj_cre_reindex.tpp"
        REPLACE_CRE_ITEM fl#glor1 #0 #0 #0 none weapon1 EQUIP
        REPLACE_CRE_ITEM fl#glor2 #0 #0 #0 none shield
        REMOVE_CRE_ITEM mage06 _mage06
        PATCH_IF "%SOURCE_RES%" STRING_COMPARE_CASE lacedo02 BEGIN
          SAY 0x8 @1033
          SAY 0xc @1033
        END
        LPF RR#MDCRE
          INT_VAR
            newxpv = 3000
            newhp = 48 //6d8
            newac = 4
            newth = 15
            newapr = 2 //2 with main, plus one with off-hand
            newsvd = 9
            newsvw = 11
            newsvp = 10
            newsvb = 12
            newsvs = 12
            newlvl1 = 9 //To approximate the PnP effort of turning them
            newlvl2 = 0
            newlvl3 = 0
            newmr = 0
            newfr = 0
            newcr = 0
            newer = 0
            newar = 0
            newrs = 0
            newrc = 0
            newrp = 0
            newrm = 0
            newstr = 18
            newstrx = 0
            newdex = 9
            newcon = 9
            newint = 14
            newwis = 9
            newchr =  9
            newmor = 14
            newalign = 51

            enforce = 1
        END
        LPF RR#MDCSC
          STR_VAR
            newscript = fl#glord
            script01 = _ghast
            script02 = _tasight
            script03 = ghast
            script04 = wtasight
            script05 = gholor01
            script06 = wdasight
            script07 = riftcr01
        END
      END
    BUT_ONLY
  END
END

COPY_EXISTING s1-6.itm "override/fl#glor1.itm"
              s1-10.itm "override/fl#glor2.itm"
  WRITE_LONG 0x60 1 //enchantment
  PATCH_IF "%DEST_RES%" STRING_EQUAL_CASE fl#glor1 BEGIN
    LPF ADD_ITEM_EQEFFECT
      INT_VAR
        opcode = 120 //weapon immunity
        target = 1
        parameter2 = 2 //non-magical
        timing = 2
    END
    LPF ADD_ITEM_EQEFFECT
      INT_VAR
        opcode = 126 //movement bonus
        target = 1
        parameter1 = 2 //double
        parameter2 = 2
        timing = 2
    END
  END
  DEFINE_ARRAY glor_opcode BEGIN 175 141 141 50 174 174 END
  DEFINE_ARRAY glor_p1 BEGIN 0 0 0 0x455700 0 0 END
  DEFINE_ARRAY glor_p2 BEGIN 2 1 33 0x190000 0 0 END
  DEFINE_ARRAY glor_timing BEGIN 0 1 1 0 1 4 END
  DEFINE_ARRAY glor_duration BEGIN 54 0 0 2 0 54 END
  DEFINE_ARRAY glor_resref BEGIN "" "" "" "" eff_p11 eff_e05 END
  PHP_EACH glor_opcode AS int => opcode BEGIN
    LPF ADD_ITEM_EFFECT
      INT_VAR
        type = 1
        opcode
        target = 2
        parameter1 = $glor_p1("%int%")
        parameter2 = $glor_p2("%int%")
        timing = $glor_timing("%int%")
        duration = $glor_duration("%int%")
        savingthrow = BIT2 //Paralysation
      STR_VAR
        resource = EVAL $glor_resref("%int%")
    END
  END
  PATCH_IF "%DEST_RES%" STRING_EQUAL_CASE fl#glor2 BEGIN
    LPF ADD_ITEM_EFFECT
      INT_VAR
        type = 1
        opcode = 146 //Cast spell
        target = 2
        parameter1 = 1
        parameter2 = 1
        timing = 1
        savingthrow = BIT2 //Poison
      STR_VAR
        resource = fl#glor2
    END
  END

COPY "atweaks/spl/base.spl" "override/fl#glor2.spl"
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 206 target = 2 timing = 1 STR_VAR resource = fl#glor2 END
  FOR (i=1;i<30;++i) BEGIN
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 12 target = 2 parameter1 = 5 parameter2 = 4194304 timing = 4 duration = i*300 END //5 points of damage
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 78 target = 2 parameter1 = 1 parameter2 = 6 timing = 4 duration = i*300 END //1 point of con
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 78 target = 2 parameter1 = 1 parameter2 = 9 timing = 4 duration = i*300 END //1 point of cha
  END

OUTER_SET $f_AddSecType(fl#glor2.spl) = DiseaseSecType

COPY "atweaks/spl/undead/fl#glord.spl" override
  FOR (i=0;i<SHORT_AT 0x68;++i) BEGIN
    WRITE_SHORT LONG_AT 0x64 + 0x28*i + 0x26 fl#sight
  END
  LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = "-1" END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 2 parameter1 = 1 parameter2 = 8 STR_VAR resource = fl#glord.eff END //Use EFF on good people

COPY "atweaks/eff/base.eff" "override/fl#glord.eff"
  WRITE_LONG  0x10 146 //cast spell
  WRITE_LONG  0x14 2
  WRITE_ASCII 0x30 fl#glor3

COPY "atweaks/spl/base.spl" "override/fl#glor3.spl"
  glor3_strref = RESOLVE_STR_REF(@1034)
  DEFINE_ARRAY glor3_opcode BEGIN 278 139 142 206 END
  DEFINE_ARRAY glor3_p1 BEGIN "-4" glor3_strref 0 0 END
  DEFINE_ARRAY glor3_p2 BEGIN 0 0 7 0 END
  DEFINE_ARRAY glor3_timing BEGIN 0 1 0 0 END
  DEFINE_ARRAY glor3_duration BEGIN 6 0 6 6 END
  DEFINE_ARRAY glor3_resref BEGIN "" "" "" fl#glor3 END
  PHP_EACH glor3_opcode AS int => opcode BEGIN
    LPF ADD_SPELL_EFFECT
      INT_VAR
        opcode
        target = 2
        parameter1 = $glor3_p1("%int%")
        parameter2 = $glor3_p2("%int%")
        timing = $glor3_timing("%int%")
        duration = $glor3_duration("%int%")
      STR_VAR
        resource = $glor3_resref("%int%")
    END
  END

OUTER_SET $f_AddSecType(fl#glord.spl) = FearSecType



// Mummies
ACTION_FOR_EACH file IN
		mummy
		mummy01
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME "%file%.cre" BEGIN
    COPY_EXISTING "%file%.cre" override
      PATCH_INCLUDE "atweaks/lib/fj_cre_validity.tpp"
      PATCH_IF valid BEGIN
        PATCH_INCLUDE "atweaks/lib/fj_cre_reindex.tpp"
        REPLACE_CRE_ITEM fl#mum #0 #0 #0 none weapon1 EQUIP
        LPF RR#MDCRE
          INT_VAR
            newxpv = 3000
            newhp = 51 //6d8+3
            newac = 3
            newth = 13
            newapr = 1
            newsvd = 10
            newsvw = 12
            newsvp = 11
            newsvb = 12
            newsvs = 13
            newmr = 0
            newlvl1 = 7
            newfr = "-33"
            newcr = 100
            newer = 0
            newar = 0
            newrs = 50
            newrc = 50
            newrp = 50
            newrm = 50
            newstr = 18
            newstrx = 99
            newint = 7
            newwis = 7
            newdex = 9
            newcon = 14
            newchr = 7
            newmor = 15
            newalign = 19 //LE

            enforce = 1
        END
        LPF RR#MDCSC
          STR_VAR
            newscript = fl#mum
            script01 = wtasight
            script02 = dw#melee
            //skipscript01 = dw#melee //do not assign a script if SCSII is installed
        END
        LPF cold_spell_immunities END
      END
    BUT_ONLY
  END
END

COPY "atweaks/itm/undead/fl#mum.itm" override
  WRITE_LONG 0x60 2 //enchantment
  //LPF ADD_ITEM_EFFECT INT_VAR type = 1 opcode = 272 target = 1 timing = 2 parameter1 = 6 parameter2 = 3 STR_VAR resource = fl#mumfa END
  //LPF ADD_ITEM_EFFECT INT_VAR type = 1 opcode = 232 target = 1 timing = 2 parameter1 = 0 parameter2 = 1 STR_VAR resource = fl#mumfa END //cast spell on self every round

  /*
COPY "atweaks/eff/base.eff" "override/fl#mumfa.eff"
  WRITE_LONG 0x10 146 //cast spell
  WRITE_LONG 0x14 2
  WRITE_LONG 0x1c 1
  WRITE_LONG 0x20 1
  WRITE_LONG 0x28 1
  WRITE_ASCII 0x30 fl#mumfa #8*/

COPY "atweaks/spl/undead/fl#mumfa.spl" override
  FOR (i=0;i<SHORT_AT 0x68;++i) BEGIN
    WRITE_SHORT LONG_AT 0x64 + 0x28*i + 0x26 fl#sight
  END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 146 target = 2 timing = 1 parameter1 = 1 parameter2 = 1 STR_VAR resource = fl#mumfn END //cast fl#mumfn
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 2 parameter1 = 1 parameter2 = 4 STR_VAR resource = fl#mumfh END //Use fl#mumfh.eff for humans
  //LPF ADD_SPELL_EFFECT INT_VAR opcode = 139 target = 1 timing = 1 parameter1 = 388 END

COPY "atweaks/eff/base.eff" "override/fl#mumfh.eff"
  WRITE_LONG 0x10 146 //cast spell
  WRITE_LONG 0x14 2
  WRITE_LONG 0x1c 1
  WRITE_LONG 0x20 1
  WRITE_LONG 0x28 1
  WRITE_ASCII 0x30 fl#mumfh #8

COPY "atweaks/eff/base.eff" "override/fl#mumfn.eff"
  WRITE_LONG 0x10 206 //Protection from fl#mumfn.spl for Humans, fl#mumfn.eff used by fl#mumfn.spl
  WRITE_LONG 0x14 2
  WRITE_LONG 0x28 1
  WRITE_ASCII 0x30 fl#mumfn #8
  
COPY "atweaks/spl/undead/fl#mumfh.spl" override
     "atweaks/spl/undead/fl#mumfn.spl" override
  FOR (i=LONG_AT 0x64;i<SOURCE_SIZE;i+=0x30) BEGIN
    READ_SHORT i type
    PATCH_IF type = 139 BEGIN
      SAY i + 0x4 @1026
    END
  END
  
OUTER_SET $f_AddSecType(fl#mumfh.spl) = FearSecType
OUTER_SET $f_AddSecType(fl#mumfn.spl) = FearSecType

COPY "atweaks/spl/undead/fl#mum.spl" override
  FOR (i=LONG_AT 0x6a;i<SOURCE_SIZE;i+=0x30) BEGIN
    READ_SHORT i type
    PATCH_IF type = 206 BEGIN
      SAY i + 0x4 @1028
    END
    PATCH_IF type = 139 BEGIN
      SAY i + 0x4 @1029
    END
  END
  //Already on the spl: display string: disease, immunity to cure spells, portrait icon, colour pulse
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 206 target = 2 timing = 1 STR_VAR resource = fl#mum END //Non-cumulativity
  FOR (i=1;i<6;++i) BEGIN
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 78 target = 2 parameter1 = 2 parameter2 = 9 timing = 4 duration = i*7200 END //2 points of chr
  END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 13 target = 2 parameter2 = 512 timing = 4 duration = 6*7200 END //death, disintegration-style
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 139 target = 2 parameter1 = RESOLVE_STR_REF(@1035) timing = 4 duration = 5*7200 END //display a waring a day before insta-death
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 139 target = 2 parameter1 = RESOLVE_STR_REF(@1036) timing = 4 duration = 6*7200 - 1 END //display flavour text right before insta-death
  
//Spells and items that cure and protect against disease and fear are patched at the end

OUTER_SET $f_AddSecType(fl#mum.spl) = DiseaseSecType



// Greater Mummies
ACTION_FOR_EACH file IN
		mumgre01
		riftcr03
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME "%file%.cre" BEGIN
    COPY_EXISTING "%file%.cre" override
      PATCH_INCLUDE "atweaks/lib/fj_cre_validity.tpp"
      PATCH_IF valid BEGIN
        PATCH_INCLUDE "atweaks/lib/fj_cre_reindex.tpp"
        REPLACE_CRE_ITEM fl#gmum #0 #0 #0 none weapon1 EQUIP
        REPLACE_TEXTUALLY CASE_INSENSITIVE immune3 immune2
        LPF DELETE_CRE_EFFECT INT_VAR opcode_to_delete = 0 END
        ADD_MEMORIZED_SPELL sppr101 #0 priest (1) //Bless //done
        ADD_MEMORIZED_SPELL sppr102 #0 priest (2) //Command //done
        ADD_MEMORIZED_SPELL sppr107 #0 priest (1) //Protection from Evil //done
        ADD_MEMORIZED_SPELL sppr109 #0 priest (1) //Sanctuary //done
        ADD_MEMORIZED_SPELL sppr111 #0 priest (2) //Armor of Faith //done
        ADD_MEMORIZED_SPELL sppr113 #0 priest (5) //Doom //done
        ADD_MEMORIZED_SPELL sppr203 #1 priest (1) //Chant //done
        ADD_MEMORIZED_SPELL sppr208 #1 priest (5) //Hold Person
        ADD_MEMORIZED_SPELL sppr211 #1 priest (3) //Silence 15' Radius //done
        ADD_MEMORIZED_SPELL sppr214 #1 priest (3) //DUHM //done
        ADD_MEMORIZED_SPELL sppr301 #2 priest (4) //Animate Dead //done
        ADD_MEMORIZED_SPELL sppr303 #2 priest (2) //Dispel Magic //done
        ADD_MEMORIZED_SPELL sppr304 #2 priest (1) //Glyph of Warding
        ADD_MEMORIZED_SPELL sppr314 #2 priest (4) //Unholy Blight //done
        ADD_MEMORIZED_SPELL sppr405 #3 priest (2) //Mental Domination //done
        ADD_MEMORIZED_SPELL sppr407 #3 priest (1) //Protection from Lightning //done
        ADD_MEMORIZED_SPELL sppr408 #3 priest (1) //Protection from Evil 10' Radius //done
        ADD_MEMORIZED_SPELL sppr411 #3 priest (2) //Poison //done
        ADD_MEMORIZED_SPELL sppr412 #3 priest (2) //Holy Power //done
        ADD_MEMORIZED_SPELL sppr414 #3 priest (2) //Cause Serious Wounds //done

        ADD_MEMORIZED_SPELL sppr505 #4 priest (2) //True Seeing //done
        ADD_MEMORIZED_SPELL sppr509 #4 priest (1) //Magic Resistance //done
        PATCH_IF RANDOM (1 2) BEGIN
          ADD_MEMORIZED_SPELL sppr511 #4 priest (2) //Slay Living //done
        END ELSE BEGIN
          ADD_MEMORIZED_SPELL sppr512 #4 priest (2) //Greater Command //done
        END
        ADD_MEMORIZED_SPELL sppr513 #4 priest (2) //Righteous Magic //done

        ADD_MEMORIZED_SPELL sppr601 #5 priest (1) //Aerial Servant //done
        ADD_MEMORIZED_SPELL sppr608 #5 priest (1) //Harm //done
        ADD_MEMORIZED_SPELL sppr603 #5 priest (1) //Blade Barrier //done
        ADD_MEMORIZED_SPELL sppr612 #5 priest (1) //Bolt of Glory //done

        ADD_MEMORIZED_SPELL sppr719 #6 priest (1) //Symbol, Death //done
        ADD_MEMORIZED_SPELL sppr718 #6 priest (1) //Symbol, Stun //done

        LPF RR#MDCRE
          INT_VAR
            newxpv = 14000
            newhp = 91 //11d8+3
            newac = "-1"
            newth = 9
            newapr = 1
            newsvd = 2
            newsvw = 6
            newsvp = 7
            newsvb = 8
            newsvs = 7
            newmr = 15
            newlvl1 = 19
            newfr = 0
            newcr = 100
            newer = "-50"
            newar = 0
            newrs = 50
            newrc = 50
            newrp = 50
            newrm = 50
            newstr = 18
            newstrx = 99
            newint = 18
            newwis = 21
            newdex = 9
            newcon = 14
            newchr = 9
            newmor = 18
            newalign = 51 //CE
            newclass = 3 //cleric

            enforce = 1
        END
        LPF RR#MDCSC
          STR_VAR
            newscript = fl#gmum
            script01 = wtasight
            script02 = "mummy01`"
        END
        LPF cold_spell_immunities END
      END
    BUT_ONLY
  END
END

COPY "atweaks/itm/undead/fl#mum.itm" "override/fl#gmum.itm"
  WRITE_LONG 0x60 4 //enchantment
  FOR (i=0;i<SHORT_AT 0x68;++i) BEGIN
    WRITE_BYTE LONG_AT 0x64 + 0x38*i + 0x16 6
    WRITE_BYTE LONG_AT 0x64 + 0x38*i + 0x18 3 //3d6
  END
  FOR (i=LONG_AT 0x6a;i<SOURCE_SIZE;i+=0x30) BEGIN
    READ_SHORT i type
    PATCH_IF type = 146 BEGIN
      WRITE_ASCII i + 0x14 fl#gmum
    END
  END
  
COPY "atweaks/spl/undead/fl#mum.spl" "override/fl#gmum.spl"
  FOR (i=LONG_AT 0x6a;i<SOURCE_SIZE;i+=0x30) BEGIN
    READ_SHORT i type
    PATCH_IF type = 206 BEGIN
      SAY i + 0x4 @1028
    END
    PATCH_IF type = 139 BEGIN
      SAY i + 0x4 @1029
    END
  END
  //Already on the spl: display string: disease, immunity to cure spells, portrait icon, colour pulse
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 206 target = 2 timing = 1 STR_VAR resource = fl#gmum END //non-cumulativity
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 206 target = 2 timing = 1 STR_VAR resource = fl#mum END //immunity from regular mummy rot
  FOR (i=1;i<6;++i) BEGIN
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 78 target = 2 parameter1 = 2 parameter2 = 9 timing = 4 duration = i*300 END //2 points of chr
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 78 target = 2 parameter1 = 1 parameter2 = 4 timing = 4 duration = i*300 END //1 point of str
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 78 target = 2 parameter1 = 1 parameter2 = 6 timing = 4 duration = i*300 END //1 point of con
  END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 13 target = 2 parameter2 = 512 timing = 4 duration = 6*300 END //death, disintigration-style
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 139 target = 2 parameter1 = RESOLVE_STR_REF(@1035) timing = 4 duration = 5*300 END //display a waring a day before insta-death
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 139 target = 2 parameter1 = RESOLVE_STR_REF(@1036) timing = 4 duration = 6*300 - 1 END //display flavour text right before insta-death
  PATCH_FOR_EACH parameter2 IN 0 1 BEGIN
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 145 target = 2 parameter2 duration = 6*300 END //disable spellcasting
  END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 142 target = 2 parameter2 = 83 duration = 6*300 END //portrait: spell failure
  
OUTER_SET $f_AddSecType(fl#gmum.spl) = DiseaseSecType

//Fear aura
COPY "atweaks/spl/undead/fl#mumfa.spl" "override/fl#gmumf.spl"
  FOR (i=0;i<SHORT_AT 0x68;++i) BEGIN
    WRITE_SHORT LONG_AT 0x64 + 0x28*i + 0x26 fl#sight
  END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 146 target = 2 timing = 1 parameter1 = 1 parameter2 = 1 STR_VAR resource = fl#gmumn END //cast fl#gmumn
  //LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 2 parameter1 = 1 parameter2 = 4 STR_VAR resource = fl#gmumh END //Use fl#mumfh.eff for humans

/*
COPY "atweaks/eff/base.eff" "override/fl#gmumh.eff"
  WRITE_LONG 0x10 146 //cast spell
  WRITE_LONG 0x14 2
  WRITE_LONG 0x1c 1
  WRITE_LONG 0x20 1
  WRITE_LONG 0x28 1
  WRITE_ASCII 0x30 fl#gmumh #8
  */
/*
COPY "atweaks/eff/base.eff" "override/fl#gmumn.eff"
  WRITE_LONG 0x10 206 //Protection from fl#mumfn.spl for Humans, fl#mumfn.eff used by fl#mumfn.spl
  WRITE_LONG 0x14 2
  WRITE_LONG 0x28 1
  WRITE_ASCII 0x30 fl#gmumn #8*/
  
COPY //"atweaks/spl/undead/fl#mumfh.spl" "override/fl#gmumh.spl"
     "atweaks/spl/undead/fl#mumfn.spl" "override/fl#gmumn.spl"
  READ_LONG  0x64 ao
  READ_SHORT 0x68 na
  READ_LONG  0x6a eo
  ne = 0
  ei = 0
  FOR(i=0;i<na;++i) BEGIN
    ei += ne
    READ_SHORT  ao + 0x28*i + 0x1e ne
    WRITE_SHORT ao + 0x28*i + 0x20 ei
    FOR (j=0;j<ne;++j) BEGIN
      READ_SHORT eo + 0x30*(ei + j) type
      PATCH_IF type = 177 BEGIN
        DELETE_BYTES eo + 0x30*(ei + j) 0x30
        --ne
        --j
      END
      WRITE_LONG eo + 0x30*(ei + j) + 0xe  THIS * 2 //Double duration for the fearsome greater mummies
      WRITE_LONG eo + 0x30*(ei + j) + 0x28 THIS - 3 //And save penalty
    END
    WRITE_SHORT ao + 0x28*i + 0x1e ne
  END

OUTER_SET $f_AddSecType(fl#gmumn.spl) = FearSecType



// Shadow
ACTION_FOR_EACH file IN
                kshadow  //circus
                rngsha01 //in shadow temple
                rngsha04 //Shadow Jailer
                sdshadow //Spawned by area script in shadow temple
                sewsha03 //Saradush sewers
                shadow01 //regular
                shadowsu //summoned shadow (shadow altar)
                uhcreat  //with the kids in umar cave
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME "%file%.cre" BEGIN
    COPY_EXISTING "%file%.cre" override
    PATCH_INCLUDE "atweaks/lib/fj_cre_validity.tpp"
      PATCH_IF valid BEGIN
        PATCH_INCLUDE "atweaks/lib/fj_cre_reindex.tpp"
        REPLACE_CRE_ITEM ring95 #0 #0 #0 none lring //Standard undead immunities
        REPLACE_CRE_ITEM immune1 #0 #0 #0 none rring //+1 or better only
        REPLACE_CRE_ITEM fl#shad #0 #0 #0 none weapon1
        LPF RR#MDCRE
          INT_VAR
            newxpv = 420
            newhp = 27 //3d8+3
            newac = 7
            newth = 17
            newapr = 1
            newsvd = 13
            newsvw = 15
            newsvp = 14
            newsvb = 16
            newsvs = 16
            newlvl1 = 4
            newlvl2 = 0
            newlvl3 = 0
            newmr = 0
            newfr = 0
            newcr = 100
            newer = 0
            newar = 0
            newrs = 0
            newrc = 0
            newrp = 0
            newrm = 0
            newstr = 13
            newstrx = 0
            newint = 7
            newwis = 7
            newdex = 10
            newcon = 9
            newchr = 9
            newalign = 0x33 //CE
            newclass = 170 //Shadow
            newgen = 4 //undead
            newrace = 132 //Shadow
            
            enforce = 1
          STR_VAR
            notenf01 = rngsha04
        END
        LPF RR#MDCSC
          STR_VAR
            newscript = fl#midmc
            script01 = wtasight
        END
        LPF cold_spell_immunities END
      END
    BUT_ONLY
  END
END

COPY_EXISTING b1-10.itm "override/fl#shad.itm"
  WRITE_LONG 0x60 1 //enchantment
  LPF configure_header
    INT_VAR
      f_TargetType = 1
      f_NumDice = 2
      f_DieSize = 5
  END
  LPF ADD_ITEM_EFFECT
    INT_VAR
      type = 1

      opcode = 44 //strengh bonus
      target = 2
      parameter1 = "-1"
      duration = 5*60
  END
  LPF ADD_ITEM_EFFECT
    INT_VAR
      type = 1

      opcode = 142 //portrait icon
      target = 2
      parameter2 = 91 //damaged ability score
      duration = 5*60
  END



// Skeleton
ACTION_FOR_EACH file IN
                ghastsu //Animate Dead
                gpskel1 //Underdark adventuring party
                rskel01 //Ranger plot
                rskel03 //Ranger plot
                skelar01 //with Vongoethe
                skelar02 //aslo Vongoethe
                skelet01 //regular
                ttskel   //tutorial
                uddoor07 //drow party by the UD exit
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME "%file%.cre" BEGIN
    COPY_EXISTING "%file%.cre" override
      PATCH_INCLUDE "atweaks/lib/fj_cre_validity.tpp"
      PATCH_IF valid BEGIN
        PATCH_INCLUDE "atweaks/lib/fj_cre_reindex.tpp"
        REPLACE_CRE_ITEM ring99 #0 #0 #0 none lring //Standard undead immunities, plus red colouring, sans charm immunity
        REPLACE_CRE_ITEM immchs #0 #0 #0 none amulet //charm and sleep immunity
        LPF RR#MDCRE
          INT_VAR
            newxpv = 65
            newhp = 8 //1d8
            newac = 7
            newth = 19
            newapr = 1
            newsvd = 14
            newsvw = 16
            newsvp = 15
            newsvb = 17
            newsvs = 17
            newlvl1 = 1
            newlvl2 = 0
            newlvl3 = 0
            newmr = 0
            newfr = 0
            newcr = 100
            newer = 0
            newar = 0
            newrs = 50
            newrc = 0
            newrp = 50
            newrm = 50
            newstr = 9
            newstrx = 0
            newint = 3
            newwis = 9
            newdex = 9
            newcon = 9
            newchr = 9
            newalign = 0x22 //Neutral
            newclass = 134 //Skeleton
            newgen = 4 //undead
            newrace = 115 //Skeleton
          STR_VAR
            enfc01 = skelet01
        END
        PATCH_IF "%file%" STRING_EQUAL_CASE skelar01 OR "%file%" STRING_EQUAL_CASE skelar02 BEGIN
          SPRINT script fl#lowr
        END ELSE BEGIN
          SPRINT script fl#lowm
        END
        LPF RR#MDCSC
          STR_VAR
            newscript = EVAL "%script%"
            script02 = wtasight
            script03 = wtarsgt
            skipfile02 = ghastsu
        END
        LPF add_skeletal_traits END
      END
    BUT_ONLY
  END
END



// Skeleton Warriors
ACTION_FOR_EACH file IN
               ar18skel
               ceskel01
               grskel1
               grskel2
               grtomb01
               hgskl04
               nevm2
               riftcr02
               rskel02
               sahskel
               skelwa
               skelwa01
               skelwa02
               skelwa03
               skelwasu
               suundead
               _skelwa
               _kelwa02
               _kelwa03
               bgskel03
               bgskel02
               bgskelwa
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME "%file%.cre" BEGIN
    COPY_EXISTING "%file%.cre" override
      PATCH_INCLUDE "atweaks/lib/fj_cre_validity.tpp"
      PATCH_IF valid BEGIN
        PATCH_INCLUDE "atweaks/lib/fj_cre_reindex.tpp"
        REPLACE_CRE_ITEM ring99 #0 #0 #0 none lring //Standard undead immunities, plus red colouring, sans charm immunity
        REPLACE_CRE_ITEM immune1 #0 #0 #0 none rring //+1 or better only
        REPLACE_CRE_ITEM immchs #0 #0 #0 none amulet //charm and sleep immunity
        LPM unturnable
        LPF RR#MDCRE
          INT_VAR
            newxpv = 4000
            newhp = 80 //9d8+8
            newac = 2
            newth = 8
            newapr = 1
            newsvd = 7
            newsvw = 9
            newsvp = 8
            newsvb = 8
            newsvs = 10
            newlvl1 = 12
            newlvl2 = 0
            newlvl3 = 0
            newmr = 90
            newfr = 0
            newcr = 100
            newer = 0
            newar = 0
            newrs = 50
            newrc = 0
            newrp = 50
            newrm = 50
            newstr = 18
            newstrx = 50
            newint = 16
            newwis = 9
            newdex = 16
            newcon = 14
            newchr = 9
            newmor = 15
            newalign = 35 //NE
            newclass = 135 //Skeleton Warrior (for ID)
            newgen = 4 //undead
            newrace = 115 //Skeleton
            
            enforce = 1
          STR_VAR
            notenf01 = hgskl04
            notenf02 = grtomb01
        END
        PATCH_IF "%file%" STRING_EQUAL_CASE skelwa03 OR "%file%" STRING_EQUAL_CASE _kelwa03 OR "%file%" STRING_EQUAL_CASE bgskel03 BEGIN
          SPRINT script fl#sklwr
        END ELSE BEGIN
          SPRINT script fl#sklwm
        END
        LPF RR#MDCSC
          STR_VAR
            newscript = EVAL "%script%"
            script01 = gpskel1
            script02 = wtasight
            script03 = _tasight
            script04 = wtarsgt
            script05 = dw#melee
            script06 = wdasight
            script07 = dw#range
            script98 = _wtarsgt
            skipfile02 = skelwasu
        END
        LPF add_skeletal_traits END
      END
    BUT_ONLY
  END
END

COPY "atweaks/spl/undead/fl#skelw.spl" override
  FOR (i=0;i<SHORT_AT 0x68;++i) BEGIN
    WRITE_SHORT LONG_AT 0x64 + 0x28*i + 0x26 fl#sight
  END



// Spectre
ACTION_FOR_EACH file IN
                spectr01 //regular (also ranger stronghold mission)
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME "%file%.cre" BEGIN
    COPY_EXISTING "%file%.cre" override
      PATCH_INCLUDE "atweaks/lib/fj_cre_validity.tpp"
      PATCH_IF valid BEGIN
        PATCH_INCLUDE "atweaks/lib/fj_cre_reindex.tpp"
        REPLACE_CRE_ITEM ring95 #0 #0 #0 none lring //Standard undead immunities
        REPLACE_CRE_ITEM immune1 #0 #0 #0 none rring
        REPLACE_CRE_ITEM fl#spect #0 #0 #0 none weapon1
        LPF RR#MDCRE
          INT_VAR
            newxpv = 3000
            newhp = 59 //7d8+3
            newac = 2
            newth = 13
            newapr = 1
            newsvd = 7
            newsvw = 9
            newsvp = 8
            newsvb = 9
            newsvs = 8
            newlvl1 = 8
            newlvl2 = 0
            newlvl3 = 0
            newmr = 0
            newfr = 0
            newcr = 100
            newer = 0
            newar = 0
            newrs = 0
            newrc = 0
            newrp = 0
            newrm = 0
            newstr = 13
            newstrx = 0
            newint = 14
            newwis = 11
            newdex = 10
            newcon = 9
            newchr = 10
            newalign = 0x13 //LE
            newgen = 4 //undead
            
            enforce = 1
        END
        LPF RR#MDCSC
          STR_VAR
            newscript = fl#higmc
            script01 = wtasight
        END
        LPF hover_immunities END
        LPF cold_spell_immunities END
      END
    BUT_ONLY
  END
END

COPY_EXISTING b1-8.itm "override/fl#spect.itm"
  WRITE_LONG 0x60 2 //enchantment
  DEFINE_ARRAY wigh_opcode BEGIN 216 139 141 END //drain 2 levels
  DEFINE_ARRAY wigh_p1 BEGIN 2 40968 0 END
  DEFINE_ARRAY wigh_p2 BEGIN 0 0 1 END
  PHP_EACH wigh_opcode AS int => opcode BEGIN
    LPF ADD_ITEM_EFFECT
      INT_VAR
        type = 1

        opcode
        target = 2
        parameter1 = $wigh_p1("%int%")
        parameter2 = $wigh_p2("%int%")
        timing = 1
     END
   END



// Wight
ACTION_FOR_EACH file IN
                nevm1 //Nev's
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME "%file%.cre" BEGIN
    COPY_EXISTING "%file%.cre" override
      PATCH_INCLUDE "atweaks/lib/fj_cre_validity.tpp"
      PATCH_IF valid BEGIN
        PATCH_INCLUDE "atweaks/lib/fj_cre_reindex.tpp"
        REPLACE_CRE_ITEM ring95 #0 #0 #0 none lring //Standard undead immunities
        REPLACE_CRE_ITEM immune1 #0 #0 #0 none rring
        REPLACE_CRE_ITEM fl#wigh #0 #0 #0 none weapon1
        WRITE_SHORT 0x28 30464 //animation: ghoul
        LPF RR#MDCRE
          INT_VAR
            newxpv = 1400
            newhp = 35 //4d8+3
            newac = 5
            newth = 15
            newapr = 1
            newsvd = 12
            newsvw = 14
            newsvp = 13
            newsvb = 15
            newsvs = 13
            newlvl1 = 5
            newlvl2 = 0
            newlvl3 = 0
            newmr = 0
            newfr = 0
            newcr = 100
            newer = 0
            newar = 0
            newrs = 0
            newrc = 0
            newrp = 0
            newrm = 0
            newstr = 16
            newstrx = 0
            newint = 10
            newwis = 9
            newdex = 9
            newcon = 9
            newchr = 9
            newalign = 0x13 //LE
            newgen = 4 //undead
            
            enforce = 1
        END
        LPF RR#MDCSC
          STR_VAR
            newscript = fl#midmc
            script01 = wdasight
        END
        LPF cold_spell_immunities END
      END
    BUT_ONLY
  END
END

COPY_EXISTING b1-4.itm "override/fl#wigh.itm"
  WRITE_LONG 0x60 1 //enchantment
  DEFINE_ARRAY wigh_opcode BEGIN 216 139 141 END //drain 1 level
  DEFINE_ARRAY wigh_p1 BEGIN 1 41495 0 END
  DEFINE_ARRAY wigh_p2 BEGIN 0 0 1 END
  PHP_EACH wigh_opcode AS int => opcode BEGIN
    LPF ADD_ITEM_EFFECT
      INT_VAR
        type = 1

        opcode
        target = 2
        parameter1 = $wigh_p1("%int%")
        parameter2 = $wigh_p2("%int%")
        timing = 1
     END
   END



// Wraith
ACTION_FOR_EACH file IN
                chwraith //Neb's child wraiths
                //telslav //Demon Wraith's slaves; not undead, apparently
                wraith01 //regular
                firwra01 //Greater Wraith at Firkraag's
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME "%file%.cre" BEGIN
    COPY_EXISTING "%file%.cre" override
      PATCH_INCLUDE "atweaks/lib/fj_cre_validity.tpp"
      PATCH_IF valid BEGIN
        PATCH_INCLUDE "atweaks/lib/fj_cre_reindex.tpp"
        REPLACE_CRE_ITEM immune1 #0 #0 #0 none lring
        PATCH_IF !"%SOURCE_RES%" STRING_EQUAL_CASE chwraith BEGIN
          REPLACE_CRE_ITEM ring95 #0 #0 #0 none rring //Standard undead immunities
        END
        PATCH_IF !"%SOURCE_RES%" STRING_EQUAL_CASE telslav BEGIN
          REPLACE_CRE_ITEM fl#wrai #0 #0 #0 none weapon1
        END
        LPF RR#MDCRE
          INT_VAR
            newxpv = 2000
            newhp = 43 //5d8+3
            newac = 4
            newth = 15
            newapr = 1
            newsvd = 10
            newsvw = 12
            newsvp = 11
            newsvb = 12
            newsvs = 13
            newlvl1 = 6
            newlvl2 = 0
            newlvl3 = 0
            newmr = 0
            newfr = 0
            newcr = 100
            newer = 0
            newar = 0
            newrs = 0
            newrc = 0
            newrp = 0
            newrm = 0
            newstr = 13
            newstrx = 0
            newint = 12
            newwis = 11
            newdex = 10
            newcon = 9
            newchr = 10
            newalign = 0x13 //LE
            newgen = 4 //undead
            
            enforce = 1
          STR_VAR
            notenf01 = chwraith
            notenf02 = telslav
            notenf03 = firwra01
        END
        LPF RR#MDCSC
          STR_VAR
            newscript = fl#higmc
            script01 = wtasight
        END
        LPF hover_immunities END
        LPF cold_spell_immunities END
      END
    BUT_ONLY
  END
END

COPY_EXISTING b1-6.itm "override/fl#wrai.itm"
  WRITE_LONG 0x60 1 //enchantment
  DEFINE_ARRAY wigh_opcode BEGIN 216 139 141 END //drain 1 level
  DEFINE_ARRAY wigh_p1 BEGIN 1 41495 0 END
  DEFINE_ARRAY wigh_p2 BEGIN 0 0 1 END
  PHP_EACH wigh_opcode AS int => opcode BEGIN
    LPF ADD_ITEM_EFFECT
      INT_VAR
        type = 1

        opcode
        target = 2
        parameter1 = $wigh_p1("%int%")
        parameter2 = $wigh_p2("%int%")
        timing = 1
     END
   END

//Spawned creature inside the Shadow Temple
COPY_EXISTING wraith01.cre "override/fl#sdwra.cre"
  WRITE_ASCIIL 0x248 gensht01 sdshadow fl#higmc

//Spawned creatures outside the Shadow Temple
COPY_EXISTING wraith01.cre "override/fl#stwra.cre"
  WRITE_ASCIIE 0x280 "%DEST_RES%" #32

//Replace Shadow Fiends in Saradush sewers with souped-up wraiths
COPY_EXISTING wraith01.cre "override/sewsha01.cre"
              wraith01.cre "override/sewsha02.cre"
  WRITE_ASCIIL 0x248 sewsha01 fl#higmc //scripts
  WRITE_ASCIIE 0x280 "%DEST_RES%" #32 //script name
  LPF RR#MDCRE
    INT_VAR
      newhp = 59
      newapr = 4
      newac = 1
      newth = 9
      newfr = 100
      newcr = 100
      newar = 100
      newrs = 30
      newrc = 30
      newrp = 30
      newrm = 30
  END



// Zombie
ACTION_FOR_EACH file IN
                bhghoul3 //undead village
                bhghoul5 //undead village
                kelzomb //Keldorn's
                nevm5 //Nev's
                rzomb01 //Ranger stronghold mission
                zombie01 //Unused, but no accounting for mods
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME "%file%.cre" BEGIN
    COPY_EXISTING "%file%.cre" override
      PATCH_INCLUDE "atweaks/lib/fj_cre_validity.tpp"
      PATCH_IF valid BEGIN
        PATCH_INCLUDE "atweaks/lib/fj_cre_reindex.tpp"
        REPLACE_CRE_ITEM ring95 #0 #0 #0 none lring //Standard undead immunities
        REPLACE_CRE_ITEM fl#zomb #0 #0 #0 none weapon1
        LPF RR#MDCRE
          INT_VAR
            newxpv = 65
            newhp = 16 //2d8
            newac = 8
            newth = 19
            newapr = 1
            newsvd = 14
            newsvw = 16
            newsvp = 15
            newsvb = 17
            newsvs = 17
            newlvl1 = 2
            newlvl2 = 0
            newlvl3 = 0
            newmr = 0
            newfr = 0
            newcr = 100
            newer = 0
            newar = 0
            newrs = 0
            newrc = 0
            newrp = 0
            newrm = 0
            newstr = 9
            newstrx = 0
            newint = 3
            newwis = 9
            newdex = 9
            newcon = 9
            newchr = 9
            newmor = 20
            newalign = 34 //Neutral
            newclass = 198 //Zombie
            newgen = 4 //undead
            newrace = 148 //Zombie
            
            enforce = 1
        END
        LPF RR#MDCSC
          STR_VAR
            newscript = fl#lowm
            script01 = wtasight
            skipfile02 = kelzomb
        END
        LPF cold_spell_immunities END
      END
    BUT_ONLY
  END
END

COPY_EXISTING b1-8.itm "override/fl#zomb.itm"
  FOR (i = 0; i < SHORT_AT 0x68; ++i) BEGIN
    WRITE_BYTE LONG_AT 0x64 + 0x38*i + 0x12 10 //speed
  END



// Zombie, Ju-ju
ACTION_FOR_EACH file IN
                zombju01
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME "%file%.cre" BEGIN
    COPY_EXISTING "%file%.cre" override
      PATCH_INCLUDE "atweaks/lib/fj_cre_validity.tpp"
      PATCH_IF valid BEGIN
        PATCH_INCLUDE "atweaks/lib/fj_cre_reindex.tpp"
        SAY NAME1 @1037
        SAY NAME2 @1037
        REPLACE_CRE_ITEM ring95 #0 #0 #0 none lring //Standard undead immunities
        REPLACE_CRE_ITEM immune1 #0 #0 #0 none rring
        REPLACE_CRE_ITEM fl#zomju #0 #0 #0 none weapon1
        LPM illusion_immunity
        LPF ADD_CRE_EFFECT
          INT_VAR
            opcode = 83 //protection from projectile
            target = 1
            parameter2 = 0x24 //Magic Missile
            timing = 9
        END
        //LPM seeinvisible
        LPF RR#MDCRE
          INT_VAR
            newxpv = 975
            newhp = 36 //3d8+12
            newac = 6
            newth = 15
            newapr = 1
            newsvd = 11
            newsvw = 13
            newsvp = 12
            newsvb = 13
            newsvs = 14
            newlvl1 = 9
            newlvl2 = 0
            newlvl3 = 0
            newmr = 0
            newfr = 50
            newcr = 100
            newer = 100
            newar = 0
            newrs = 0
            newrc = 50
            newrp = 50
            newrm = 50
            newstr = 9
            newstrx = 0
            newint = 7
            newwis = 9
            newdex = 16
            newcon = 9
            newchr = 9
            newmor = 20
            newalign = 35 //NE
            newclass = 198 //Zombie
            newgen = 4 //undead
            newrace = 148 //Zombie
            
            enforce = 1
        END
        LPF RR#MDCSC
          STR_VAR
            newscript = fl#midmc
            script01 = wtasight
        END
        LPF cold_spell_immunities END
        LPF electrical_spell_immunities END
      END
    BUT_ONLY
  END
END

COPY_EXISTING b3-12.itm "override/fl#zomju.itm"
  WRITE_LONG 0x60 1 //enchantment



// Zombie, Sea
ACTION_FOR_EACH file IN
                sahzomb
                zombse01
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME "%file%.cre" BEGIN
    COPY_EXISTING "%file%.cre" override
      PATCH_INCLUDE "atweaks/lib/fj_cre_validity.tpp"
      PATCH_IF valid BEGIN
        PATCH_INCLUDE "atweaks/lib/fj_cre_reindex.tpp"
        REPLACE_CRE_ITEM ring95 #0 #0 #0 none lring //Standard undead immunities
        REPLACE_CRE_ITEM fl#zomse #0 #0 #0 none weapon1
        LPM illusion_immunity
        LPM unturnable
        //LPM seeinvisible
        ADD_MEMORIZED_SPELL sppr102 #0 priest //Command
        ADD_MEMORIZED_SPELL sppr113 #0 priest //Doom
        ADD_MEMORIZED_SPELL sppr208 #1 priest //Hold Person
        LPF RR#MDCRE
          INT_VAR
            newxpv = 420
            newhp = 40 //5d8
            newac = 6
            newth = 15
            newapr = 1
            newsvd = 11
            newsvw = 13
            newsvp = 12
            newsvb = 13
            newsvs = 14
            newlvl1 = 5
            newlvl2 = 3
            newlvl3 = 0
            newmr = 0
            newfr = 50
            newcr = "-100"
            newer = "-100"
            newar = 0
            newrs = 0
            newrc = 0
            newrp = 0
            newrm = 0
            newstr = 18
            newstrx = 0
            newint = 7
            newwis = 9
            newdex = 9
            newcon = 9
            newchr = 9
            newmor = 20
            newalign = 0x33 //NE
            newclass = 8 //F/C
            newgen = 4 //undead
            newrace = 148 //Zombie
            
            enforce = 1
        END
        LPF RR#MDCSC
          STR_VAR
            newscript = fl#zomse
            script01 = wtasight
            script03 = wdasight
        END
      END
    BUT_ONLY
  END
END

COPY "atweaks/spl/base.spl" "override/fl#zomse.spl" //Aura
  READ_LONG 0x64 ao
  FOR (i = 0; i < SHORT_AT 0x68; ++i) BEGIN
    WRITE_BYTE  ao + 0x28*i + 0xc 4 //any point
    WRITE_SHORT ao + 0x28*i + 0x26 fl#sight
  END
  DEFINE_ARRAY zomse_opcode BEGIN 278 0 142 139 END
  DEFINE_ARRAY zomse_p1 BEGIN "-1" "-1" 0 17398 END
  DEFINE_ARRAY zomse_p2 BEGIN 0 0 126 0 END
  PHP_EACH zomse_opcode AS int => opcode BEGIN 
    LPF ADD_SPELL_EFFECT
      INT_VAR
        opcode
        target = 2
        parameter1 = $zomse_p1("%int%")
        parameter2 = $zomse_p2("%int%")
        timing = opcode = 139 ? 1 : 0
        duration = opcode = 139 ? 0 : 30
        savingthrow = BIT2 //poison
    END
  END
  LPF ADD_SPELL_EFFECT
    INT_VAR
      opcode = 206 //non-cumulativity
      target = 2
      duration = 30
      savingthrow = BIT2
    STR_VAR
      resource = fl#zomse
  END

COPY_EXISTING b1-10.itm "override/fl#zomse.itm" //Sorry, but the zombie animation does not support weapons, so fists it is
  WRITE_LONG 0x60 1 //enchantment
  LPF ADD_ITEM_EFFECT
    INT_VAR
      type = 1

      opcode = 146 //cast spell
      target = 2
      parameter1 = 1
      parameter2 = 1
      timing = 1
      probability1 = 10
    STR_VAR
      resource = fl#zomsd
  END

COPY "atweaks/spl/base.spl" "override/fl#zomsd.spl"
  LPF ADD_SPELL_EFFECT
    INT_VAR
      opcode = 78
      target = 2
      parameter1 = 1
      parameter2 = 4 //strength
      timing = 4
      duration = 8*300
  END
  LPF ADD_SPELL_EFFECT
    INT_VAR
      opcode = 78
      target = 2
      parameter1 = 1
      parameter2 = 6 //constitution
      timing = 4
      duration = 8*300
  END
  LPF ADD_SPELL_EFFECT
    INT_VAR
      opcode = 142 //portrait icon
      target = 2
      parameter2 = 7 //nauseated
      timing = 1
  END
  LPF ADD_SPELL_EFFECT
    INT_VAR
      opcode = 139
      target = 2
      parameter1 = 39752 //stricken by a foul disease
      timing = 1
  END

OUTER_SET $f_AddSecType(fl#zomsd.spl) = DiseaseSecType



//Replace the stupid run-away-and-or-attack script with one that is just responsible for running away
COPY_EXISTING sahlace.cre override
              sahskel.cre override
              sahzomb.cre override
  REPLACE_TEXTUALLY sahamb01 fl#saham (8)
BUT_ONLY



// Scripts
ACTION_IF BG1 AND !BGT BEGIN
  OUTER_SPRINT DurlagArea fw0516
  OUTER_SPRINT TutuJournal
  "
    EraseJournalEntry(84201)
    EraseJournalEntry(84195)
    EraseJournalEntry(83499)
    EraseJournalEntry(83502)
    EraseJournalEntry(83506)
    EraseJournalEntry(83508)
    EraseJournalEntry(83510)
    EraseJournalEntry(83512)
    EraseJournalEntry(83522)
    EraseJournalEntry(83710)
    EraseJournalEntry(83561)
    EraseJournalEntry(83738)
    EraseJournalEntry(83894)
    EraseJournalEntry(83899)
    EraseJournalEntry(83905)
    EraseJournalEntry(83989)
    EraseJournalEntry(83993)
    EraseJournalEntry(83995)
"
  OUTER_SPRINT Skelwa _skelwa
  OUTER_SPRINT GGhoul _houllor
  OUTER_SPRINT SkeletC _kelet_c
  OUTER_SPRINT killgood _illgood
  OUTER_SPRINT gooddeat _ooddeat
END ELSE ACTION_IF BGT BEGIN
  OUTER_SPRINT DurlagArea ard016
  OUTER_SPRINT TutuJournal ""
  OUTER_SPRINT Skelwa bgskelwa
  OUTER_SPRINT GGhoul ghoullor
  OUTER_SPRINT SkeletC skelet_c
  OUTER_SPRINT killgood killgood
  OUTER_SPRINT gooddeat gooddeat
END

ACTION_IF BG1 BEGIN
  COPY - "atweaks/baf/undead/killgood.baf" "...atweaks/fl-inlined/%killgood%.baf"
         "atweaks/baf/undead/gooddeat.baf" "...atweaks/fl-inlined/%gooddeat%.baf"
  COMPILE ~atweaks/baf/undead/fl#bg1kn.baf~ "...atweaks/fl-inlined/%killgood%.baf" "...atweaks/fl-inlined/%gooddeat%.baf" EVAL
END 

COMPILE ~atweaks/baf/undead/fl#death.baf~
        ~atweaks/baf/undead/fl#mum.baf~
        ~atweaks/baf/undead/fl#ghoul.baf~
        ~atweaks/baf/undead/fl#ghast.baf~
        "atweaks/baf/undead/fl#dksw5.baf"
        "atweaks/baf/undead/fl#glord.baf"
        "atweaks/baf/undead/fl#sklwm.baf"
        "atweaks/baf/undead/fl#sklwr.baf"
        "atweaks/baf/undead/fl#bansh.baf"
	"atweaks/baf/undead/fl#gmum.baf"
        "atweaks/baf/undead/fl#lowm.baf"
        "atweaks/baf/undead/fl#zomse.baf"
        "atweaks/baf/undead/fl#lowr.baf"
        "atweaks/baf/undead/fl#midmc.baf"
        "atweaks/baf/undead/fl#higmc.baf"
        "atweaks/baf/undead/fl#saham.baf"



//Write the queued-up values
ACTION_PHP_EACH f_AddSecType AS file => SecTypeToAdd BEGIN
  COPY_EXISTING "%file%" override
    WRITE_BYTE 0x27 SecTypeToAdd
  BUT_ONLY
END

OUTER_SET $f_ImmunitySecType(78) = DiseaseSecType
OUTER_SET $f_ImmunitySecType(24) = FearSecType

OUTER_SET $f_RemoveSecType(79) = DiseaseSecType
OUTER_SET $f_RemoveSecType(161) = FearSecType

LAF integrate_sectypes END

