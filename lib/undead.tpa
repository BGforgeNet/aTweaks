
INCLUDE "atweaks/lib/lib.tpa"

DEFINE_PATCH_FUNCTION add_skeletal_traits BEGIN
  READ_ASCII 0 sig (4)  
  PATCH_IF "%sig%" STRING_EQUAL_CASE "CRE " AND SOURCE_SIZE > 0x2d3 BEGIN
    WRITE_BYTE 0x5a 100 //Cold
    WRITE_BYTE 0x5f 100 //Magic cold
    WRITE_BYTE 0x60 50  //Slashing
    WRITE_BYTE 0x62 50  //Piercinc
    WRITE_BYTE 0x63 50  //Missile
    LPF cold_spell_immunities END
  END ELSE BEGIN
    PATCH_FAIL ~Incorrect file type or below minimum size~
  END
END

DEFINE_PATCH_MACRO unturnable BEGIN
  LPF ADD_CRE_EFFECT
    INT_VAR
       opcode = 297 //Immunity to Turn Undead
       target = 1
       timing = 9
       paramater2 = 1
  END
END

DEFINE_PATCH_MACRO seeinvisible BEGIN
  LPF ADD_CRE_EFFECT
    INT_VAR
      opcode = 193
      target = 1
      parameter2 = 1
      timing = 9
  END
END

DEFINE_PATCH_MACRO illusion_immunity BEGIN
  LPF ADD_CRE_EFFECT
    INT_VAR
      opcode = 291 //disable visual effect
      target = 1
      parameter2 = 1
      timing = 9
  END
  LPF ADD_CRE_EFFECT
    INT_VAR
      opcode = 204 //immunity to school
      target = 1
      parameter2 = 5 //illusion
      timing = 9
  END
END


//Wall of Ice
ACTION_IF !FILE_EXISTS_IN_GAME rr#icew.spl BEGIN
  COPY ~atweaks/bam/rr#icew.bam~ ~override~                                          // Wall of Ice BAM
  COPY ~atweaks/spl/rr#icew.spl~ ~override~                                          // Wall of Ice
    SAY NAME1 @1024 SAY NAME2 @1024
    READ_LONG  0x64 ab_off
    READ_SHORT 0x68 ab_num
    FOR(i=0; i<ab_num; i+=1) BEGIN
      WRITE_SHORT (ab_off+i*0x28+0x26) %rr#icew%                                     // use new projectile
    END
END

//Symbol, Pain
ACTION_IF !FILE_EXISTS_IN_GAME rr#spain.spl BEGIN
  COPY ~aTweaks/SPL/RR#SPAIN.SPL~ ~override~                                         // Symbol Pain
    READ_LONG  0x64 ab_off
    READ_SHORT 0x68 ab_num
    FOR(i=0; i<ab_num; i+=1) BEGIN
      WRITE_SHORT (ab_off+i*0x28+0x26) %RR#SPAIN%                                    // use new projectile (party friendly)
    END
END



//Sensible Encounters

//Replace the Vampyre with a Spectre, because Vampyres are not undead and Spectres are underrepresented
COPY_EXISTING nevmtrap.bcs override
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~"nevm4"~ ~"spectr01"~
  END
BUT_ONLY
IF nevm4

//Replace Shadow Fiend with a Wraith, because Shadow Fiends are not undead
COPY_EXISTING ar1401.bcs override
              ar5013.bcs override
              spwndead.bcs override
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~"sdshadfi"~ ~"fl#sdwra"~
    REPLACE_TEXTUALLY ~"shadfi01"~ ~"wraith01"~
  END
BUT_ONLY

COPY_EXISTING ar1404.bcs override
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~"shadfi01"~ ~"fl#stwra"~
  END
BUT_ONLY
IF shadfi01

//Replace Shadow Fiends with mummies, for triple-mummy goodness
COPY_EXISTING ar0813.are override
  READ_LONG  0x54 ao
  FOR (i = 0; i < SHORT_AT 0x58; ++i) BEGIN
    READ_ASCII ao + 0x110 * i + 0x80 resref
    PATCH_IF "%resref%" STRING_EQUAL_CASE shadfi01 BEGIN
      WRITE_ASCII ao + 0x110 * i        Mummy #32
      WRITE_LONG  ao + 0x110 * i + 0x30 57472 //IC_MUMMY
      WRITE_ASCII ao + 0x110 * i + 0x80 mummy01 #8
    END
  END
BUT_ONLY
IF shadfi01

//Replace Shadow Fiends with Wraiths
COPY_EXISTING ar1202.are override
  READ_LONG 0xc0 reo
  FOR (i = 0; i < 10; ++i) BEGIN
    READ_ASCII reo + 0x48 + 0x8 * i resref
    PATCH_IF "%resref%" STRING_EQUAL_CASE shadfi01 BEGIN
      WRITE_ASCII reo + 0x48 + 0x8 * i wraith01 #8
    END
  END
BUT_ONLY
IF shadfi01

//Replace Shadow Fiends with Wraiths
COPY_EXISTING ar1404.are override
  READ_LONG  0x54 ao
  FOR (i = 0; i < SHORT_AT 0x58; ++i) BEGIN
    READ_ASCII ao + 0x110 * i + 0x80 resref
    PATCH_IF "%resref%" STRING_EQUAL_CASE shadfi01 BEGIN
      WRITE_ASCII ao + 0x110 * i        Wraith #32
      WRITE_ASCII ao + 0x110 * i + 0x80 fl#stwra #8
    END
  END
BUT_ONLY
IF shadfi01
