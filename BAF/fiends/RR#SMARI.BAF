
//Note: this script always fights against the party

IF
  HasItem("dw#sumfi",Myself)
THEN
  RESPONSE #100
    DestroyItem("dw#sumfi")
    Continue()
END

IF
	Die()
        Global("RR#EACheck","LOCALS",1) // Fiend was summoned by an enemy
        Global("RR#XP","LOCALS",0)
THEN
	RESPONSE #100
                SetGlobal("RR#XP","LOCALS",1)
                AddexperienceParty(23000) // Marilith XP value
END

IF
        Global("RR#EACheck","LOCALS",0)
	Allegiance(LastSummonerOf(Myself),GOODCUTOFF)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
                SetGlobal("RR#EACheck","LOCALS",2)
		LeaveParty()
		ChangeEnemyAlly(Myself,ALLY)
                DialogueInterrupt(FALSE)
		SetInterrupt(TRUE)
END

IF
        Global("RR#EACheck","LOCALS",0)
	OR(2)
	  Allegiance(LastSummonerOf(Myself),EVILCUTOFF)
          !Exists(LastSummonerOf(Myself))
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
                SetGlobal("RR#EACheck","LOCALS",1)
		Enemy()
		SetInterrupt(TRUE)
END

IF
	OnCreation()
        Global("RR#EACheck","LOCALS",2) // summoned by the player
	!InParty(LastSummonerOf(Myself))
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		LeaveParty()
		ChangeEnemyAlly(Myself,ALLY)
                DialogueInterrupt(FALSE)
		SetInterrupt(TRUE)
END

IF
        Global("RR#EACheck","LOCALS",2) // summoned by the player
        Global("RR#Hostile","LOCALS",1)
	Allegiance(Myself,EVILCUTOFF)
        !See([PC])
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
                SetGlobal("RR#Hostile","LOCALS",0)
		LeaveParty()
		ChangeEnemyAlly(Myself,ALLY)
                DialogueInterrupt(FALSE)
		SetInterrupt(TRUE)
END

IF
        Global("RR#EACheck","LOCALS",2)
        Global("RR#Hostile","LOCALS",0)
	Allegiance(Myself,GOODCUTOFF)
        See([PC])
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
                SetGlobal("RR#Hostile","LOCALS",1)
		Enemy()
		SetInterrupt(TRUE)
END

IF
	OR(2)
	  StateCheck(Myself,STATE_REALLY_DEAD)
	  StateCheck(Myself,STATE_DISABLED)
THEN
	RESPONSE #100
		NoAction()
END

IF
	!InActiveArea(Myself)
	!See([0])
THEN
	RESPONSE #100
		NoAction()
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	OR(2)
          AreaCheck("AR3004") // Dead Magic zone
          AreaCheck("AR3008") // Dead Magic zone
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Cast","LOCALS",600) // no spellcasting in Dead Magic zones
END

IF
	OR(11)
	  StateCheck(NearestEnemyOf(Myself),STATE_ILLUSIONS)
	  StateCheck(SecondNearestEnemyOf(Myself),STATE_ILLUSIONS)
	  StateCheck(ThirdNearestEnemyOf(Myself),STATE_ILLUSIONS)
	  StateCheck(FourthNearestEnemyOf(Myself),STATE_ILLUSIONS)
	  StateCheck(FifthNearestEnemyOf(Myself),STATE_ILLUSIONS)
	  StateCheck(SixthNearestEnemyOf(Myself),STATE_ILLUSIONS)
	  StateCheck(SeventhNearestEnemyOf(Myself),STATE_ILLUSIONS)
	  StateCheck(EighthNearestEnemyOf(Myself),STATE_ILLUSIONS)
	  StateCheck(NinthNearestEnemyOf(Myself),STATE_ILLUSIONS)
	  StateCheck(TenthNearestEnemyOf(Myself),STATE_ILLUSIONS)
	  See([GOODCUTOFF.0.0.0.0.ILLUSIONARY])
THEN
	RESPONSE #100
		ReallyForceSpellRES("RR#DDETI",Myself) // Detect Illusion
		DisplayStringHead(Myself,31858) // Detects Illusion
		Continue()
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
        AttackedBy([GOODCUTOFF],DEFAULT)
        !Allegiance(Myself,GOODCUTOFF)
	CheckStatGT(LastAttackerOf(Myself),0,PROTECTION_FROM_EVIL)
        !See(NearestEnemyOf(Myself))
        !Race(LastAttackerOf(Myself),LICH)
        !Race(LastAttackerOf(Myself),RAKSHASA)
        Range(LastAttackerOf(Myself),10)
	CheckStatLT(LastSeenBy(Myself),100,RESISTPOISON)
        !Class(LastSeenBy(Myself),MONK)
        !Class(LastSeenBy(Myself),DRUID_ALL)
        !Kit(LastSeenBy(Myself),CAVALIER)
	!HasItemEquiped("CLCK26",LastSeenBy(Myself)) // Cloak of Mirroring
	!HasItemEquiped("AMUL22",LastSeenBy(Myself)) // Periapt of Proof Against Poison
	!HasItemEquiped("HELM30",LastSeenBy(Myself)) // Thieves' Hood
	!HasItemEquiped("RING39",LastSeenBy(Myself)) // Ring of Gaxx
	!HasItemEquiped("RING46",LastSeenBy(Myself)) // Ring of Anti-Venom
	!HasItemEquiped("SW1H34",LastSeenBy(Myself)) // Bastard Sword +1: Albruin
	!CheckStat(LastSeenBy(Myself),2,WIZARD_SPELL_TRAP)
        !CheckStat(LastSeenBy(Myself),6,WIZARD_SPELL_IMMUNITY) // Spell Immunity: Evocation
  RandomNum(3,1)
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Cast","LOCALS",6)
		SetGlobalTimer("RR#CDmg","LOCALS",12) // This timer inidicates continual damage spells (i.e. Cloudkill and Ice Storm)
		DisplayStringHead(Myself,@663) // Foolish mortal, your petty trickery cannot deceive me.
		ForceSpellRES("RR#WI502",Myself) // Cloudkill
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
        Allegiance(Myself,GOODCUTOFF)
        !Range([0.0.DEMONIC.0.0.0.MASK_LAWFUL],6) // do not disrupt the Blood War
        AttackedBy([GOODCUTOFF],DEFAULT)
	CheckStatGT(LastAttackerOf(Myself),0,PROTECTION_FROM_EVIL)
        !See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("RR#Cast","LOCALS",6)
		DisplayStringHead(Myself,@724) // Bah, mortals are ever fools!
                ForceSpellRES("RR#TELAW",Myself) // Teleport away
		SetInterrupt(TRUE)
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	Exists(LastAttackerOf(Myself))
	!Range(LastAttackerOf(Myself),6)
        InMyArea(LastAttackerOf(Myself))
	OR(2)
          !Allegiance(LastAttackerOf(Myself),GOODCUTOFF) // don't teleport to attacking party members if green circled
          !Allegiance(Myself,GOODCUTOFF)
	!StateCheck(LastAttackerOf(Myself),STATE_REALLY_DEAD)
        !StateCheck(LastAttackerOf(Myself),STATE_INVISIBLE)
        CheckStat(LastAttackerOf(Myself),0,PROTECTION_FROM_EVIL)
	!CheckStatGT(LastSeenBy(Myself),3,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no PfMW
        CheckStatLT(LastAttackerOf(Myself),100,RESISTSLASHING)
	!See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("RR#Cast","LOCALS",6)
                ForceSpellRES("RR#TELWE",LastAttackerOf(Myself)) // Teleport Without Error
		SetInterrupt(TRUE)
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	StateCheck(NearestEnemyOf(Myself),STATE_IMMOBILE) // Note: these blocks are for high priority teleporting to immobile targets
        !Range([0.0.DEMONIC.0.0.0.MASK_LAWFUL],6) // but they should not disrupt the Blood War
	!CheckStatGT(NearestEnemyOf(Myself),2,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Absolute Immunity or PfMW
        CheckStatLT(NearestEnemyOf(Myself),100,RESISTSLASHING)
	!Range(NearestEnemyOf(Myself),6)
	See(NearestEnemyOf(Myself))
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("RR#Cast","LOCALS",6)
                ForceSpellRES("RR#TELWE",LastSeenBy(Myself)) // Teleport Without Error
		SetInterrupt(TRUE)
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	!StateCheck(NearestEnemyOf(Myself),STATE_IMMOBILE) // Note: only teleport to other targets if the nearest enemy is not already immobile
	StateCheck(SecondNearestEnemyOf(Myself),STATE_IMMOBILE) // Note: these blocks are for high priority teleporting to immobile targets
        !Range([0.0.DEMONIC.0.0.0.MASK_LAWFUL],6) // but they should not disrupt the Blood War
	!CheckStatGT(SecondNearestEnemyOf(Myself),2,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Absolute Immunity or PfMW
        CheckStatLT(SecondNearestEnemyOf(Myself),100,RESISTSLASHING)
	!Range(SecondNearestEnemyOf(Myself),6)
	See(SecondNearestEnemyOf(Myself))
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("RR#Cast","LOCALS",6)
                ForceSpellRES("RR#TELWE",LastSeenBy(Myself)) // Teleport Without Error
		SetInterrupt(TRUE)
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	!StateCheck(NearestEnemyOf(Myself),STATE_IMMOBILE) // Note: only teleport to other targets if the nearest enemy is not already immobile
	StateCheck(ThirdNearestEnemyOf(Myself),STATE_IMMOBILE) // Note: these blocks are for high priority teleporting to immobile targets
        !Range([0.0.DEMONIC.0.0.0.MASK_LAWFUL],6) // but they should not disrupt the Blood War
	!CheckStatGT(ThirdNearestEnemyOf(Myself),2,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Absolute Immunity or PfMW
        CheckStatLT(ThirdNearestEnemyOf(Myself),100,RESISTSLASHING)
	!Range(ThirdNearestEnemyOf(Myself),6)
	See(ThirdNearestEnemyOf(Myself))
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("RR#Cast","LOCALS",6)
                ForceSpellRES("RR#TELWE",LastSeenBy(Myself)) // Teleport Without Error
		SetInterrupt(TRUE)
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	!StateCheck(NearestEnemyOf(Myself),STATE_IMMOBILE) // Note: only teleport to other targets if the nearest enemy is not already immobile
	StateCheck(FourthNearestEnemyOf(Myself),STATE_IMMOBILE) // Note: these blocks are for high priority teleporting to immobile targets
        !Range([0.0.DEMONIC.0.0.0.MASK_LAWFUL],6) // but they should not disrupt the Blood War
	!CheckStatGT(FourthNearestEnemyOf(Myself),2,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Absolute Immunity or PfMW
        CheckStatLT(FourthNearestEnemyOf(Myself),100,RESISTSLASHING)
	!Range(FourthNearestEnemyOf(Myself),6)
	See(FourthNearestEnemyOf(Myself))
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("RR#Cast","LOCALS",6)
                ForceSpellRES("RR#TELWE",LastSeenBy(Myself)) // Teleport Without Error
		SetInterrupt(TRUE)
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	!StateCheck(NearestEnemyOf(Myself),STATE_IMMOBILE) // Note: only teleport to other targets if the nearest enemy is not already immobile
	StateCheck(FifthNearestEnemyOf(Myself),STATE_IMMOBILE) // Note: these blocks are for high priority teleporting to immobile targets
        !Range([0.0.DEMONIC.0.0.0.MASK_LAWFUL],6) // but they should not disrupt the Blood War
	!CheckStatGT(FifthNearestEnemyOf(Myself),2,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Absolute Immunity or PfMW
        CheckStatLT(FifthNearestEnemyOf(Myself),100,RESISTSLASHING)
	!Range(FifthNearestEnemyOf(Myself),6)
	See(FifthNearestEnemyOf(Myself))
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("RR#Cast","LOCALS",6)
                ForceSpellRES("RR#TELWE",LastSeenBy(Myself)) // Teleport Without Error
		SetInterrupt(TRUE)
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	!StateCheck(NearestEnemyOf(Myself),STATE_IMMOBILE) // Note: only teleport to other targets if the nearest enemy is not already immobile
	StateCheck(SixthNearestEnemyOf(Myself),STATE_IMMOBILE) // Note: these blocks are for high priority teleporting to immobile targets
        !Range([0.0.DEMONIC.0.0.0.MASK_LAWFUL],6) // but they should not disrupt the Blood War
	!CheckStatGT(SixthNearestEnemyOf(Myself),2,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Absolute Immunity or PfMW
        CheckStatLT(SixthNearestEnemyOf(Myself),100,RESISTSLASHING)
	!Range(SixthNearestEnemyOf(Myself),6)
	See(SixthNearestEnemyOf(Myself))
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("RR#Cast","LOCALS",6)
                ForceSpellRES("RR#TELWE",LastSeenBy(Myself)) // Teleport Without Error
		SetInterrupt(TRUE)
END

IF
        !GlobalTimerNotExpired("RR#Taunt","LOCALS")
        !StateCheck(Myself,STATE_INVISIBLE)
	!See([0.0.DEMONIC.0.0.0.MASK_LAWFUL])
        !See(NearestEnemyOf(Myself))
	See([0.HUMANOID.0.INNOCENT])
THEN
	RESPONSE #40
		SetInterrupt(FALSE)
		SetGlobalTimer("RR#Taunt","LOCALS",12)
		DisplayStringHead(Myself,@654) // I shall feast upon your soul, mortal!
                DisplayStringHead(LastSeenBy(Myself),@666) // No, please! Aaaaaargh!
		ApplySpellRES("RR#FEAR",LastSeenBy(Myself)) // Fear
		SetInterrupt(TRUE)
	RESPONSE #30
		SetInterrupt(FALSE)
		SetGlobalTimer("RR#Taunt","LOCALS",12)
                DisplayStringHead(LastSeenBy(Myself),@668) // A d-demon!? I must flee!
		ApplySpellRES("RR#FEAR",LastSeenBy(Myself)) // Fear
		DisplayStringHead(Myself,@655) // There is no escape!
		SetInterrupt(TRUE)
	RESPONSE #30
		SetInterrupt(FALSE)
		SetGlobalTimer("RR#Taunt","LOCALS",12)
		DisplayStringHead(Myself,@656) // The Abyss awaits you! Hahahaha!
                DisplayStringHead(LastSeenBy(Myself),@669) // N-no! Stay back!
		ApplySpellRES("RR#FEAR",LastSeenBy(Myself)) // Fear
		SetInterrupt(TRUE)
END

IF
	!GlobalTimerNotExpired("RR#Tactic","LOCALS")
        !GlobalTimerNotExpired("RR#CDmg","LOCALS") // This timer inidicates continual damage spells (i.e. Cloudkill and Ice Storm)
	!See([EVILCUTOFF])
	RandomNum(5,1)
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Tactic","LOCALS",6) // special case, shorter timers for continual damage spells
                SetGlobal("RR#Bhave","LOCALS",2) // Cloudkill
END

IF
	!GlobalTimerNotExpired("RR#Tactic","LOCALS")
	RandomNum(6,1)
THEN
	RESPONSE #100
                SetGlobal("RR#Bhave","LOCALS",9) // Teleport
		SetGlobalTimer("RR#Tactic","LOCALS",12)
END

IF
	!GlobalTimerNotExpired("RR#Tactic","LOCALS")
	OR(2)
	  RandomNum(4,1)
          !Range(NearestEnemyOf(Myself),6)
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Tactic","LOCALS",12)
                SetGlobal("RR#Bhave","LOCALS",1) // Curse
END

IF
	!GlobalTimerNotExpired("RR#Tactic","LOCALS")
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Tactic","LOCALS",12)
                SetGlobal("RR#Bhave","LOCALS",0) // Cause Serious Wounds
END

IF
        Global("RR#Bhave","LOCALS",9) // Teleport to the optimal target
        !Range([0.0.DEMONIC.0.0.0.MASK_LAWFUL],6) // but do not disrupt the Blood War
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	!StateCheck([GOODCUTOFF.0.0.MAGE],STATE_PANIC)
	!StateCheck([GOODCUTOFF.0.0.MAGE],STATE_MIRRORIMAGE)
        CheckStatLT([GOODCUTOFF.0.0.MAGE],1,STONESKINS)
	!CheckStatGT([GOODCUTOFF.0.0.MAGE],2,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Absolute Immunity or PfMW
        CheckStatLT([GOODCUTOFF.0.0.MAGE],100,RESISTSLASHING)
	!Range([GOODCUTOFF.0.0.MAGE],6)
	See([GOODCUTOFF.0.0.MAGE])
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("RR#Cast","LOCALS",6)
                ForceSpellRES("RR#TELWE",LastSeenBy(Myself)) // Teleport Without Error
		AttackOneRound(LastSeenBy(Myself))
		SetInterrupt(TRUE)
END

IF
        Global("RR#Bhave","LOCALS",9) // Teleport to the optimal target
        !Range([0.0.DEMONIC.0.0.0.MASK_LAWFUL],6) // but do not disrupt the Blood War
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	!StateCheck([GOODCUTOFF.0.0.MAGE_THIEF],STATE_PANIC)
	!StateCheck([GOODCUTOFF.0.0.MAGE_THIEF],STATE_MIRRORIMAGE)
        CheckStatLT([GOODCUTOFF.0.0.MAGE_THIEF],1,STONESKINS)
	!CheckStatGT([GOODCUTOFF.0.0.MAGE_THIEF],2,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Absolute Immunity or PfMW
        CheckStatLT([GOODCUTOFF.0.0.MAGE_THIEF],100,RESISTSLASHING)
	!Range([GOODCUTOFF.0.0.MAGE_THIEF],6)
	See([GOODCUTOFF.0.0.MAGE_THIEF])
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("RR#Cast","LOCALS",6)
                ForceSpellRES("RR#TELWE",LastSeenBy(Myself)) // Teleport Without Error
		AttackOneRound(LastSeenBy(Myself))
		SetInterrupt(TRUE)
END

IF
        Global("RR#Bhave","LOCALS",9) // Teleport to the optimal target
        !Range([0.0.DEMONIC.0.0.0.MASK_LAWFUL],6) // but do not disrupt the Blood War
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	!StateCheck([GOODCUTOFF.0.0.CLERIC_MAGE],STATE_PANIC)
	!StateCheck([GOODCUTOFF.0.0.CLERIC_MAGE],STATE_MIRRORIMAGE)
        CheckStatLT([GOODCUTOFF.0.0.CLERIC_MAGE],1,STONESKINS)
	!CheckStatGT([GOODCUTOFF.0.0.CLERIC_MAGE],2,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Absolute Immunity or PfMW
        CheckStatLT([GOODCUTOFF.0.0.CLERIC_MAGE],100,RESISTSLASHING)
	!Range([GOODCUTOFF.0.0.CLERIC_MAGE],6)
	See([GOODCUTOFF.0.0.CLERIC_MAGE])
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("RR#Cast","LOCALS",6)
                ForceSpellRES("RR#TELWE",LastSeenBy(Myself)) // Teleport Without Error
		AttackOneRound(LastSeenBy(Myself))
		SetInterrupt(TRUE)
END

IF
        Global("RR#Bhave","LOCALS",9) // Teleport to the optimal target
        !Range([0.0.DEMONIC.0.0.0.MASK_LAWFUL],6) // but do not disrupt the Blood War
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	!StateCheck([GOODCUTOFF.0.0.BARD_ALL],STATE_PANIC)
	!StateCheck([GOODCUTOFF.0.0.BARD_ALL],STATE_MIRRORIMAGE)
        CheckStatLT([GOODCUTOFF.0.0.BARD_ALL],1,STONESKINS)
	!CheckStatGT([GOODCUTOFF.0.0.BARD_ALL],2,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Absolute Immunity or PfMW
        CheckStatLT([GOODCUTOFF.0.0.BARD_ALL],100,RESISTSLASHING)
	!Range([GOODCUTOFF.0.0.BARD_ALL],6)
	See([GOODCUTOFF.0.0.BARD_ALL])
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("RR#Cast","LOCALS",6)
                ForceSpellRES("RR#TELWE",LastSeenBy(Myself)) // Teleport Without Error
		AttackOneRound(LastSeenBy(Myself))
		SetInterrupt(TRUE)
END

IF
        Global("RR#Bhave","LOCALS",9) // Teleport to the optimal target
        !Range([0.0.DEMONIC.0.0.0.MASK_LAWFUL],6) // but do not disrupt the Blood War
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	!StateCheck(WorstAC([PC]),STATE_PANIC)
	!StateCheck(WorstAC([PC]),STATE_MIRRORIMAGE)
        CheckStatLT(WorstAC([PC]),1,STONESKINS)
	!CheckStatGT(WorstAC([PC]),2,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Absolute Immunity or PfMW
        CheckStatLT(WorstAC([PC]),100,RESISTSLASHING)
	!Range(WorstAC([PC]),6)
	See(WorstAC([PC]))
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("RR#Cast","LOCALS",6)
                ForceSpellRES("RR#TELWE",LastSeenBy(Myself)) // Teleport Without Error
		AttackOneRound(LastSeenBy(Myself))
		SetInterrupt(TRUE)
END

IF
        Global("RR#Bhave","LOCALS",9) // Teleport to the optimal target
        !Range([0.0.DEMONIC.0.0.0.MASK_LAWFUL],6) // but do not disrupt the Blood War
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	!StateCheck(MostDamagedOf([PC]),STATE_PANIC)
	!StateCheck(MostDamagedOf([PC]),STATE_MIRRORIMAGE)
        CheckStatLT(MostDamagedOf([PC]),1,STONESKINS)
	!CheckStatGT(MostDamagedOf([PC]),2,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Absolute Immunity or PfMW
        CheckStatLT(MostDamagedOf([PC]),100,RESISTSLASHING)
	!Range(MostDamagedOf([PC]),6)
	See(MostDamagedOf([PC]))
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("RR#Cast","LOCALS",6)
                ForceSpellRES("RR#TELWE",LastSeenBy(Myself)) // Teleport Without Error
		AttackOneRound(LastSeenBy(Myself))
		SetInterrupt(TRUE)
END

IF
        Global("RR#Bhave","LOCALS",2)
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
        !See([EVILCUTOFF])
	See(NearestEnemyOf(Myself))
	CheckStatLT(LastSeenBy(Myself),50,RESISTMAGIC)
	CheckStatLT(LastSeenBy(Myself),100,RESISTPOISON)
        !Class(LastSeenBy(Myself),MONK)
        !Class(LastSeenBy(Myself),DRUID_ALL)
        !Kit(LastSeenBy(Myself),CAVALIER)
	!HasItemEquiped("CLCK26",LastSeenBy(Myself)) // Cloak of Mirroring
	!HasItemEquiped("AMUL22",LastSeenBy(Myself)) // Periapt of Proof Against Poison
	!HasItemEquiped("HELM30",LastSeenBy(Myself)) // Thieves' Hood
	!HasItemEquiped("RING39",LastSeenBy(Myself)) // Ring of Gaxx
	!HasItemEquiped("RING46",LastSeenBy(Myself)) // Ring of Anti-Venom
	!HasItemEquiped("SW1H34",LastSeenBy(Myself)) // Bastard Sword +1: Albruin
	!CheckStat(LastSeenBy(Myself),2,WIZARD_SPELL_TRAP)
        !CheckStat(LastSeenBy(Myself),6,WIZARD_SPELL_IMMUNITY) // Spell Immunity: Evocation
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Cast","LOCALS",6)
		SetGlobalTimer("RR#CDmg","LOCALS",12) // This timer inidicates continual damage spells (i.e. Cloudkill and Ice Storm)
		ForceSpellRES("RR#WI502",LastSeenBy(Myself)) // Cloudkill
END

IF
        Global("RR#Bhave","LOCALS",2)
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
        !See([EVILCUTOFF])
	See(SecondNearestEnemyOf(Myself))
	CheckStatLT(LastSeenBy(Myself),50,RESISTMAGIC)
	CheckStatLT(LastSeenBy(Myself),100,RESISTPOISON)
        !Class(LastSeenBy(Myself),MONK)
        !Class(LastSeenBy(Myself),DRUID_ALL)
        !Kit(LastSeenBy(Myself),CAVALIER)
	!HasItemEquiped("CLCK26",LastSeenBy(Myself)) // Cloak of Mirroring
	!HasItemEquiped("AMUL22",LastSeenBy(Myself)) // Periapt of Proof Against Poison
	!HasItemEquiped("HELM30",LastSeenBy(Myself)) // Thieves' Hood
	!HasItemEquiped("RING39",LastSeenBy(Myself)) // Ring of Gaxx
	!HasItemEquiped("RING46",LastSeenBy(Myself)) // Ring of Anti-Venom
	!HasItemEquiped("SW1H34",LastSeenBy(Myself)) // Bastard Sword +1: Albruin
	!CheckStat(LastSeenBy(Myself),2,WIZARD_SPELL_TRAP)
        !CheckStat(LastSeenBy(Myself),6,WIZARD_SPELL_IMMUNITY) // Spell Immunity: Evocation
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Cast","LOCALS",6)
		SetGlobalTimer("RR#CDmg","LOCALS",12) // This timer inidicates continual damage spells (i.e. Cloudkill and Ice Storm)
		ForceSpellRES("RR#WI502",LastSeenBy(Myself)) // Cloudkill
END

IF
        Global("RR#Bhave","LOCALS",2)
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
        !See([EVILCUTOFF])
	See(ThirdNearestEnemyOf(Myself))
	CheckStatLT(LastSeenBy(Myself),50,RESISTMAGIC)
	CheckStatLT(LastSeenBy(Myself),100,RESISTPOISON)
        !Class(LastSeenBy(Myself),MONK)
        !Class(LastSeenBy(Myself),DRUID_ALL)
        !Kit(LastSeenBy(Myself),CAVALIER)
	!HasItemEquiped("CLCK26",LastSeenBy(Myself)) // Cloak of Mirroring
	!HasItemEquiped("AMUL22",LastSeenBy(Myself)) // Periapt of Proof Against Poison
	!HasItemEquiped("HELM30",LastSeenBy(Myself)) // Thieves' Hood
	!HasItemEquiped("RING39",LastSeenBy(Myself)) // Ring of Gaxx
	!HasItemEquiped("RING46",LastSeenBy(Myself)) // Ring of Anti-Venom
	!HasItemEquiped("SW1H34",LastSeenBy(Myself)) // Bastard Sword +1: Albruin
	!CheckStat(LastSeenBy(Myself),2,WIZARD_SPELL_TRAP)
        !CheckStat(LastSeenBy(Myself),6,WIZARD_SPELL_IMMUNITY) // Spell Immunity: Evocation
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Cast","LOCALS",6)
		SetGlobalTimer("RR#CDmg","LOCALS",12) // This timer inidicates continual damage spells (i.e. Cloudkill and Ice Storm)
		ForceSpellRES("RR#WI502",LastSeenBy(Myself)) // Cloudkill
END

IF
        Global("RR#Bhave","LOCALS",2)
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
        !See([EVILCUTOFF])
	See(FourthNearestEnemyOf(Myself))
	CheckStatLT(LastSeenBy(Myself),50,RESISTMAGIC)
	CheckStatLT(LastSeenBy(Myself),100,RESISTPOISON)
        !Class(LastSeenBy(Myself),MONK)
        !Class(LastSeenBy(Myself),DRUID_ALL)
        !Kit(LastSeenBy(Myself),CAVALIER)
	!HasItemEquiped("CLCK26",LastSeenBy(Myself)) // Cloak of Mirroring
	!HasItemEquiped("AMUL22",LastSeenBy(Myself)) // Periapt of Proof Against Poison
	!HasItemEquiped("HELM30",LastSeenBy(Myself)) // Thieves' Hood
	!HasItemEquiped("RING39",LastSeenBy(Myself)) // Ring of Gaxx
	!HasItemEquiped("RING46",LastSeenBy(Myself)) // Ring of Anti-Venom
	!HasItemEquiped("SW1H34",LastSeenBy(Myself)) // Bastard Sword +1: Albruin
	!CheckStat(LastSeenBy(Myself),2,WIZARD_SPELL_TRAP)
        !CheckStat(LastSeenBy(Myself),6,WIZARD_SPELL_IMMUNITY) // Spell Immunity: Evocation
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Cast","LOCALS",6)
		SetGlobalTimer("RR#CDmg","LOCALS",12) // This timer inidicates continual damage spells (i.e. Cloudkill and Ice Storm)
		ForceSpellRES("RR#WI502",LastSeenBy(Myself)) // Cloudkill
END

IF
        Global("RR#Bhave","LOCALS",2)
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
        !See([EVILCUTOFF])
	See(FifthNearestEnemyOf(Myself))
	CheckStatLT(LastSeenBy(Myself),50,RESISTMAGIC)
	CheckStatLT(LastSeenBy(Myself),100,RESISTPOISON)
        !Class(LastSeenBy(Myself),MONK)
        !Class(LastSeenBy(Myself),DRUID_ALL)
        !Kit(LastSeenBy(Myself),CAVALIER)
	!HasItemEquiped("CLCK26",LastSeenBy(Myself)) // Cloak of Mirroring
	!HasItemEquiped("AMUL22",LastSeenBy(Myself)) // Periapt of Proof Against Poison
	!HasItemEquiped("HELM30",LastSeenBy(Myself)) // Thieves' Hood
	!HasItemEquiped("RING39",LastSeenBy(Myself)) // Ring of Gaxx
	!HasItemEquiped("RING46",LastSeenBy(Myself)) // Ring of Anti-Venom
	!HasItemEquiped("SW1H34",LastSeenBy(Myself)) // Bastard Sword +1: Albruin
	!CheckStat(LastSeenBy(Myself),2,WIZARD_SPELL_TRAP)
        !CheckStat(LastSeenBy(Myself),6,WIZARD_SPELL_IMMUNITY) // Spell Immunity: Evocation
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Cast","LOCALS",6)
		SetGlobalTimer("RR#CDmg","LOCALS",12) // This timer inidicates continual damage spells (i.e. Cloudkill and Ice Storm)
		ForceSpellRES("RR#WI502",LastSeenBy(Myself)) // Cloudkill
END

IF
        Global("RR#Bhave","LOCALS",2)
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
        !See([EVILCUTOFF])
	See(SixthNearestEnemyOf(Myself))
	CheckStatLT(LastSeenBy(Myself),50,RESISTMAGIC)
	CheckStatLT(LastSeenBy(Myself),100,RESISTPOISON)
        !Class(LastSeenBy(Myself),MONK)
        !Class(LastSeenBy(Myself),DRUID_ALL)
        !Kit(LastSeenBy(Myself),CAVALIER)
	!HasItemEquiped("CLCK26",LastSeenBy(Myself)) // Cloak of Mirroring
	!HasItemEquiped("AMUL22",LastSeenBy(Myself)) // Periapt of Proof Against Poison
	!HasItemEquiped("HELM30",LastSeenBy(Myself)) // Thieves' Hood
	!HasItemEquiped("RING39",LastSeenBy(Myself)) // Ring of Gaxx
	!HasItemEquiped("RING46",LastSeenBy(Myself)) // Ring of Anti-Venom
	!HasItemEquiped("SW1H34",LastSeenBy(Myself)) // Bastard Sword +1: Albruin
	!CheckStat(LastSeenBy(Myself),2,WIZARD_SPELL_TRAP)
        !CheckStat(LastSeenBy(Myself),6,WIZARD_SPELL_IMMUNITY) // Spell Immunity: Evocation
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Cast","LOCALS",6)
		SetGlobalTimer("RR#CDmg","LOCALS",12) // This timer inidicates continual damage spells (i.e. Cloudkill and Ice Storm)
		ForceSpellRES("RR#WI502",LastSeenBy(Myself)) // Cloudkill
END

IF
        Global("RR#Bhave","LOCALS",1)
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	See(NearestEnemyOf(Myself))
        !StateCheck(LastSeenBy(Myself),STATE_CHANTBAD)
        !StateCheck(LastSeenBy(Myself),STATE_DISABLED)
	CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
	!CheckStat(LastSeenBy(Myself),2,WIZARD_SPELL_IMMUNITY) // Spell Immunity: Conjuration
	CheckStatLT(LastSeenBy(Myself),50,RESISTMAGIC)
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Cast","LOCALS",6)
		ForceSpellRES("RR#CURSE",LastSeenBy(Myself)) // Curse
END

IF
        Global("RR#Bhave","LOCALS",1)
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	See(SecondNearestEnemyOf(Myself))
        !StateCheck(LastSeenBy(Myself),STATE_CHANTBAD)
        !StateCheck(LastSeenBy(Myself),STATE_DISABLED)
	CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
	!CheckStat(LastSeenBy(Myself),2,WIZARD_SPELL_IMMUNITY) // Spell Immunity: Conjuration
	CheckStatLT(LastSeenBy(Myself),50,RESISTMAGIC)
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Cast","LOCALS",6)
		ForceSpellRES("RR#CURSE",LastSeenBy(Myself)) // Curse
END

IF
        Global("RR#Bhave","LOCALS",1)
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	See(ThirdNearestEnemyOf(Myself))
        !StateCheck(LastSeenBy(Myself),STATE_CHANTBAD)
        !StateCheck(LastSeenBy(Myself),STATE_DISABLED)
	CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
	!CheckStat(LastSeenBy(Myself),2,WIZARD_SPELL_IMMUNITY) // Spell Immunity: Conjuration
	CheckStatLT(LastSeenBy(Myself),50,RESISTMAGIC)
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Cast","LOCALS",6)
		ForceSpellRES("RR#CURSE",LastSeenBy(Myself)) // Curse
END

IF
        Global("RR#Bhave","LOCALS",1)
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	See(FourthNearestEnemyOf(Myself))
        !StateCheck(LastSeenBy(Myself),STATE_CHANTBAD)
        !StateCheck(LastSeenBy(Myself),STATE_DISABLED)
	CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
	!CheckStat(LastSeenBy(Myself),2,WIZARD_SPELL_IMMUNITY) // Spell Immunity: Conjuration
	CheckStatLT(LastSeenBy(Myself),50,RESISTMAGIC)
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Cast","LOCALS",6)
		ForceSpellRES("RR#CURSE",LastSeenBy(Myself)) // Curse
END

IF
        Global("RR#Bhave","LOCALS",1)
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	See(FifthNearestEnemyOf(Myself))
        !StateCheck(LastSeenBy(Myself),STATE_CHANTBAD)
        !StateCheck(LastSeenBy(Myself),STATE_DISABLED)
	CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
	!CheckStat(LastSeenBy(Myself),2,WIZARD_SPELL_IMMUNITY) // Spell Immunity: Conjuration
	CheckStatLT(LastSeenBy(Myself),50,RESISTMAGIC)
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Cast","LOCALS",6)
		ForceSpellRES("RR#CURSE",LastSeenBy(Myself)) // Curse
END

IF
        Global("RR#Bhave","LOCALS",1)
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	See(SixthNearestEnemyOf(Myself))
        !StateCheck(LastSeenBy(Myself),STATE_CHANTBAD)
        !StateCheck(LastSeenBy(Myself),STATE_DISABLED)
	CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
	!CheckStat(LastSeenBy(Myself),2,WIZARD_SPELL_IMMUNITY) // Spell Immunity: Conjuration
	CheckStatLT(LastSeenBy(Myself),50,RESISTMAGIC)
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Cast","LOCALS",6)
		ForceSpellRES("RR#CURSE",LastSeenBy(Myself)) // Curse
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	See(NearestEnemyOf(Myself))
	Range(LastSeenBy(Myself),4)
	!General(LastSeenBy(Myself),UNDEAD)
        !Race(LastSeenBy(Myself),SLIME)
	!Race(LastSeenBy(Myself),DEMONIC)
	!Race(LastSeenBy(Myself),GOLEM)
	!HasItemEquiped("CLCK26",LastSeenBy(Myself)) // Cloak of Mirroring
	CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
	!CheckStat(LastSeenBy(Myself),2,WIZARD_SPELL_TRAP)
	!CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_DEFLECTION)
	!CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_TURNING)
	!CheckStat(LastSeenBy(Myself),1,CLERIC_SHIELD_OF_THE_ARCHONS)
        !CheckStat(LastSeenBy(Myself),7,WIZARD_SPELL_IMMUNITY)  // Spell Immunity: Necromancy
	CheckStatLT(LastSeenBy(Myself),50,RESISTMAGIC)
	CheckStatLT(LastSeenBy(Myself),50,MAGICDAMAGERESISTANCE)
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Cast","LOCALS",6)
		ForceSpellRES("RR#DCSW",LastSeenBy(Myself)) // Cause Serious Wounds
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	See(SecondNearestEnemyOf(Myself))
	Range(LastSeenBy(Myself),4)
	!General(LastSeenBy(Myself),UNDEAD)
        !Race(LastSeenBy(Myself),SLIME)
	!Race(LastSeenBy(Myself),DEMONIC)
	!Race(LastSeenBy(Myself),GOLEM)
	!HasItemEquiped("CLCK26",LastSeenBy(Myself)) // Cloak of Mirroring
	CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
	!CheckStat(LastSeenBy(Myself),2,WIZARD_SPELL_TRAP)
	!CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_DEFLECTION)
	!CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_TURNING)
	!CheckStat(LastSeenBy(Myself),1,CLERIC_SHIELD_OF_THE_ARCHONS)
        !CheckStat(LastSeenBy(Myself),7,WIZARD_SPELL_IMMUNITY)  // Spell Immunity: Necromancy
	CheckStatLT(LastSeenBy(Myself),50,RESISTMAGIC)
	CheckStatLT(LastSeenBy(Myself),50,MAGICDAMAGERESISTANCE)
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Cast","LOCALS",6)
		ForceSpellRES("RR#DCSW",LastSeenBy(Myself)) // Cause Serious Wounds
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	See(ThirdNearestEnemyOf(Myself))
	Range(LastSeenBy(Myself),4)
	!General(LastSeenBy(Myself),UNDEAD)
        !Race(LastSeenBy(Myself),SLIME)
	!Race(LastSeenBy(Myself),DEMONIC)
	!Race(LastSeenBy(Myself),GOLEM)
	!HasItemEquiped("CLCK26",LastSeenBy(Myself)) // Cloak of Mirroring
	CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
	!CheckStat(LastSeenBy(Myself),2,WIZARD_SPELL_TRAP)
	!CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_DEFLECTION)
	!CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_TURNING)
	!CheckStat(LastSeenBy(Myself),1,CLERIC_SHIELD_OF_THE_ARCHONS)
        !CheckStat(LastSeenBy(Myself),7,WIZARD_SPELL_IMMUNITY)  // Spell Immunity: Necromancy
	CheckStatLT(LastSeenBy(Myself),50,RESISTMAGIC)
	CheckStatLT(LastSeenBy(Myself),50,MAGICDAMAGERESISTANCE)
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Cast","LOCALS",6)
		ForceSpellRES("RR#DCSW",LastSeenBy(Myself)) // Cause Serious Wounds
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	See(FourthNearestEnemyOf(Myself))
	Range(LastSeenBy(Myself),4)
	!General(LastSeenBy(Myself),UNDEAD)
        !Race(LastSeenBy(Myself),SLIME)
	!Race(LastSeenBy(Myself),DEMONIC)
	!Race(LastSeenBy(Myself),GOLEM)
	!HasItemEquiped("CLCK26",LastSeenBy(Myself)) // Cloak of Mirroring
	CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
	!CheckStat(LastSeenBy(Myself),2,WIZARD_SPELL_TRAP)
	!CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_DEFLECTION)
	!CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_TURNING)
	!CheckStat(LastSeenBy(Myself),1,CLERIC_SHIELD_OF_THE_ARCHONS)
        !CheckStat(LastSeenBy(Myself),7,WIZARD_SPELL_IMMUNITY)  // Spell Immunity: Necromancy
	CheckStatLT(LastSeenBy(Myself),50,RESISTMAGIC)
	CheckStatLT(LastSeenBy(Myself),50,MAGICDAMAGERESISTANCE)
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Cast","LOCALS",6)
		ForceSpellRES("RR#DCSW",LastSeenBy(Myself)) // Cause Serious Wounds
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	See(FifthNearestEnemyOf(Myself))
	Range(LastSeenBy(Myself),4)
	!General(LastSeenBy(Myself),UNDEAD)
        !Race(LastSeenBy(Myself),SLIME)
	!Race(LastSeenBy(Myself),DEMONIC)
	!Race(LastSeenBy(Myself),GOLEM)
	!HasItemEquiped("CLCK26",LastSeenBy(Myself)) // Cloak of Mirroring
	CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
	!CheckStat(LastSeenBy(Myself),2,WIZARD_SPELL_TRAP)
	!CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_DEFLECTION)
	!CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_TURNING)
	!CheckStat(LastSeenBy(Myself),1,CLERIC_SHIELD_OF_THE_ARCHONS)
        !CheckStat(LastSeenBy(Myself),7,WIZARD_SPELL_IMMUNITY)  // Spell Immunity: Necromancy
	CheckStatLT(LastSeenBy(Myself),50,RESISTMAGIC)
	CheckStatLT(LastSeenBy(Myself),50,MAGICDAMAGERESISTANCE)
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Cast","LOCALS",6)
		ForceSpellRES("RR#DCSW",LastSeenBy(Myself)) // Cause Serious Wounds
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	See(SixthNearestEnemyOf(Myself))
	Range(LastSeenBy(Myself),4)
	!General(LastSeenBy(Myself),UNDEAD)
        !Race(LastSeenBy(Myself),SLIME)
	!Race(LastSeenBy(Myself),DEMONIC)
	!Race(LastSeenBy(Myself),GOLEM)
	!HasItemEquiped("CLCK26",LastSeenBy(Myself)) // Cloak of Mirroring
	CheckStat(LastSeenBy(Myself),0,MINORGLOBE)
	!CheckStat(LastSeenBy(Myself),2,WIZARD_SPELL_TRAP)
	!CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_DEFLECTION)
	!CheckStatGT(LastSeenBy(Myself),0,WIZARD_SPELL_TURNING)
	!CheckStat(LastSeenBy(Myself),1,CLERIC_SHIELD_OF_THE_ARCHONS)
        !CheckStat(LastSeenBy(Myself),7,WIZARD_SPELL_IMMUNITY)  // Spell Immunity: Necromancy
	CheckStatLT(LastSeenBy(Myself),50,RESISTMAGIC)
	CheckStatLT(LastSeenBy(Myself),50,MAGICDAMAGERESISTANCE)
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Cast","LOCALS",6)
		ForceSpellRES("RR#DCSW",LastSeenBy(Myself)) // Cause Serious Wounds
END

IF
	!CheckStatGT([0],2,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Absolute Immunity or PfMW
        CheckStatLT([0],100,RESISTSLASHING)
	See([0])
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT(NearestEnemyOf(Myself),2,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Absolute Immunity or PfMW
        CheckStatLT(NearestEnemyOf(Myself),100,RESISTSLASHING)
	See(NearestEnemyOf(Myself)) // note: this block is intended to check for opponents who may be panicked
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT(SixthNearestEnemyOf(Myself),2,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Absolute Immunity or PfMW
	!StateCheck(SixthNearestEnemyOf(Myself),STATE_PANIC)
        CheckStatLT(SixthNearestEnemyOf(Myself),100,RESISTSLASHING)
	!StateCheck(SixthNearestEnemyOf(Myself),STATE_MIRRORIMAGE)
        CheckStatLT(SixthNearestEnemyOf(Myself),1,STONESKINS)
	See(SixthNearestEnemyOf(Myself))
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT(FifthNearestEnemyOf(Myself),2,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Absolute Immunity or PfMW
	!StateCheck(FifthNearestEnemyOf(Myself),STATE_PANIC)
        CheckStatLT(FifthNearestEnemyOf(Myself),100,RESISTSLASHING)
	!StateCheck(FifthNearestEnemyOf(Myself),STATE_MIRRORIMAGE)
        CheckStatLT(FifthNearestEnemyOf(Myself),1,STONESKINS)
	See(FifthNearestEnemyOf(Myself))
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT(FourthNearestEnemyOf(Myself),2,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Absolute Immunity or PfMW
	!StateCheck(FourthNearestEnemyOf(Myself),STATE_PANIC)
        CheckStatLT(FourthNearestEnemyOf(Myself),100,RESISTSLASHING)
	!StateCheck(FourthNearestEnemyOf(Myself),STATE_MIRRORIMAGE)
        CheckStatLT(FourthNearestEnemyOf(Myself),1,STONESKINS)
	See(FourthNearestEnemyOf(Myself))
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT(ThirdNearestEnemyOf(Myself),2,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Absolute Immunity or PfMW
	!StateCheck(ThirdNearestEnemyOf(Myself),STATE_PANIC)
        CheckStatLT(ThirdNearestEnemyOf(Myself),100,RESISTSLASHING)
	!StateCheck(ThirdNearestEnemyOf(Myself),STATE_MIRRORIMAGE)
        CheckStatLT(ThirdNearestEnemyOf(Myself),1,STONESKINS)
	See(ThirdNearestEnemyOf(Myself))
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT(SecondNearestEnemyOf(Myself),2,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Absolute Immunity or PfMW
	!StateCheck(SecondNearestEnemyOf(Myself),STATE_PANIC)
        CheckStatLT(SecondNearestEnemyOf(Myself),100,RESISTSLASHING)
	!StateCheck(SecondNearestEnemyOf(Myself),STATE_MIRRORIMAGE)
        CheckStatLT(SecondNearestEnemyOf(Myself),1,STONESKINS)
	See(SecondNearestEnemyOf(Myself))
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT(NearestEnemyOf(Myself),2,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Absolute Immunity or PfMW
        !StateCheck(NearestEnemyOf(Myself),STATE_PANIC)
        CheckStatLT(NearestEnemyOf(Myself),100,RESISTSLASHING)
	!StateCheck(NearestEnemyOf(Myself),STATE_MIRRORIMAGE)
        CheckStatLT(NearestEnemyOf(Myself),1,STONESKINS)
	See(NearestEnemyOf(Myself))
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	InWeaponRange([GOODCUTOFF.0.0.DRUID_ALL]) // special case, nearby spellcaster priority targeting
	!CheckStatGT([GOODCUTOFF.0.0.DRUID_ALL],2,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Absolute Immunity or PfMW
	!StateCheck([GOODCUTOFF.0.0.DRUID_ALL],STATE_PANIC)
        CheckStatLT([GOODCUTOFF.0.0.DRUID_ALL],100,RESISTSLASHING)
        CheckStatLT([GOODCUTOFF.0.0.DRUID_ALL],1,STONESKINS)
	See([GOODCUTOFF.0.0.DRUID_ALL])
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	InWeaponRange([GOODCUTOFF.0.0.CLERIC_ALL]) // special case, nearby spellcaster priority targeting
	!CheckStatGT([GOODCUTOFF.0.0.CLERIC_ALL],2,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Absolute Immunity or PfMW
	!StateCheck([GOODCUTOFF.0.0.CLERIC_ALL],STATE_PANIC)
        CheckStatLT([GOODCUTOFF.0.0.CLERIC_ALL],100,RESISTSLASHING)
        CheckStatLT([GOODCUTOFF.0.0.CLERIC_ALL],1,STONESKINS)
	See([GOODCUTOFF.0.0.CLERIC_ALL])
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	InWeaponRange([GOODCUTOFF.0.0.MAGE_ALL]) // special case, nearby spellcaster priority targeting
	!CheckStatGT([GOODCUTOFF.0.0.MAGE_ALL],2,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Absolute Immunity or PfMW
	!StateCheck([GOODCUTOFF.0.0.MAGE_ALL],STATE_PANIC)
        CheckStatLT([GOODCUTOFF.0.0.MAGE_ALL],100,RESISTSLASHING)
        CheckStatLT([GOODCUTOFF.0.0.MAGE_ALL],1,STONESKINS)
	See([GOODCUTOFF.0.0.MAGE_ALL])
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT(SixthNearestEnemyOf(Myself),2,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Absolute Immunity or PfMW
	StateCheck(SixthNearestEnemyOf(Myself),STATE_IMMOBILE)
        CheckStatLT(SixthNearestEnemyOf(Myself),100,RESISTSLASHING)
	See(SixthNearestEnemyOf(Myself))
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT(FifthNearestEnemyOf(Myself),2,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Absolute Immunity or PfMW
	StateCheck(FifthNearestEnemyOf(Myself),STATE_IMMOBILE)
        CheckStatLT(FifthNearestEnemyOf(Myself),100,RESISTSLASHING)
	See(FifthNearestEnemyOf(Myself))
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT(FourthNearestEnemyOf(Myself),2,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Absolute Immunity or PfMW
	StateCheck(FourthNearestEnemyOf(Myself),STATE_IMMOBILE)
        CheckStatLT(FourthNearestEnemyOf(Myself),100,RESISTSLASHING)
	See(FourthNearestEnemyOf(Myself))
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT(ThirdNearestEnemyOf(Myself),2,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Absolute Immunity or PfMW
	StateCheck(ThirdNearestEnemyOf(Myself),STATE_IMMOBILE)
        CheckStatLT(ThirdNearestEnemyOf(Myself),100,RESISTSLASHING)
	See(ThirdNearestEnemyOf(Myself))
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT(SecondNearestEnemyOf(Myself),2,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Absolute Immunity or PfMW
	StateCheck(SecondNearestEnemyOf(Myself),STATE_IMMOBILE)
        CheckStatLT(SecondNearestEnemyOf(Myself),100,RESISTSLASHING)
	See(SecondNearestEnemyOf(Myself))
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT(NearestEnemyOf(Myself),2,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Absolute Immunity or PfMW
	StateCheck(NearestEnemyOf(Myself),STATE_IMMOBILE)
        CheckStatLT(NearestEnemyOf(Myself),100,RESISTSLASHING)
	See(NearestEnemyOf(Myself))
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	HitBy([GOODCUTOFF],0)
	!CheckStatGT(LastHitter(Myself),2,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Absolute Immunity or PfMW
        !StateCheck(LastHitter(Myself),STATE_PANIC)
        CheckStatLT(LastHitter(Myself),100,RESISTSLASHING)
	RandomNum(3,1)
	See(LastHitter(Myself))
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	!CheckStatGT([0.0.DEMONIC.0.0.0.MASK_LAWFUL],2,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Absolute Immunity or PfMW
        CheckStatLT([0.0.DEMONIC.0.0.0.MASK_LAWFUL],100,RESISTSLASHING)
	See([0.0.DEMONIC.0.0.0.MASK_LAWFUL])
	False()
THEN
	RESPONSE #100
		NoAction()
END

IF
	Race(LastSeenBy(Myself),DEMONIC)
        Alignment(LastSeenBy(Myself),MASK_CHAOTIC)
        Allegiance(Myself,EVILCUTOFF)
	OR(2)
          Allegiance(LastSeenBy(Myself),EVILCUTOFF)
          Allegiance(LastSeenBy(Myself),NEUTRAL)
THEN
	RESPONSE #100
		RandomWalkContinuous()
END

IF
	Race(LastSeenBy(Myself),DEMONIC)
        Alignment(LastSeenBy(Myself),MASK_CHAOTIC)
        Allegiance(Myself,GOODCUTOFF)
	OR(2)
          Allegiance(LastSeenBy(Myself),GOODCUTOFF)
          Allegiance(LastSeenBy(Myself),NEUTRAL)
THEN
	RESPONSE #100
		RandomWalkContinuous()
END

IF
        Global("RR#Smnr","LOCALS",0)
	Exists(LastSummonerOf(Myself))
        !CheckStatGT(LastSummonerOf(Myself),0,PROTECTION_FROM_EVIL)
	!HasItemEquipedReal("STAF11",LastSummonerOf(Myself)) // Staff of the Magi
	!HasItemEquiped("WA2AMU",LastSummonerOf(Myself)) // Sensate Amulet
	!HasItemEquiped("LICH",LastSummonerOf(Myself)) // Lich innate Protection from Evil
        !StateCheck(LastSummonerOf(Myself),STATE_REALLY_DEAD)
        !StateCheck(Myself,STATE_INVISIBLE)
        See(LastSummonerOf(Myself))
  !Race(LastSummonerOf(Myself),DEMONIC)
  !Alignment(LastSummonerOf(Myself),CHAOTIC_EVIL)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
                SetGlobal("RR#Smnr","LOCALS",1)
		FaceObject(LastSummonerOf(Myself))
		DisplayStringHead(Myself,@680) // You will taste the flames of the Abyss for daring to summon me here!
		SetInterrupt(TRUE)
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
        !Range(LastSeenBy(Myself),6)
        !Range([EVILCUTOFF],30)
	OR(2)
          StateCheck(LastSeenBy(Myself),STATE_PANIC)
          RandomNum(7,1)
	CheckStatLT(LastSeenBy(Myself),50,RESISTMAGIC)
	CheckStatLT(LastSeenBy(Myself),100,RESISTPOISON)
        !Class(LastSeenBy(Myself),MONK)
        !Class(LastSeenBy(Myself),DRUID_ALL)
        !Kit(LastSeenBy(Myself),CAVALIER)
	!HasItemEquiped("CLCK26",LastSeenBy(Myself)) // Cloak of Mirroring
	!HasItemEquiped("AMUL22",LastSeenBy(Myself)) // Periapt of Proof Against Poison
	!HasItemEquiped("HELM30",LastSeenBy(Myself)) // Thieves' Hood
	!HasItemEquiped("RING39",LastSeenBy(Myself)) // Ring of Gaxx
	!HasItemEquiped("RING46",LastSeenBy(Myself)) // Ring of Anti-Venom
	!HasItemEquiped("SW1H34",LastSeenBy(Myself)) // Bastard Sword +1: Albruin
	!CheckStat(LastSeenBy(Myself),2,WIZARD_SPELL_TRAP)
        !CheckStat(LastSeenBy(Myself),6,WIZARD_SPELL_IMMUNITY) // Spell Immunity: Evocation
THEN
	RESPONSE #100
		SetGlobalTimer("RR#Cast","LOCALS",6)
		SetGlobalTimer("RR#CDmg","LOCALS",12) // This timer inidicates continual damage spells (i.e. Cloudkill and Ice Storm)
		ForceSpellRES("RR#WI502",LastSeenBy(Myself)) // Cloudkill
END

IF
	!GlobalTimerNotExpired("RR#Cast","LOCALS")
	!Range(LastSeenBy(Myself),6)
        InMyArea(LastSeenBy(Myself))
	!StateCheck(LastSeenBy(Myself),STATE_REALLY_DEAD)
        !StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)
	!CheckStatGT(LastSeenBy(Myself),2,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Absolute Immunity or PfMW
        CheckStatLT(LastSeenBy(Myself),100,RESISTSLASHING)
THEN
	RESPONSE #100
		SetInterrupt(FALSE)
		SetGlobalTimer("RR#Cast","LOCALS",6)
                ForceSpellRES("RR#TELWE",LastSeenBy(Myself)) // Teleport Without Error
		SetInterrupt(TRUE)
END

IF
	!StateCheck(LastSeenBy(Myself),STATE_REALLY_DEAD)
        !StateCheck(LastSeenBy(Myself),STATE_INVISIBLE)
	!CheckStatGT(LastSeenBy(Myself),2,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // no Absolute Immunity or PfMW
        CheckStatLT(LastSeenBy(Myself),100,RESISTSLASHING)
        ActionListEmpty()
THEN
	RESPONSE #100
		AttackReevaluate(LastSeenBy(Myself),15)
END

IF
	ActionListEmpty()
THEN
	RESPONSE #100
		RandomWalkContinuous()
END
