
COPY "atweaks/bam/fl#cacoc.bam" override
     "atweaks/bam/fl#cacol.bam" override
     "atweaks/bam/fl#fienc.bam" override
     "atweaks/bam/fl#fienl.bam" override
     "atweaks/bam/fl#gatea.bam" override
     "atweaks/bam/fl#gateb.bam" override
     "atweaks/bam/fl#gatec.bam" override
     "atweaks/bam/fl#gatel.bam" override

COPY "atweaks/2da/spwi510.2da" "override/fl#cacoc.2da"
     "atweaks/2da/spwi510.2da" "override/fl#cacon.2da"
     "atweaks/2da/spwi510.2da" "override/fl#cacol.2da"
     "atweaks/2da/spwi510.2da" "override/fl#fienc.2da"
     "atweaks/2da/spwi510.2da" "override/fl#fienn.2da"
     "atweaks/2da/spwi510.2da" "override/fl#fienl.2da"
     "atweaks/2da/spwi510.2da" "override/fl#gatec.2da"
     "atweaks/2da/spwi510.2da" "override/fl#gaten.2da"
     "atweaks/2da/spwi510.2da" "override/fl#gatel.2da"

//Puke     
APPEND fl#cacoc.2da "TANARRI fl#cacoc 3"
APPEND fl#cacon.2da "TANARRI fl#cacoc 3"
APPEND fl#cacon.2da "BAATEZU fl#cacol 3"
APPEND fl#cacol.2da "BAATEZU fl#cacol 3"
APPEND fl#fienc.2da "TANARRI fl#fienc 3"
APPEND fl#fienn.2da "TANARRI fl#fienc 3"
APPEND fl#fienn.2da "BAATEZU fl#fienl 3"
APPEND fl#fienl.2da "BAATEZU fl#fienl 3"
APPEND fl#gatec.2da "TANARRI fl#gatec 3"
APPEND fl#gaten.2da "TANARRI fl#gatec 3"
APPEND fl#gaten.2da "BAATEZU fl#gatel 3"
APPEND fl#gatel.2da "BAATEZU fl#gatel 3"
     
COPY_EXISTING spcaco.eff "override/fl#cacoc.eff"
              spcaco.eff "override/fl#cacon.eff"
              spcaco.eff "override/fl#cacol.eff"
              spcaco.eff "override/fl#fienc.eff"
              spcaco.eff "override/fl#fienn.eff"
              spcaco.eff "override/fl#fienl.eff"
              spcaco.eff "override/fl#gatec.eff"
              spcaco.eff "override/fl#gaten.eff"
              spcaco.eff "override/fl#gatel.eff"
  WRITE_LONG   0x10 214 //opcode (select spell)
  WRITE_LONG   0x14 1 //target (self)
  WRITE_LONG   0x18 0 //power
  WRITE_LONG   0x1c 0 //p1
  WRITE_LONG   0x20 0 //p2
  WRITE_SHORT  0x24 1 //timing (instant)
  WRITE_LONG   0x28 0 //duration
  WRITE_SHORT  0x2c 100 //prob1
  WRITE_SHORT  0x2e 0 //prob2
  WRITE_ASCIIE 0x30 "%DEST_RES%" #8 //resource
  FOR (i=0x38;i<0x65;i+=0x4) BEGIN
    WRITE_LONG i 0 
  END

COPY_EXISTING spwi707.spl "override/fl#cacoc.spl"
              spwi707.spl "override/fl#cacol.spl"
              spwi807.spl "override/fl#fienc.spl"
              spwi807.spl "override/fl#fienl.spl"
              spwi905.spl "override/fl#gatec.spl"
              spwi905.spl "override/fl#gatel.spl"
  PATCH_IF "%DEST_RES%" STRING_MATCHES_REGEXP "fl#....c$" = 0 BEGIN
    SAY NAME1 @755
    alignment = 48
  END ELSE PATCH_IF "%DEST_RES%" STRING_MATCHES_REGEXP "fl#....l$" = 0 BEGIN
    SAY NAME1 @756
    alignment = 16
  END
  READ_LONG  0x64 ao
  READ_LONG  0x6a eo
  READ_SHORT 0x70 ei
  FOR (i=0;i<SHORT_AT 0x68;++i) BEGIN
    WRITE_ASCIIE ao + 0x28*i + 0x4  "%DEST_RES%" #8 //ability icon
    WRITE_SHORT ao + 0x28*i + 0x20 ei
    READ_SHORT  ao + 0x28*i + 0x1e ne
    FOR (j=0;j<ne;++j) BEGIN
      READ_SHORT eo + 0x30*(j + ei)       type
      READ_LONG  eo + 0x30*(j + ei) + 0x4 p1
      PATCH_IF type != 177 OR (type = 177 AND p1 != alignment) BEGIN
        DELETE_BYTES eo + 0x30*(j + ei) 0x30
        --j
        --ne
      END
      PATCH_IF type = 177 AND p1 = alignment BEGIN
        WRITE_LONG eo + 0x30*(j + ei) + 0x4 0 //don't filter by IDS
        WRITE_LONG eo + 0x30*(j + ei) + 0x8 2
      END  
    END
    WRITE_SHORT ao + 0x28*i + 0x1e ne
    ei += ne
  END

OUTER_SPRINT $array(spwi707) fl#caco
OUTER_SPRINT $array(spwi807) fl#fien
OUTER_SPRINT $array(spwi905) fl#gate
OUTER_SPRINT $array(sppr703) fl#gate
  
COPY_EXISTING spwi707.spl override
              spwi807.spl override
              spwi905.spl override
              sppr703.spl override
  //UPDATE DESC
  READ_LONG  0x64 ao
  READ_LONG  0x6a eo
  READ_SHORT 0x70 ei
  PATCH_IF "%SOURCE_RES%" STRING_EQUAL_CASE spwi905 OR "%SOURCE_RES%" STRING_EQUAL_CASE sppr703 BEGIN
    WRITE_ASCII 0x3a fl#gatea #8 //spellbook icon
  END
  FOR (i=0;i<SHORT_AT 0x68;++i) BEGIN
    PATCH_IF "%SOURCE_RES%" STRING_EQUAL_CASE spwi905 OR "%SOURCE_RES%" STRING_EQUAL_CASE sppr703 BEGIN
      WRITE_ASCII ao + 0x28*i + 0x4 fl#gateb #8 //ability icon
    END
    WRITE_BYTE  ao + 0x28*i + 0xc  5 //target (caster)   
    WRITE_BYTE  ao + 0x28*i + 0x12 0 //casting speed
    WRITE_SHORT ao + 0x28*i + 0x20 ei
    READ_SHORT  ao + 0x28*i + 0x1e ne
    FOR (j=0;j<ne;++j) BEGIN
      READ_SHORT eo + 0x30*(ei + j) type
      READ_LONG  eo + 0x30*(ei + j) p1
      PATCH_IF type = 177 BEGIN 
        WRITE_ASCIIE eo + 0x30*(ei + j) + 0x14 $array("%SOURCE_RES%") #7
        PATCH_IF p1 = 16 BEGIN
          WRITE_ASCII eo + 0x30*(ei + j) + 0x1b l #1
        END
        PATCH_IF p1 = 32 BEGIN
          WRITE_ASCII eo + 0x30*(ei + j) + 0x1b n #1
        END
        PATCH_IF p1 = 48 BEGIN
          WRITE_ASCII eo + 0x30*(ei + j) + 0x1b c #1
        END
      END
    END
  END
BUT_ONLY

COPY_EXISTING scrl81.itm override
              scrl9b.itm override
              scrl9n.itm override
  //UPDATE DESC
BUT_ONLY              
      
      
    
