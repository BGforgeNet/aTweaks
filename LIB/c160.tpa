
INCLUDE "atweaks/lib/dsandstuff.tpa"


INCLUDE "atweaks/lib/lib.tpa"

COPY_EXISTING msectype.2da override
  COUNT_2DA_ROWS 2 num_row
  INSERT_2DA_ROW num_row 2 ~Disease fl#replace~
  REPLACE "fl#replace" @1025
  DiseaseSecType = num_row - 1
  COUNT_2DA_ROWS 2 num_row
  INSERT_2DA_ROW num_row 2 ~ParalyzingFear fl#replace~
  REPLACE "fl#replace" @1027
  FearSecType = num_row - 1
  //PRETTY_PRINT_2DA
  
ADD_PROJECTILE "atweaks/pro/fl#sight.pro"                                          //INAREANP with twice the radius


//Banshee

ACTION_FOR_EACH file IN
                banshe01
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME "%file%.cre" BEGIN
    COPY_EXISTING "%file%.cre" override
      PATCH_INCLUDE "atweaks/lib/fj_cre_validity.tpp"
      PATCH_IF valid BEGIN
        PATCH_INCLUDE "atweaks/lib/fj_cre_reindex.tpp"
        LPF RR#MDCRE
          INT_VAR
            newxpv = 4000
            newhp = 56
            newac = 0
            newth = 13
            newapr = 1
            newsvd = 9
            newsvw = 11
            newsvp = 10
            newsvb = 12
            newsvs = 12
            level1 = 17
            level2 = 0
            level3 = 0
            newmr = 50
            newfr = 0
            newcr = 100
            newer = 100
            newar = 0
            newrs = 0
            newrc = 0
            newrp = 0
            newrm = 0
            newstr = 16
            newstrx = 0
            newint = 16
            newwis = 16
            newdex = 15
            newcon = 9
            newchr = 9
            newmor = 13
            newalign = 51 //CE
            
            enforce = 1
        END
        LPF RR#MDCSC
          STR_VAR
            newscript = fl#bansh
            script01 = banshe01
            script02 = wtasight
        END
      END
    BUT_ONLY
  END
END

COMPILE "atweaks/baf/fl#bansh.baf"

COPY "atweaks/spl/fl#banwa.spl" override                                           //Banshee's wail
  FOR(i=0;i<SHORT_AT 0x68;++i) BEGIN
    WRITE_SHORT LONG_AT 0x64+i*0x28+0x26 fl#sight
  END
  


// Death Knights

ACTION_FOR_EACH file IN
               deathk                                                              //Death Knight from Durlag's Tower
               _deathk                                                             //Tutu
               deathkni                                                            //Plain Death Knight used in ToB
               deck615                                                             //DoMT hitman
               uddeath                                                             //Undeadark Death Knights
               uddeath2                                                            //Underdark Death Knight leader
               gooddeat                                                            //Durlag's Tower DK mirror fiend
               _ooddeat                                                            //Tutu
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME "%file%.cre" BEGIN
    COPY_EXISTING "%file%.cre" override
      PATCH_INCLUDE "atweaks/lib/fj_cre_validity.tpp"
      PATCH_IF valid BEGIN
        PATCH_INCLUDE "atweaks/lib/fj_cre_reindex.tpp"
        PATCH_IF "%file%" STRING_MATCHES_REGEXP "_?g?ooddeat$" BEGIN
          SAY 0x8 @1020                                                            //Death Knight
          SAY 0xc @1020
        END
        REPLACE_CRE_ITEM helmnoan #0 #0 #0 none helmet                             //Helmet
        REPLACE_CRE_ITEM ring99 #0 #0 #0 none lring                                //Standard undead immunities
        LPF ADD_CRE_EFFECT INT_VAR
                          opcode = 297                                             //Immunity to Turn Undead
                          target = 1 
                          timing = 9
                          paramater2 = 1 END
        LPF DELETE_CRE_EFFECT INT_VAR opcode_to_delete = 193 END                   //Invisibility detection by script
        LPF DELETE_CRE_ITEM STR_VAR item_to_delete = "sw2h02\|sw2hdeat\|_?sw2h05" END
        LPF RR#MDCRE
          INT_VAR
            newxpv = 10000
            newhp = 90 //9d10
            newac = 0
            newth = 8
            newapr = 1
            newsvd = 6
            newsvw = 8
            newsvp = 7
            newsvb = 7
            newsvs = 9
            newmr = 75
            newlvl1 = 9 //Fighter
            newlvl2 = 20 //Mage
            newlvl3 = 8 //Cleric
            newfr = 0
            newcr = 0
            newer = 0
            newar = 0
            newrs = 0
            newrc = 0
            newrp = 0
            newrm = 0
            newstr = 18
            newstrx = 100
            newint = 18
            newwis = 16
            newdex = 18
            newcon = 11
            newchr = 10
            newmor = 17
            newgen = 4 //Undead
            newrace = 115 //Skeleton
            newclass = 17 //F_M_C
            newalign = 51
          STR_VAR
            enfc01 = deathk
            enfc02 = _deathk
            enfc03 = deathkni
            enfc04 = deck615
            enfc05 = uddeath
            enfc06 = uddeath2
            enfc07 = gooddeat
            enfc08 = _ooddeat
        END
        PATCH_IF "%file%" STRING_MATCHES_REGEXP "_?g?ooddeat$" BEGIN
          PATCH_IF "%file%" STRING_MATCHES_REGEXP "_?deathk$" = 0 BEGIN
            SPRINT script "fl#bg1kn"
          END ELSE BEGIN
            SPRINT script "fl#death"
          END
          LPF RR#MDCSC
            STR_VAR
              newscript = EVAL "%script%"
              script01 = deathkni
              script02 = wtasight
              script03 = _eathkni
              script04 = _tasight
              script05 = uddeath
              script06 = bpwtsigt
              script07 = bpmag18a
              script08 = bpdemon
          END
        END
      END
    BUT_ONLY
  END
END

COPY_EXISTING misc56.itm "override/fl#brsw.itm"                                    //Broken sword
  SAY 0x8 @1031
  SAY 0xc @1031
  SAY 0x50 @1032
  SAY 0x54 @1032

COPY ~atweaks/spl/fldkaura.spl~ override                                           // Death Knight Fear Aura
     "atweaks/itm/fl#dksw3.itm" override                                           //Two-handed sword +4
     "atweaks/itm/fl#dksw4.itm" override                                           //Bastard sword +2, +1 APR
     "atweaks/itm/fl#dksw5.itm" override                                           //Bastard sword +2, of dancing
     "atweaks/itm/fl#dksw6.itm" override                                           //Bastard sword +2, of life stealing

COPY "atweaks/cre/fl#dksw5.cre" override                                           //Dancing sword
  SAY 0x8 @1030
  SAY 0xc @1030
  WRITE_ASCII 0x248 fl#dksw5 #8

ACTION_IF !FILE_EXISTS_IN_GAME "rr#spain.spl" BEGIN
  ADD_PROJECTILE ~aTweaks/PRO/RR#SPAIN.PRO~                                        // New projectile for Symbol Pain (party friendly)
  COPY ~aTweaks/SPL/RR#SPAIN.SPL~ ~override~                                       // Pit Fiend Symbol Pain
    READ_LONG  0x64 ab_off
    READ_SHORT 0x68 ab_num
    FOR(i=0; i<ab_num; i+=1) BEGIN
      WRITE_SHORT (ab_off+i*0x28+0x26) %RR#SPAIN%                                  // use new projectile (party friendly)
    END
END

ACTION_IF !FILE_EXISTS_IN_GAME "fl#icew.spl" BEGIN
  ADD_PROJECTILE ~atweaks/pro/fl#icew.pro~                                          //Single-round Ice Storm (cue imagination stretch)
  COPY ~atweaks/spl/fl#icew.spl~ override                                           //Death Knight's Wall of Ice (sheet of falling ice)
    SAY 0x8 @1024
    FOR(i=0;i<SHORT_AT 0x68;++i) BEGIN
      WRITE_SHORT LONG_AT 0x64+i*0x28+0x26 fl#icew
    END
END
  
OUTER_SPRINT $dksw(1) sw1h42
OUTER_SPRINT $dksw(2) sw2h20
OUTER_SPRINT $dksw(3) fl#dksw3
OUTER_SPRINT $dksw(4) fl#dksw4
OUTER_SPRINT $dksw(5) fl#dksw5
OUTER_SPRINT $dksw(6) fl#dksw6


OUTER_SET r = RANDOM(1 6)
OUTER_SET c = RANDOM(1 100)
ACTION_FOR_EACH file IN
                deathk
                gooddeat
                _deathk
                _ooddeat
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME "%file%.cre" BEGIN
    COPY_EXISTING "%file%.cre" override
      PATCH_INCLUDE "atweaks/lib/fj_cre_validity.tpp"
      PATCH_IF valid BEGIN
        PATCH_IF c <= 80 BEGIN
          SPRINT sword $dksw("%r%")
          PATCH_IF r = 1 BEGIN
            ADD_CRE_ITEM "%sword%" #0 #0 #0 unstealable weapon1 EQUIP
          END ELSE PATCH_IF r = 2 BEGIN
            ADD_CRE_ITEM "%sword%" #0 #0 #0 unstealable weapon1 EQUIP TWOHANDED
          END ELSE PATCH_IF r = 3 BEGIN
            ADD_CRE_ITEM "%sword%" #0 #0 #0 "unstealable&undroppable" weapon1 EQUIP TWOHANDED
            ADD_CRE_ITEM fl#brsw #0 #0 #0 unstealable inv1
          END ELSE PATCH_IF r = 4 OR r = 6 BEGIN
            ADD_CRE_ITEM "%sword%" #0 #0 #0 "unstealable&undroppable" weapon1 EQUIP
            ADD_CRE_ITEM fl#brsw #0 #0 #0 unstealable inv1
          END ELSE BEGIN
            ADD_CRE_ITEM fl#dksw5 #0 #1 #0 "unstealable&undroppable" weapon1 EQUIP
            ADD_CRE_ITEM sw2h01 #0 #0 #0 unstealable weapon2 TWOHANDED
            ADD_CRE_ITEM fl#brsw #0 #0 #0 unstealable inv1
          END
        END ELSE BEGIN
          REPLACE_CRE_ITEM sw2h01 #0 #0 #0 unstealable weapon1 EQUIP TWOHANDED
        END
      END
    BUT_ONLY
  END
END


ACTION_IF !BG1 OR BGT BEGIN
  COPY_EXISTING uddeath.cre  "override/fl#udkn1.cre"
                uddeath.cre  "override/fl#udkn2.cre"
                uddeath.cre  "override/fl#udkn3.cre"
                deathkni.cre "override/fl#wkkn1.cre"
                deathkni.cre "override/fl#wkkn2.cre"
                deathkni.cre "override/fl#wkkn3.cre"
  
  COPY_EXISTING uddeath.cre  override
                uddeath2.cre override
                deathkni.cre override
                deck615.cre  override
                fl#udkn1.cre override
                fl#udkn2.cre override
                fl#udkn3.cre override
                fl#wkkn1.cre override
                fl#wkkn2.cre override
                fl#wkkn3.cre override
    PATCH_INCLUDE "atweaks/lib/fj_cre_validity.tpp"
    PATCH_IF valid BEGIN
      r = RANDOM(1 6)
      SPRINT sword $dksw("%r%")
      PATCH_IF r = 1 BEGIN
        ADD_CRE_ITEM "%sword%" #0 #0 #0 unstealable weapon1 EQUIP
      END ELSE PATCH_IF r = 2 BEGIN
        ADD_CRE_ITEM "%sword%" #0 #0 #0 unstealable weapon1 EQUIP TWOHANDED
      END ELSE PATCH_IF r = 3 BEGIN
        ADD_CRE_ITEM "%sword%" #0 #0 #0 "unstealable&undroppable" weapon1 EQUIP TWOHANDED
        ADD_CRE_ITEM fl#brsw #0 #0 #0 unstealable inv1
      END ELSE PATCH_IF r = 4 OR r = 6 BEGIN
        ADD_CRE_ITEM "%sword%" #0 #0 #0 "unstealable&undroppable" weapon1 EQUIP
        ADD_CRE_ITEM fl#brsw #0 #0 #0 unstealable inv1
      END ELSE BEGIN
        ADD_CRE_ITEM fl#dksw5 #0 #1 #0 "unstealable&undroppable" weapon1 EQUIP
        ADD_CRE_ITEM sw2h01 #0 #0 #0 unstealable weapon2 TWOHANDED
        ADD_CRE_ITEM fl#brsw #0 #0 #0 unstealable inv1
      END
    END
  BUT_ONLY
  
  COPY_EXISTING udsac.bcs override
    DECOMPILE_BCS_TO_BAF
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~("uddeath",[2929.1562],12)~ ~("fl#udkn1",[2929.1562],12)~
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~("uddeath",[2724.1143],12)~ ~("fl#udkn2",[2724.1143],12)~
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~("uddeath",[3280.1348],12)~ ~("fl#udkn3",[3280.1348],12)~
    COMPILE_BAF_TO_BCS
  BUT_ONLY
  
  COPY_EXISTING ar3007.bcs override
    DECOMPILE_BCS_TO_BAF
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~("deathkni",[550.390],0)~ ~("fl#wkkn1",[550.390],0)~
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~("deathkni",[383.505],12)~ ~("fl#wkkn2",[383.505],12)~
      REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~("deathkni",[574.683],10)~ ~("fl#wkkn3",[574.683],10)~
    COMPILE_BAF_TO_BCS
  BUT_ONLY
END



//Ghasts

ACTION_FOR_EACH file IN
               ghast                                                                //BG1 standard Ghast
               ghastd                                                               //Durlag's Tower Ghast Trap Ghast
               _ghast
               _ghastd
               ghasts                                                               //Tiax' summoned ghast
               _ghasts
               ghast01                                                              //BG2 standard Ghast
               nevm3                                                                //Nev's undead trap Ghast
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME "%file%.cre" BEGIN
    COPY_EXISTING "%file%.cre" override
      PATCH_INCLUDE "atweaks/lib/fj_cre_validity.tpp"
      PATCH_IF valid BEGIN
        PATCH_INCLUDE "atweaks/lib/fj_cre_reindex.tpp"
        REPLACE_CRE_ITEM fl#ghast #0 #0 #0 none weapon1 EQUIP
        LPF RR#MDCRE
          INT_VAR
            newxpv = 650
            newhp = 32 //4d8
            newac = 4
            newth = 17
            newapr = 3
            newsvd = 9
            newsvw = 11
            newsvp = 10
            newsvb = 12
            newsvs = 12
            newlvl1 = 4
            newlvl2 = 0
            newlvl3 = 0
            newmr = 0
            newfr = 0
            newcr = 0
            newer = 0
            newar = 0
            newrs = 0
            newrc = 0
            newrp = 0
            newrm = 0
            newstr = 18
            newdex = 9
            newcon = 9
            newint = 12
            newwis = 9
            newchr = 7
            newmor = 14
            newclass = 118
            newalign = 51
          STR_VAR
        END
        LPF RR#MDCSC
          STR_VAR
            newscript = fl#ghast
            script01 = ghast
            script02 = wtasight
            script03 = ghastd
            script04 = movep1
            script05 = _ghast
            script06 = _tasight
            script07 = _ghastd
            script08 = _movep1
            script09 = dw#melee
        END
      END
    BUT_ONLY
  END
END

COPY "atweaks/itm/fl#ghast.itm" override
     "atweaks/spl/fl#ghaau.spl" override



//Ghouls and Lacedons

ACTION_FOR_EACH file IN
               ghoul                                                               //BG1 Ghoul
               korax                                                               //Korax the Ghoul
               _ghoul                                                              //Tutu
               _korax                                                              //Tutu
               lacedo01                                                            //Lacedon
               sahlace                                                             //Lacedon
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME "%file%.cre" BEGIN
    COPY_EXISTING "%file%.cre" override
      PATCH_INCLUDE "atweaks/lib/fj_cre_validity.tpp"
      PATCH_IF valid BEGIN
        PATCH_INCLUDE "atweaks/lib/fj_cre_reindex.tpp"
        REPLACE_CRE_ITEM fl#ghoul #0 #0 #0 none weapon1 EQUIP
        LPF RR#MDCRE
          INT_VAR
            newxpv = 175
            newhp = 16 //2d8
            newac = 6
            newth = 19
            newapr = 3
            newsvd = 12
            newsvw = 14
            newsvp = 13
            newsvb = 15
            newsvs = 13
            newlvl1 = 2
            newlvl2 = 0
            newlvl3 = 0
            newmr = 0
            newfr = 0
            newcr = 0
            newer = 0
            newar = 0
            newrs = 0
            newrc = 0
            newrp = 0
            newrm = 0
            newstr = 16
            newdex = 9
            newcon = 9
            newint = 7
            newwis = 7
            newchr = 7
            newmor = 12
            newalign = 51 //CE
          STR_VAR
            enfc01 = ghoul
            enfc02 = _ghoul
            enfc03 = lacedo01
            enfc04 = sahlace
        END
        LPF RR#MDCSC
          STR_VAR
            newscript = fl#ghoul
            script01 = wdasight
            script02 = _dasight
            script03 = sahamb01
            script04 = _tasight
            script05 = wtasight
            skipfile01 = korax
            skipfile02 = _korax
        END
      END
    BUT_ONLY
  END
END

COPY "atweaks/itm/fl#ghoul.itm" override
  LPF ADD_ITM_EFFECT INT_VAR type = 1 opcode = 177 target = 2 duration = 1 parameter1 = 2 parameter2 = 4 STR_VAR resource = fl#ghoul END //Protection from fl#ghoul.spl for elves
  LPF ADD_ITM_EFFECT INT_VAR type = 1 opcode = 146 target = 2 timing = 1 parameter1 = 1 parameter2 = 1 STR_VAR resource = fl#ghoul END //Cast fl#ghoul.spl

COPY "atweaks/eff/base.eff" "override/fl#ghoul.eff"
  WRITE_LONG 0x10 206
  WRITE_LONG 0x14 2
  WRITE_LONG 0x28 1
  WRITE_ASCII 0x30 fl#ghoul #8

COPY "atweaks/spl/fl#ghoul.spl" override



//Ghoul lords

ACTION_FOR_EACH file IN
                ghogr01
                gholor01
                riftcr01
                ghoullor
                _houllor
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME "%file%.cre" BEGIN
    COPY_EXISTING "%file%.cre" override
      PATCH_INCLUDE "atweaks/lib/fj_cre_validity.tpp"
      PATCH_IF valid BEGIN
        PATCH_INCLUDE "atweaks/lib/fj_cre_reindex.tpp"
        REPLACE_CRE_ITEM fl#glord #0 #0 #0 none weapon1 EQUIP
        REMOVE_CRE_ITEM mage06 _mage06
        SAY 0x8 @1033
        SAY 0xc @1033
        LPF RR#MDCRE
          INT_VAR
            newxpv = 3000
            newhp = 48 //6d8
            newac = 4
            newth = 15
            newapr = 3
            newsvd = 9
            newsvw = 11
            newsvp = 10
            newsvb = 12
            newsvs = 12
            newlvl1 = 7
            newlvl2 = 0
            newlvl3 = 0
            newmr = 0
            newfr = 0
            newcr = 0
            newer = 0
            newar = 0
            newrs = 0
            newrc = 0
            newrp = 0
            newrm = 0
            newstr = 18
            newstrx = 0
            newdex = 9
            newcon = 9
            newint = 14
            newwis = 9
            newchr =  9
            newmor = 14
            newalign = 51
          STR_VAR
            enfc01 = ghogr01
            enfc02 = gholor01
            enfc03 = riftcr01
            enfc04 = ghoullor
            enfc05 = _houllor
        END
        LPF RR#MDCSC
          STR_VAR
            newscript = fl#glord
            script01 = _ghast
            script02 = _tasight
            script03 = ghast
            script04 = wtasight
            script05 = gholor01
            script06 = wdasight
            script07 = riftcr01
        END
      END
    BUT_ONLY
  END
END

COPY "atweaks/itm/fl#glord.itm" override
     "atweaks/eff/fl#glord.eff" override
     
COPY "atweaks/spl/fl#glord.spl" override
  WRITE_BYTE 0x27 FearSecType
  FOR (i=0;i<SHORT_AT 0x68;++i) BEGIN
    WRITE_SHORT LONG_AT 0x64 + 0x28*i + 0x26 fl#sight
  END
  FOR (i=LONG_AT 0x6a;i<SOURCE_SIZE;i+=0x30) BEGIN
    READ_SHORT i type
    PATCH_IF type = 139 BEGIN
      SAY i + 0x4 @1034
    END
  END



//Mummies

ACTION_FOR_EACH file IN
               mummy
               mummy01
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME "%file%.cre" BEGIN
    COPY_EXISTING "%file%.cre" override
      PATCH_INCLUDE "atweaks/lib/fj_cre_validity.tpp"
      PATCH_IF valid BEGIN
        PATCH_INCLUDE "atweaks/lib/fj_cre_reindex.tpp"
        REPLACE_CRE_ITEM fl#mum #0 #0 #0 none weapon1 EQUIP
        LPF RR#MDCRE
          INT_VAR
            newxpv = 3000
            newhp = 51 //6d8+3
            newac = 3
            newth = 13
            newapr = 1
            newsvd = 10
            newsvw = 12
            newsvp = 11
            newsvb = 12
            newsvs = 13
            newmr = 0
            newlvl1 = 6
            newfr = "-33"
            newcr = 100
            newer = 0
            newar = 0
            newrs = 50
            newrc = 50
            newrp = 50
            newrm = 50
            newstr = 18
            newstrx = 99
            newint = 7
            newwis = 7
            newdex = 9
            newcon = 14
            newchr = 7
            newmor = 15
            newalign = 19 //LE
          STR_VAR
            enfc01 = mummy
            enfc02 = mummy01
        END
        LPF RR#MDCSC
          STR_VAR
            newscript = fl#mum
            script01 = wtasight
            script02 = dw#melee
            //skipscript01 = dw#melee //do not assign a script if SCSII is installed
        END
      END
    BUT_ONLY
  END
END

COPY "atweaks/itm/fl#mum.itm" override
  //LPF ADD_ITM_EFFECT INT_VAR type = 1 opcode = 272 target = 1 timing = 2 parameter1 = 6 parameter2 = 3 STR_VAR resource = fl#mumfa END
  //LPF ADD_ITM_EFFECT INT_VAR type = 1 opcode = 232 target = 1 timing = 2 parameter1 = 0 parameter2 = 1 STR_VAR resource = fl#mumfa END //cast spell on self every round

  /*
COPY "atweaks/eff/base.eff" "override/fl#mumfa.eff"
  WRITE_LONG 0x10 146 //cast spell
  WRITE_LONG 0x14 2
  WRITE_LONG 0x1c 1
  WRITE_LONG 0x20 1
  WRITE_LONG 0x28 1
  WRITE_ASCII 0x30 fl#mumfa #8*/

COPY "atweaks/spl/fl#mumfa.spl" override
  FOR (i=0;i<SHORT_AT 0x68;++i) BEGIN
    WRITE_SHORT LONG_AT 0x64 + 0x28*i + 0x26 fl#sight
  END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 146 target = 2 timing = 1 parameter1 = 1 parameter2 = 1 STR_VAR resource = fl#mumfn END //cast fl#mumfn
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 2 parameter1 = 1 parameter2 = 4 STR_VAR resource = fl#mumfh END //Use fl#mumfh.eff for humans
  //LPF ADD_SPELL_EFFECT INT_VAR opcode = 139 target = 1 timing = 1 parameter1 = 388 END

COPY "atweaks/eff/base.eff" "override/fl#mumfh.eff"
  WRITE_LONG 0x10 146 //cast spell
  WRITE_LONG 0x14 2
  WRITE_LONG 0x1c 1
  WRITE_LONG 0x20 1
  WRITE_LONG 0x28 1
  WRITE_ASCII 0x30 fl#mumfh #8

COPY "atweaks/eff/base.eff" "override/fl#mumfn.eff"
  WRITE_LONG 0x10 206 //Protection from fl#mumfn.spl for Humans, fl#mumfn.eff used by fl#mumfn.spl
  WRITE_LONG 0x14 2
  WRITE_LONG 0x28 1
  WRITE_ASCII 0x30 fl#mumfn #8
  
COPY "atweaks/spl/fl#mumfh.spl" override
     "atweaks/spl/fl#mumfn.spl" override
  WRITE_BYTE 0x27 FearSecType
  FOR (i=LONG_AT 0x64;i<SOURCE_SIZE;i+=0x30) BEGIN
    READ_SHORT i type
    PATCH_IF type = 139 BEGIN
      SAY i + 0x4 @1026
    END
  END

COPY "atweaks/spl/fl#mum.spl" override
  WRITE_BYTE 0x27 DiseaseSecType
  FOR (i=LONG_AT 0x6a;i<SOURCE_SIZE;i+=0x30) BEGIN
    READ_SHORT i type
    PATCH_IF type = 206 BEGIN
      SAY i + 0x4 @1028
    END
    PATCH_IF type = 139 BEGIN
      SAY i + 0x4 @1029
    END
  END
  
//Spells and items that cure and protect against disease and fear are patched at the end



// Greater Mummies

ACTION_FOR_EACH file IN
               mumgre01
               riftcr03
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME "%file%.cre" BEGIN
    COPY_EXISTING "%file%.cre" override
      PATCH_INCLUDE "atweaks/lib/fj_cre_validity.tpp"
      PATCH_IF valid BEGIN
        PATCH_INCLUDE "atweaks/lib/fj_cre_reindex.tpp"
        REPLACE_CRE_ITEM fl#gmum #0 #0 #0 none weapon1 EQUIP
        REPLACE_TEXTUALLY CASE_INSENSITIVE immune3 immune2
        LPF DELETE_CRE_EFFECT INT_VAR opcode_to_delete = 0 END
        ADD_MEMORIZED_SPELL sppr101 #0 priest (1) //Bless
        ADD_MEMORIZED_SPELL sppr102 #0 priest (2) //Command
        ADD_MEMORIZED_SPELL sppr107 #0 priest (1) //Protection from Evil
        ADD_MEMORIZED_SPELL sppr109 #0 priest (1) //Sanctuary
        ADD_MEMORIZED_SPELL sppr111 #0 priest (2) //Armor of Faith
        ADD_MEMORIZED_SPELL sppr113 #0 priest (5) //Doom
        ADD_MEMORIZED_SPELL sppr201 #1 priest (1) //Aid
        ADD_MEMORIZED_SPELL sppr203 #1 priest (1) //Chant
        ADD_MEMORIZED_SPELL sppr208 #1 priest (5) //Hold Person
        ADD_MEMORIZED_SPELL sppr211 #1 priest (3) //Silence 15' Radius
        ADD_MEMORIZED_SPELL sppr214 #1 priest (2) //DUHM
        ADD_MEMORIZED_SPELL sppr301 #2 priest (4) //Animate Dead
        ADD_MEMORIZED_SPELL sppr303 #2 priest (2) //Dispel Magic
        ADD_MEMORIZED_SPELL sppr304 #2 priest (1) //Glyph of Warding
        ADD_MEMORIZED_SPELL sppr314 #2 priest (4) //Unholy Blight
        ADD_MEMORIZED_SPELL sppr405 #3 priest (2) //Mental Domination
        ADD_MEMORIZED_SPELL sppr407 #3 priest (1) //Protection from Lightning
        ADD_MEMORIZED_SPELL sppr408 #3 priest (1) //Protection from Evil 10' Radius
        ADD_MEMORIZED_SPELL sppr411 #3 priest (1) //Poison
        ADD_MEMORIZED_SPELL sppr412 #3 priest (3) //Holy Power
        ADD_MEMORIZED_SPELL sppr414 #3 priest (2) //Cause Serious Wounds

        ADD_MEMORIZED_SPELL sppr505 #4 priest (2) //True Seeing
        ADD_MEMORIZED_SPELL sppr509 #4 priest (1) //Magic Resistance
        PATCH_IF RANDOM (1 2) BEGIN
          ADD_MEMORIZED_SPELL sppr511 #4 priest (2) //Slay Living
        END ELSE BEGIN
          ADD_MEMORIZED_SPELL sppr512 #4 priest (2) //Greater Command
        END
        ADD_MEMORIZED_SPELL sppr513 #4 priest (2) //Righteous Magic

        ADD_MEMORIZED_SPELL sppr601 #5 priest (1) //Aerial Servant
        ADD_MEMORIZED_SPELL sppr608 #5 priest (1) //Harm
        ADD_MEMORIZED_SPELL sppr603 #5 priest (1) //Blade Barrier
        ADD_MEMORIZED_SPELL sppr612 #5 priest (1) //Bolt of Glory

        ADD_MEMORIZED_SPELL sppr719 #6 priest (1) //Symbol, Death
        ADD_MEMORIZED_SPELL sppr718 #6 priest (1) //Symbol, Stun

        LPF RR#MDCRE
          INT_VAR
            newxpv = 14000
            newhp = 91 //11d8+3
            newac = "-1"
            newth = 9
            newapr = 1
            newsvd = 2
            newsvw = 6
            newsvp = 7
            newsvb = 8
            newsvs = 7
            newmr = 15
            newlvl1 = 19
            newfr = 0
            newcr = 100
            newer = "-50"
            newar = 0
            newrs = 50
            newrc = 50
            newrp = 50
            newrm = 50
            newstr = 18
            newstrx = 99
            newint = 18
            newwis = 21
            newdex = 9
            newcon = 14
            newchr = 9
            newmor = 18
            newalign = 51 //CE
            newclass = 3 //cleric
          STR_VAR
            enfc01 = mumgre01
            enfc02 = riftcr03
        END
      END
    BUT_ONLY
  END
END

COPY "atweaks/itm/fl#mum.itm" "override/fl#gmum.itm"
  FOR (i=0;i<SHORT_AT 0x68;++i) BEGIN
    WRITE_BYTE LONG_AT 0x64 + 0x38*i + 0x16 6
    WRITE_BYTE LONG_AT 0x64 + 0x38*i + 0x18 3 //3d6
  END
  FOR (i=LONG_AT 0x6a;i<SOURCE_SIZE;i+=0x30) BEGIN
    READ_SHORT i type
    PATCH_IF type = 146 BEGIN
      WRITE_ASCII i + 0x14 fl#gmum
    END
  END
  
COPY "atweaks/spl/fl#mum.spl" "override/fl#gmum.spl"
  WRITE_BYTE 0x27 DiseaseSecType
  FOR (i=LONG_AT 0x6a;i<SOURCE_SIZE;i+=0x30) BEGIN
    READ_SHORT i type
    PATCH_IF type = 206 BEGIN
      SAY i + 0x4 @1028
    END
    PATCH_IF type = 139 BEGIN
      SAY i + 0x4 @1029
    END
  END
  //Disease

//Fear aura
COPY "atweaks/spl/fl#mumfa.spl" "override/fl#gmumf.spl"
  FOR (i=0;i<SHORT_AT 0x68;++i) BEGIN
    WRITE_SHORT LONG_AT 0x64 + 0x28*i + 0x26 fl#sight
  END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 146 target = 2 timing = 1 parameter1 = 1 parameter2 = 1 STR_VAR resource = fl#gmumn END //cast fl#mumfn
  //LPF ADD_SPELL_EFFECT INT_VAR opcode = 177 target = 2 parameter1 = 1 parameter2 = 4 STR_VAR resource = fl#gmumh END //Use fl#mumfh.eff for humans

/*
COPY "atweaks/eff/base.eff" "override/fl#gmumh.eff"
  WRITE_LONG 0x10 146 //cast spell
  WRITE_LONG 0x14 2
  WRITE_LONG 0x1c 1
  WRITE_LONG 0x20 1
  WRITE_LONG 0x28 1
  WRITE_ASCII 0x30 fl#gmumh #8
  */
/*
COPY "atweaks/eff/base.eff" "override/fl#gmumn.eff"
  WRITE_LONG 0x10 206 //Protection from fl#mumfn.spl for Humans, fl#mumfn.eff used by fl#mumfn.spl
  WRITE_LONG 0x14 2
  WRITE_LONG 0x28 1
  WRITE_ASCII 0x30 fl#gmumn #8*/
  
COPY //"atweaks/spl/fl#mumfh.spl" "override/fl#gmumh.spl"
     "atweaks/spl/fl#mumfn.spl" "override/fl#gmumn.spl"
  WRITE_BYTE 0x27 FearSecType
  READ_LONG  0x64 ao
  READ_SHORT 0x68 na
  READ_LONG  0x6a eo
  ne = 0
  ei = 0
  FOR(i=0;i<na;++i) BEGIN
    ei += ne
    READ_SHORT  ao + 0x28*i + 0x1e ne
    WRITE_SHORT ao + 0x28*i + 0x20 ei
    FOR (j=0;j<ne;++j) BEGIN
      READ_SHORT eo + 0x30*(ei + j) type
      PATCH_IF type = 177 BEGIN
        DELETE_BYTES eo + 0x30*(ei + j) 0x30
        --ne
        --j
      END
      WRITE_LONG eo + 0x30*(ei + j) + 0xe  THIS * 2 //Double duration for the fearsome greater mummies
      WRITE_LONG eo + 0x30*(ei + j) + 0x28 THIS - 3 //And save penalty
    END
  END



//Skeleton Warriors

ACTION_FOR_EACH file IN
               ar18skel
               ceskel01
               grskel1
               grskel2
               grtomb01
               hgskl04
               nevm2
               riftcr02
               rskel02
               sahskel
               skelwa
               skelwa01
               skelwa02
               skelwa03
               skelwasu
               suundead
               _skelwa
               _kelwa02
               _kelwa03
               bgskel03
               bgskel02
               bgskelwa
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME "%file%.cre" BEGIN
    COPY_EXISTING "%file%.cre" override
      PATCH_INCLUDE "atweaks/lib/fj_cre_validity.tpp"
      PATCH_IF valid BEGIN
        PATCH_INCLUDE "atweaks/lib/fj_cre_reindex.tpp"
        REPLACE_CRE_ITEM ring99 #0 #0 #0 none lring                                //Standard undead immunities
        REPLACE_CRE_ITEM immune1 #0 #0 #0 none rring                               //+1 or better only
        LPF ADD_CRE_EFFECT INT_VAR
                          opcode = 297                                             //Immunity to Turn Undead
                          target = 1
                          timing = 9
                          paramater2 = 1 END
        LPF RR#MDCRE
          INT_VAR
            newxpv = 4000
            newhp = 80 //9d8+8
            newac = 2
            newth = 8
            newapr = 1
            newsvd = 7
            newsvw = 9
            newsvp = 8
            newsvb = 8
            newsvs = 10
            newlvl1 = 9
            newlvl2 = 0
            newlvl3 = 0
            newmr = 90
            newfr = 0
            newcr = 100
            newer = 0
            newar = 0
            newrs = 50
            newrc = 0
            newrp = 50
            newrm = 50
            newstr = 18
            newstrx = 50
            newint = 16
            newwis = 9
            newdex = 16
            newcon = 14
            newchr = 9
            newmor = 15
            newalign = 35 //NE
            newclass = 135 //Skeleton Warrior (for ID)
            newgen = 4 //undead
            newrace = 115 //Skeleton
            
            enforce = 1
          STR_VAR
            skipfile01 = hgskl04
            skipfile02 = grtomb01
        END
        PATCH_IF "%file%" STRING_EQUAL_CASE skelwa03 OR "%file%" STRING_EQUAL_CASE _kelwa03 OR "%file%" STRING_EQUAL_CASE bgskel03 BEGIN
          SPRINT script fl#sklwr
        END ELSE BEGIN
          SPRINT script fl#sklwm
        END
        LPF RR#MDCSC
          STR_VAR
            newscript = EVAL "%script%"
            script01 = gpskel1
            script02 = wtasight
            script03 = _tasight
            script04 = wtarsgt
            script05 = dw#melee
            script06 = wdasight
            script07 = dw#range
            script98 = _wtarsgt
        END
      END
    BUT_ONLY
  END
END

COPY "atweaks/spl/fl#skelw.spl" override
  FOR (i=0;i<SHORT_AT 0x68;++i) BEGIN
    WRITE_SHORT LONG_AT 0x64 + 0x28*i + 0x26 fl#sight
  END



// Scripts

ACTION_IF GAME_IS ~tutu tutu_totsc~ BEGIN
  OUTER_SPRINT DurlagArea fw0516
  OUTER_SPRINT TutuJournal
  "
    EraseJournalEntry(84201)
    EraseJournalEntry(84195)
    EraseJournalEntry(83499)
    EraseJournalEntry(83502)
    EraseJournalEntry(83506)
    EraseJournalEntry(83508)
    EraseJournalEntry(83510)
    EraseJournalEntry(83512)
    EraseJournalEntry(83522)
    EraseJournalEntry(83710)
    EraseJournalEntry(83561)
    EraseJournalEntry(83738)
    EraseJournalEntry(83894)
    EraseJournalEntry(83899)
    EraseJournalEntry(83905)
    EraseJournalEntry(83989)
    EraseJournalEntry(83993)
    EraseJournalEntry(83995)
"
  OUTER_SPRINT Skelwa _skelwa
  OUTER_SPRINT GGhoul _houllor
  OUTER_SPRINT SkeletC _kelet_c
  OUTER_SPRINT killgood _illgood
  OUTER_SPRINT gooddeat _ooddeat
END ELSE ACTION_IF GAME_IS bgt BEGIN
  OUTER_SPRINT DurlagArea ard016
  OUTER_SPRINT TutuJournal ""
  OUTER_SPRINT Skelwa bgskelwa
  OUTER_SPRINT GGhoul ghoullor
  OUTER_SPRINT SkeletC skelet_c
  OUTER_SPRINT killgood killgood
  OUTER_SPRINT gooddeat gooddeat
END

ACTION_IF BG1 BEGIN
  COPY - "atweaks/baf/killgood.baf" "...atweaks/fl-inlined/%killgood%.baf"
         "atweaks/baf/gooddeat.baf" "...atweaks/fl-inlined/%gooddeat%.baf"
  COMPILE ~atweaks/baf/fl#bg1kn.baf~ "...atweaks/fl-inlined/%killgood%.baf" "...atweaks/fl-inlined/%gooddeat%.baf" EVAL
END 

COMPILE ~atweaks/baf/fl#death.baf~
        ~atweaks/baf/fl#mum.baf~
        ~atweaks/baf/fl#ghoul.baf~
        ~atweaks/baf/fl#ghast.baf~
        "atweaks/baf/fl#dksw5.baf"
        "atweaks/baf/fl#glord.baf"
        "atweaks/baf/fl#sklwm.baf"
        "atweaks/baf/fl#sklwr.baf"

        

        
COPY_EXISTING_REGEXP GLOB ~.+\.\(spl\|itm\)$~ override
  PATCH_IF SOURCE_SIZE > 0x71 BEGIN
    READ_LONG  0x64 ao
    READ_SHORT 0x68 na
    READ_LONG  0x6a eo
    READ_SHORT 0x6e ei1
    READ_SHORT 0x70 gne
    ei = ei1 + gne
    ne = 0
    al = "%SOURCE_EXT%" STRING_EQUAL_CASE itm ? 0x38 : 0x28
    FOR (i=0;i<gne;++i) BEGIN
      READ_SHORT eo + 0x30*(ei1 + i)       type ELSE 999
      READ_LONG  eo + 0x30*(ei1 + i) + 0x8 p2   ELSE 999
      
      
      PATCH_IF type = 101 AND p2 = 24 BEGIN
        PATCH_FOR_EACH spell IN fl#mumfn fl#mumfh fl#glord fl#gmumn BEGIN
          PATCH_IF "%SOURCE_EXT%" STRING_EQUAL_CASE itm BEGIN
            LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 206 target = 1 timing = 2 STR_VAR resource = EVAL "%spell%" END
            ++i
            ++gne
          END
        END
      END
      
      
      PATCH_IF type = 101 AND p2 = 78 BEGIN
        PATCH_FOR_EACH spell IN fl#mum fl#gmum BEGIN
          PATCH_IF "%SOURCE_EXT%" STRING_EQUAL_CASE itm BEGIN
            LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 206 target = 1 timing = 2 STR_VAR resource = EVAL "%spell%" END
            ++i
            ++gne
          END
        END
      END
    END
    

    FOR (i=0;i<na AND ao + na*al < SOURCE_SIZE;++i) BEGIN
      ei += ne
      WRITE_SHORT ao + al*i + 0x20 ei
      READ_SHORT  ao + al*i + 0x1e ne
      FOR (j=0;j<ne;++j) BEGIN
        READ_SHORT eo + 0x30*(ei + j)        type   ELSE 999
        READ_BYTE  eo + 0x30*(ei + j) + 0x2  target ELSE 0
        READ_BYTE  eo + 0x30*(ei + j) + 0x3  power  ELSE 0
        READ_LONG  eo + 0x30*(ei + j) + 0x4  p1     ELSE 999
        READ_LONG  eo + 0x30*(ei + j) + 0x8  p2     ELSE 999
        READ_BYTE  eo + 0x30*(ei + j) + 0xc  timing ELSE 0
        READ_BYTE  eo + 0x30*(ei + j) + 0xd  dr     ELSE 0
        READ_LONG  eo + 0x30*(ei + j) + 0xe  time   ELSE 0
        

        
        PATCH_IF type = 79 BEGIN //cure disease
          INSERT_BYTES  eo + 0x30*(ei + j)        0x30
            WRITE_SHORT eo + 0x30*(ei + j)        221 //remove sec type
            WRITE_BYTE  eo + 0x30*(ei + j) + 0x2  target
            WRITE_BYTE  eo + 0x30*(ei + j) + 0x3  power
            WRITE_LONG  eo + 0x30*(ei + j) + 0x4  9
            WRITE_LONG  eo + 0x30*(ei + j) + 0x8  DiseaseSecType
            WRITE_BYTE  eo + 0x30*(ei + j) + 0xc  1
            WRITE_BYTE  eo + 0x30*(ei + j) + 0x12 100
          ++ne
          ++j
        END
        
        
        
        PATCH_IF type = 161 BEGIN //remove fear
          INSERT_BYTES  eo + 0x30*(ei + j)        0x30
            WRITE_SHORT eo + 0x30*(ei + j)        221 //remove sec type
            WRITE_BYTE  eo + 0x30*(ei + j) + 0x2  target
            WRITE_BYTE  eo + 0x30*(ei + j) + 0x3  power
            WRITE_LONG  eo + 0x30*(ei + j) + 0x4  9
            WRITE_LONG  eo + 0x30*(ei + j) + 0x8  FearSecType
            WRITE_BYTE  eo + 0x30*(ei + j) + 0xc  1
            WRITE_BYTE  eo + 0x30*(ei + j) + 0x12 100
          ++ne
          ++j
        END
        
        
        
        PATCH_IF type = 101 AND p2 = 78 BEGIN //immunity to disease
          PATCH_FOR_EACH spell IN fl#mum fl#gmum BEGIN
            INSERT_BYTES   eo + 0x30*(ei + j)        0x30
              WRITE_SHORT  eo + 0x30*(ei + j)        206 //protection from spell
              WRITE_BYTE   eo + 0x30*(ei + j) + 0x2  target
              WRITE_BYTE   eo + 0x30*(ei + j) + 0x3  power
              WRITE_BYTE   eo + 0x30*(ei + j) + 0xc  timing
              WRITE_BYTE   eo + 0x30*(ei + j) + 0xd  dr
              WRITE_LONG   eo + 0x30*(ei + j) + 0xe  time
              WRITE_BYTE   eo + 0x30*(ei + j) + 0x12 100
              WRITE_ASCIIE eo + 0x30*(ei + j) + 0x14 "%spell%" #8
            ++ne
            ++j
          END
        END
        
        

        PATCH_IF type = 101 AND p2 = 24 BEGIN //immunity to fear
          PATCH_FOR_EACH spell IN fl#mumfn fl#mumfh fl#glord fl#gmumn BEGIN
            INSERT_BYTES   eo + 0x30*(ei + j)        0x30
              WRITE_SHORT  eo + 0x30*(ei + j)        206 //protection from spell
              WRITE_BYTE   eo + 0x30*(ei + j) + 0x2  target
              WRITE_BYTE   eo + 0x30*(ei + j) + 0x3  power
              WRITE_BYTE   eo + 0x30*(ei + j) + 0xc  timing
              WRITE_BYTE   eo + 0x30*(ei + j) + 0xd  dr
              WRITE_LONG   eo + 0x30*(ei + j) + 0xe  time
              WRITE_BYTE   eo + 0x30*(ei + j) + 0x12 100
              WRITE_ASCIIE eo + 0x30*(ei + j) + 0x14 "%spell%" #8
            ++ne
            ++j
          END
        END
      END
      WRITE_SHORT ao + al*i + 0x1e ne
    END
  END
BUT_ONLY
