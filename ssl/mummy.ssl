INCLUDE FILE(definitions.ssl)

//Fear Aura every round
IF
  !StateCheck(Myself,STATE_REALLY_DEAD)
  !GlobalTimerNotExpired("flFearAura","LOCALS")
THEN
  RESPONSE #100
    SetGlobalTimer("flFearAura","LOCALS")
    ForceSpellRES("fl#mumfa",Myself)
    Continue()
END

IF
  Global("RR#Active","LOCALS",0)
  HitBy([GOODCUTOFF],0)
THEN
  RESPONSE #100
    SetGlobal("RR#Active","LOCALS",1)
    Enemy()
    Continue()
END

IF
  Global("RR#Active","LOCALS",0)
  Allegiance(Myself,EVILCUTOFF)
  See([GOODCUTOFF])
THEN
  RESPONSE #100
    SetGlobal("RR#Active","LOCALS",1)
    Continue()
END

INCLUDE FILE(mill.ssl)

IF
  Global("RR#Active","LOCALS",0)
  !Allegiance(Myself,ENEMY)
THEN
  RESPONSE #100
    NoAction()
END

BEGIN LOOP(loopvar||1;2;3;4;5;6)
IF
  See(Playerloopvar)
  !GlobalTimerNotExpired("flChasePlayerloopvar","LOCALS")
THEN
  RESPONSE #100
    SetGlobalTimer("flChasePlayerloopvar","LOCALS",6)
    Continue()
END
END LOOP

IF TRIGGER
  TargetBlock(Squishy)
  TargetBlock(CloseEnemies)
  TriggerBlock(CrushRes|Stoneskin|MirrorImage|Helpless|Enemy|OKForMelee)
  Range(scstarget,10)
  HPPercentLT(scstarget,25)
THEN DO
  Action(Attack)
END

IF TRIGGER
  TargetBlock(Squishy)
  TargetBlock(CloseEnemies)
  TriggerBlock(CrushRes|Stoneskin|MirrorImage|Helpless|Enemy|OKForMelee)
  Range(scstarget,10)
THEN DO
  Action(Attack)
END

IF TRIGGER
  TargetBlock(Squishy)
  TargetBlock(CloseEnemies)
  TriggerBlock(CrushRes|Stoneskin|MirrorImage|Helpless|Enemy|OKForMelee)
THEN DO
  Action(Attack)
END

IF TRIGGER
  TargetBlock(Squishy)
  TargetBlock(CloseEnemies)
  TriggerBlock(Enemy)
THEN DO
  Action(Attack)
END

//Pursue
BEGIN LOOP (loopvar||1;2;3;4;5;6)
IF
  !See([GOODCUTOFF])
  GlobalTimerNotExpired("flChasePlayerloopvar","LOCALS")
THEN
  RESPONSE #100
    MoveToObject(Playerloopvar)
END
END LOOP

//Just keep busy
IF TRIGGER
  ActionListEmpty()
  Target(NearestEnemyOf(Myself))
  TriggerBlock(Enemy)
THEN DO
  Action(Attack)
END

IF TRIGGER
  !Allegiance(Myself,ENEMY)
  Target(NearestEnemyOf(Myself))
THEN DO
  Action(Attack)
END

IF TRIGGER
  !Allegiance(Myself,ENEMY)
  AttackedBy([ANYONE],DEFAULT)
  Target(LastAttackerOf(Myself))
THEN DO
  Action(Attack)
END


