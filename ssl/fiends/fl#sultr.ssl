//call lightning
//color spray, 7x/day (different from the mage spell, because the mage spell sucks but the PnP spell does not)
//detect invisibility, always active
//fear
//fire storm, 1x/day
//symbol, any type, 1x/day
//shout (cone-shaped, deafen and 2d6 damage, save vs spell, does not affect silenced targets)
//mass suggestion, 1x/day (charm, save vs spell at -1)
//gaze of fascination (hold person if the target saves vs spell, "see a dearly loved and respected person" if the target fails his save. charm? [non-hold effects not implemented])
//They all have this:
//animate dead
//cause disease
//charm person
//produce flame
//teleport without error

INCLUDE FILE(fiends/definitions.ssl)
VARIABLE(IsUltroloth=True)

IF
  Die()
  Global("fl#EACheck","LOCALS",1) // Fiend was summoned by an enemy
  Global("fl#XP","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("fl#XP","LOCALS",1)
    AddexperienceParty(26000) //Ultroloth XP value
END

IF
  HasItem("dw#sumfi",Myself)
THEN
  RESPONSE #100
    DestroyItem("dw#sumfi")
    Continue()
END

INCLUDE FILE(fiends/include/summoned_check.ssl)

INCLUDE FILE(fiends/include/get_mad_at_summoner.ssl)

IF
  StateCheck(Myself,STATE_REALLY_DEAD)
THEN
  RESPONSE #100
    NoAction()
END

IF 
  !InActiveArea(Myself)
  !See([ANYONE])
THEN
  RESPONSE #100
    NoAction()
END

IF
  !GlobalTimerNotExpired("flCast","LOCALS")
  OR(2)
    AreaCheck("AR3004")
    AreaCheck("AR3008")
THEN
  RESPONSE #100
    SetGlobalTimer("flCast","LOCALS",600) //No casting in dead-magic areas
END

INCLUDE FILE(fiends/include/teleport_away.ssl)

INCLUDE FILE(fiends/include/high_porting.ssl)

INCLUDE FILE(fiends/include/yugoloth_rampage.ssl)

//Call Lightning
IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(Disabled|MR50|ElecResFull|MinorGlobe|SIAlt)
  AreaType(OUTDOOR)
  !HasItemEquiped("clck26",scstarget)
  !Race(scstarget,RAKSHASA)
  !Race(scstarget,LICH)
THEN DO
  Combine()
  Action(ForceSpellRES,"RR#PR302"|100|50)
END

//Fire Storm
IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(Disabled|FireRes75|SIEvo|NotSummoned)
  !Race(scstarget,RAKSHASA)
  GlobalLT("flFireStorm","LOCALS",1)
THEN DO
  Action(ForceSpellRES,"RR#PR705"|100|100)
  IncrementGlobal("flFireStorm","LOCALS",1)
  SetGlobalTimer("flFireStormTimer","LOCALS",24) //Better than nothing, or maybe it's just silly
END

//Symbol, Death
IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(Disabled|MR50|SICon|Slayable|NotSummoned)
  OR(4)
    Class(scstarget,MAGE)
    Class(scstarget,MAGE_THIEF)
    Class(scstarget,CLERIC_MAGE)
    HPPercentLT(scstarget,40)
  GlobalLT("flSymbol","LOCALS",1)
THEN DO
  Action(ForceSpellRES,"RR#WI817"|100|100)
  IncrementGlobal("flSymbol","LOCALS",1)
END

//Symbol, Stun
IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(Disabled|MR50|SICon|Stun|NotSummoned)
  GlobalLT("flSymbol","LOCALS",1)
THEN DO
  Combine()
  Action(ForceSpellRES,"RR#WI816"|50|100)
  IncrementGlobal("flSymbol","LOCALS",1)
END

//Symbol, Pain
IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(Disabled|MR50|SICon|Stun|NotSummoned)
  GlobalLT("flSymbol","LOCALS",1)
THEN DO
  Combine()
  Action(ForceSpellRES,"RR#SPAIN"|50|100)
  IncrementGlobal("flSymbol","LOCALS",1)
END

//Symbol, Fear
IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(Disabled|MR50|SICon|Fear|NotSummoned)
  GlobalLT("flSymbol","LOCALS",1)
THEN DO
  Combine()
  Action(ForceSpellRES,"RR#WI811"|50|100)
  IncrementGlobal("flSymbol","LOCALS",1)
END

//Gaze of Fascination
IF TRIGGER
  TargetBlock(EnemiesInOrder)
  General(scstarget,HUMANOID)
  TriggerBlock(Disabled|Hold)
  !HasItemEquiped("RR#HEL03",scstarget) // Darkveil (RR)
THEN DO
  Combine()
  Action(ForceSpellRESVarTimer,"fl#ultgz",flUltGaze,6|100|100)
END

//Animate Dead
IF TRIGGER
  !GlobalTimerNotExpired("flFireStormTimer","LOCALS")
  TargetBlock(EnemiesInOrder)
  OR(3)
    Class(scstarget,MAGE_ALL)
    Class(scstarget,BARD_ALL)
    Class(scstarget,DRUID_ALL)
THEN DO
  Combine()
  Action(ForceSpellRES,"fl#adead"|50|100)
END

IF
  See([GOODCUTOFF.UNDEAD.0.SKELETON_WARRIOR])
  Allegiance(Myself,GOODCUTOFF)
  Name("fl#propertyoftheyugoloth2",LastSeenBy(Myself))
  !StateCheck(LastSeenBy(Myself),STATE_DISABLED)
  !GlobalTimerNotExpired("flUltrolothSkelWa","LOCALS")
THEN
  RESPONSE #100
    SetGlobalTimer("flUltrolothSkelWa","LOCALS",12)
END

IF
  See([EVILCUTOFF.UNDEAD.0.SKELETON_WARRIOR])
  Allegiance(Myself,EVILCUTOFF)
  Name("fl#propertyoftheyugoloth2",LastSeenBy(Myself))
  !StateCheck(LastSeenBy(Myself),STATE_DISABLED)
  !GlobalTimerNotExpired("flUltrolothSkelWa","LOCALS")
THEN
  RESPONSE #100
    SetGlobalTimer("flUltrolothSkelWa","LOCALS",12)
END

IF
  !GlobalTimerNotExpired("flFireStormTimer","LOCALS")
  !GlobalTimerNotExpired("flUltrolothSkelWa","LOCALS")
  !GlobalTimerNotExpired("flCast","LOCALS")
THEN
  RESPONSE #100
    ForceSpellRES("fl#adead",Myself)
    SetGlobalTimer("flUltrolothSkelWa","LOCALS",12)
    SetGlobalTimer("flCast","LOCALS",6)
END


//Cause Disease
IF TRIGGER
  TargetBlock(EnemiesInOrderShort)
  Range(scstarget,2)
  TriggerBlock(Disabled|MR75|MinorGlobe|SpellTurning|SIAbj|Disease)
  !Race(scstarget,RAKSHASA)
THEN DO
  Combine()
  Action(ForceSpellRES,"fl#csdis"|50|100)
END

//Shout
IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(Disabled|MR50|SIEvo)
  OR(5)
    Class(scstarget,MAGE_ALL)
    Class(scstarget,BARD_ALL)
    Class(scstarget,CLERIC_ALL)
    Class(scstarget,CLERIC_RANGER)
    Class(scstarget,DRUID_ALL)
  !StateCheck(scstarget,STATE_SILENCED)
  Range(scstarget,20)
  !Race(scstarget,RAKSHASA)
  !Race(scstarget,LICH)
THEN DO
  Action(ForceSpellRES,"fl#shout"|100|100)
END

//Fear
IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(Disabled|MR50|SIIll|Fear)
  Range(scstarget,20)
  !Race(scstarget,RAKSHASA)
THEN DO
  Action(ForceSpellRES,"fl#cnefr"|50|100)
END

//Mass Suggestion
IF TRIGGER
  Allegiance(Myself,GOODCUTOFF)
  !GlobalTimerNotExpired("flFireStormTimer","LOCALS")
  TargetBlock(EnemiesInOrder)
  General(scstarget,HUMANOID)
  NumCreatureGT([EVILCUTOFF.HUMANOID],4)
  !Race(scstarget,RAKSHASA)
  TriggerBlock(Disabled|MR50|SIEnc|Charm)
  GlobalLT("flMassSuggestion","LOCALS",1)
THEN DO
  Action(ForceSpellRESMyself,"fl#mssug"|100|50)
  IncrementGlobal("flMassSuggestion","LOCALS",1)
END

IF TRIGGER
  Allegiance(Myself,EVILCUTOFF)
  !GlobalTimerNotExpired("flFireStormTimer","LOCALS")
  TargetBlock(EnemiesInOrder)
  General(scstarget,HUMANOID)  
  NumCreatureGT([GOODCUTOFF.HUMANOID],3)
  !Race(scstarget,RAKSHASA)
  TriggerBlock(Disabled|MR50|SIEnc|Charm)
  GlobalLT("flMassSuggestion","LOCALS",1)
THEN DO
  Action(ForceSpellRESMyself,"fl#mssug"|100|50)
  IncrementGlobal("flMassSuggestion","LOCALS",1)
END

//Colour Spray
IF TRIGGER
  TargetBlock(EnemiesInOrderShort)
  TriggerBlock(Disabled|MR50|MinorGlobe|Sleep|SIAlt)
  !StateCheck(scstarget,STATE_BLIND)
  LevelLT(scstarget,6)
  Range(scstarget,15)
  !Race(scstarget,RAKSHASA)
  !Race(scstarget,LICH)
  GlobalLT("flColourSpray","LOCALS",7)
THEN DO
  Action(ForceSpellRES,"fl#clrsp"|100|50)
  IncrementGlobal("flColourSpray","LOCALS",1)
END

IF TRIGGER
  TargetBlock(EnemiesInOrderShort)
  TriggerBlock(Disabled|MR50|MinorGlobe|Sleep|SIAlt)
  !StateCheck(scstarget,STATE_BLIND)
  LevelGT(scstarget,5)
  LevelLT(scstarget,16)
  Range(scstarget,15)
  !Race(scstarget,RAKSHASA)
  !Race(scstarget,LICH)
  GlobalLT("flColourSpray","LOCALS",7)
THEN DO
  Action(ForceSpellRES,"fl#clrsp"|100|100)
  IncrementGlobal("flColourSpray","LOCALS",1)
END

IF TRIGGER
  TargetBlock(EnemiesInOrderShort)
  TriggerBlock(Disabled|MR50|MinorGlobe|Blindness|SIAlt)
  !StateCheck(scstarget,STATE_BLIND)
  LevelGT(scstarget,15)
  LevelLT(scstarget,18)
  Range(scstarget,15)
  !Race(scstarget,RAKSHASA)
  !Race(scstarget,LICH)
  GlobalLT("flColourSpray","LOCALS",7)
THEN DO
  Action(ForceSpellRES,"fl#clrsp"|100|100)
  IncrementGlobal("flColourSpray","LOCALS",1)
END

IF TRIGGER
  TargetBlock(EnemiesInOrderShort)
  TriggerBlock(Disabled|MR50|MinorGlobe|Confusion|SIAlt)
  !StateCheck(scstarget,STATE_BLIND)
  LevelGT(scstarget,17)
  Range(scstarget,15)
  !Race(scstarget,RAKSHASA)
  !Race(scstarget,LICH)
  GlobalLT("flColourSpray","LOCALS",7)
THEN DO
  Action(ForceSpellRES,"fl#clrsp"|100|100)
  IncrementGlobal("flColourSpray","LOCALS",1)
END

//Produce Flame
IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(MR75|FireResFull|MinorGlobe|SpellTurning|SIEvo)
  !Race(scstarget,RAKSHASA)
  !Race(scstarget,LICH)
  HPLT(scstarget,15)
  !HasItemEquiped("clck26",scstarget)
THEN DO
  Combine()
  Action(ForceSpellRES,"RR#PFLAM"|100|100)
END

//Charm Person
IF TRIGGER
  !GlobalTimerNotExpired("flFireStormTimer","LOCALS")
  TargetBlock(EnemiesInOrder)
  CheckStatGT(scstarget,5,SAVEVSSPELL)
  TriggerBlock(Disabled|MR50|MinorGlobe|SpellTurning|SIEnc|Charm)
  General(scstarget,HUMANOID)
  !Race(scstarget,RAKSHASA)
THEN DO
  Combine()
  Action(ForceSpellRES,"RR#WI104"|50|100)
END

IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(WeaponImmunityUltroloth|MagicDamageFull)
  StateCheck(scstarget,STATE_IMMOBILE)
THEN DO
  AttackOneRound(scstarget)
END

IF TRIGGER
  TargetBlock(EnemyMagesAndPriests)
  TriggerBlock(Disabled|WeaponImmunityUltroloth|MagicDamageFull|MirrorImage)
  !Range(scstarget,6)
  OR(4)
    Class(scstarget,MAGE_ALL) //Otherwise the script can misbehave, with this block executing for pretty much everyone (due to the targeting, a non-mage, none-priest can be LastSeenBy(Myself) from somewhere above)
    Class(scstarget,DRUID_ALL)
    Class(scstarget,CLERIC_ALL)
    Class(scstarget,CLERIC_RANGER)  
THEN DO
  Combine()
  Action(ForceSpellRES,"RR#TELWE"|100|100)
END

IF TRIGGER
  TargetBlock(EnemyMagesAndPriests)
  TriggerBlock(Disabled|WeaponImmunityUltroloth|MagicDamageFull|MirrorImage)
THEN DO
  AttackOneRound(scstarget)
END

IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(Disabled|WeaponImmunityUltroloth|MagicDamageFull)
  !Range(scstarget,6)
THEN DO
  Action(ForceSpellRES,"RR#TELWE")
END

IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(Disabled|WeaponImmunityUltroloth|MagicDamageFull)
THEN DO
  AttackOneRound(scstarget)
END

IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(WeaponImmunityUltroloth|MagicDamageFull)
  !Range(scstarget,6)
THEN DO
  Action(ForceSpellRES,"RR#TELWE")
END

IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(WeaponImmunityUltroloth|MagicDamageFull)
THEN DO
  AttackOneRound(scstarget)
END

INCLUDE FILE(fiends/include/end_attack.ssl)

INCLUDE FILE(include/open_door.ssl)

IF
  ActionListEmpty()
THEN
  RESPONSE #100
    RandomWalkContinuous()
END
  
  
