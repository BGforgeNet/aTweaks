//call lightning
//color spray, 7x/day (different from the mage spell, because the mage spell sucks but the PnP spell does not)
//detect invisibility, always active
//fear
//fire storm, 1x/day
//symbol, any type, 1x/day
//shout (cone-shaped, deafen and 2d6 damage, save vs spell, does not affect silenced targets)
//mass suggestion, 1x/day (charm, save vs spell at -1)
//gaze of fascination (hold person if the target saves vs spell, "see a dearly loved and respected person" if the target fails his save. charm?)
//They all have this:
//animate dead
//cause disease
//charm person
//produce flame
//teleport without error


//Produce Flame
IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(Disabled|MR50|FireRes50|MinorGlobe|SpellTurning|SIEvo)
  !Race(scstarget,RAKSHASA)
  !Race(scstarget,LICH)
THEN DO
  Combine()
  Action(ForceSpellRES,"RR#PFLAM"|100|100)
END

//Charm Person
IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(Disabled|MR50|MinorGlobe|SpellTurning|SIEnc|Charm)
  General(scstarget,HUMANOID)
  !Race(scstarget,RAKSHASA)
THEN DO
  Combine()
  Action(ForceSpellRES,"RR#WI104"|50|100)
END

//Cause Disease (beefier)
IF TRIGGER
  TargetBlock(EnemiesInOrderShort)
  Range(scstarget,5)
  TriggerBlock(Disabled|MR75|MinorGlobe|SpellTurning|SIAbj|Disease)
THEN DO
  Combine()
  Action(ForceSpellRES,"fl#csdi2"|100|100)
END

//Animate Dead
IF TRIGGER
  TargetBlock(EnemiesInOrder)
  OR(3)
    Class(scstarget,MAGE_ALL)
    Class(scstarget,BARD_ALL)
    Class(scstarget,DRUID_ALL)
THEN DO
  Combine()
  Action(ForceSpellRES,"RR#PR301"|25|100)
END    

IF
  Allegiance(Myself,EVILCUTOFF)
  !See([EVILCUTOFF.UNDEAD.0.SKELTON_WARRIOR.0.SUMMONED])
  !GlobalTimerNotExpired("cast","LOCALS")
THEN
  RESPONSE #100
    ForceSpellRES("RR#PR301",Myself)
    SetGlobalTimer("cast","LOCALS",6)
END  

IF
  Allegiance(Myself,GOODCUTOFF)
  !See([GOODCUTOFF.UNDEAD.0.SKELTON_WARRIOR.0.SUMMONED])
  !GlobalTimerNotExpired("cast","LOCALS")
THEN
  RESPONSE #100
    ForceSpellRES("RR#PR301",Myself)
    SetGlobalTimer("cast","LOCALS",6)
END

//Mass Suggestion
IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(Disabled|MR50|SIEnc|Charm)
  GlobalLT("flMassSuggestion","LOCALS",1)
THEN DO
  Action(ForceSpellRES,"fl#mssug"|100|100)
  IncrementGlobal("flMassSuggestion","LOCALS",1)
END

//Shout
IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(Disabled|MR50|SIEvo)
  !StateCheck(scstarget,STATE_SILENCED)
  !Range(scstarget,5)
  Range(scstarget,15)
THEN DO
  Action(ForceSpellRES,"fl#shout"|100|100)
END

//Symbol, Death
IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(Disabled|MR50|SICon|Slayable)
  OR(4)
    Class(scstarget,MAGE)
    Class(scstarget,MAGE_THIEF)
    Class(scstarget,MAGE_CLERIC)
    HPPercentLT(scstarget,40)
  GlobalLT("flSymbol","LOCALS",1)
THEN DO
  Action(ForceSpellRES,"RR#WI817"|100|50)
  IncrementGlobal("flSymbol","LOCALS",1)
END

//Symbol, Stun
IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(Disabled|MR50|SICon|Stun)
  GlobalLT("flSymbol","LOCALS",1)
THEN DO
  Action(ForceSpellRES,"RR#WI816"|50|100)
  IncrementGlobal("flSymbol","LOCALS",1)
END

//Symbol, Pain
IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(Disabled|MR50|SICon|Stun)
  GlobalLT("flSymbol","LOCALS",1)
THEN DO
  Action(ForceSpellRES,"RR#SPAIN"|50|100)
  IncrementGlobal("flSymbol","LOCALS",1)
END

//Symbol, Fear
IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(Disabled|MR50|SICon|Fear)
  GlobalLT("flSymbol","LOCALS",1)
THEN DO
  Action(ForceSpellRES,"RR#WI811"|50|100)
  IncrementGlobal("flSymbol","LOCALS",1)
END


//Fire Storm
IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(Disabled|FireRes75|SIEvo)
  !Race(scstarget,RAKSHASA)
THEN DO
  Action(ForceSpellRES,"RR#PR705"|100|100)
END

//Fear
IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(Disabled|MR50|SIIll|Fear)
  !Range(scstarget,5)
  Range(scstarget,15)
THEN DO
  Action(ForceSpellRES,"fl#cnefr"|100|100)
END

//Colour Spray
IF TRIGGER
  TargetBlock(EnemiesInOrderShort)
  TriggerBlock(Disabled|MR50|MinorGlobe|Sleep|SIAlt)
  LevelLT(scstarget,6)
  Range(scstarget,10)
  GlobalLT("flColourSpray","LOCALS",7)
THEN DO
  Action(ForceSpellRES,"fl#clrsp"|100|50)
  IncrementGlobal("flColourSpray","LOCALS",1)
END

IF TRIGGER
  TargetBlock(EnemiesInOrderShort)
  TriggerBlock(Disabled|MR50|MinorGlobe|Sleep|SIAlt)
  LevelGT(scstarget,5)
  LevelLT(scstarget,16)
  Range(scstarget,10)
  GlobalLT("flColourSpray","LOCALS",7)
THEN DO
  Action(ForceSpellRES,"fl#clrsp"|100|100)
  IncrementGlobal("flColourSpray","LOCALS",1)
END

IF TRIGGER
  TargetBlock(EnemiesInOrderShort)
  TriggerBlock(Disabled|MR50|MinorGlobe|Blindness|SIAlt)
  LevelGT(scstarget,15)
  LevelLT(scstarget,18)
  Range(scstarget,10)
  GlobalLT("flColourSpray","LOCALS",7)
THEN DO
  Action(ForceSpellRES,"fl#clrsp"|100|100)
  IncrementGlobal("flColourSpray","LOCALS",1)
END

IF TRIGGER
  TargetBlock(EnemiesInOrderShort)
  TriggerBlock(Disabled|MR50|MinorGlobe|Confusion|SIAlt)
  LevelGT(scstarget,17)
  Range(scstarget,10)
  GlobalLT("flColourSpray","LOCALS",7)
THEN DO
  Action(ForceSpellRES,"fl#clrsp"|100|100)
  IncrementGlobal("flColourSpray","LOCALS",1)
END

IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(Disabled|MR50|ElecResFull|MinorGlobe|SIAlt)
  AreaType(OUTDOOR)
THEN DO
  Combine()
  Action(ForceSpellRES,"RR#PR302"ยก100|50)
END
  
  
