//fear, 1x/day
//invisibility
//magic missile
//telekinesis
//mage of 12th level
//They all have this:
//animate dead
//cause disease
//charm person
//produce flame
//teleport without error

INCLUDE FILE(fiends/definitions.ssl)

IF
  HasItem("dw#sumfi",Myself)
THEN
  RESPONSE #100
    DestroyItem("dw#sumfi")
    Continue()
END

IF
  Die()
  Global("fl#EACheck","LOCALS",1) // Fiend was summoned by an enemy
  Global("fl#XP","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("fl#XP","LOCALS",1)
    AddexperienceParty(20000) //Arcanaloth XP value
END

INCLUDE FILE(fiends/include/summoned_check.ssl)

INCLUDE FILE(fiends/include/get_mad_at_summoner.ssl)

IF
  StateCheck(Myself,STATE_REALLY_DEAD)
THEN
  RESPONSE #100
    NoAction()
END

IF 
  !InActiveArea(Myself)
  !See([ANYONE])
THEN
  RESPONSE #100
    NoAction()
END

IF
  !GlobalTimerNotExpired("cast","LOCALS")
  OR(2)
    AreaCheck("AR3004")
    AreaCheck("AR3008")
THEN
  RESPONSE #100
    SetGlobalTimer("cast","LOCALS",600) //No casting in dead-magic areas
END

IF
  Global("flDecideMinorSequencer","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("flDecideMinorSequencer","LOCALS",1)
  RESPONSE #100
    SetGlobal("flDecideMinorSequencer","LOCALS",2)
  RESPONSE #100
    SetGlobal("flDecideMinorSequencer","LOCALS",3)
END

IF
  Global("flContingency","LOCALS",0)
THEN
  RESPONSE #100
    DisplayStringHead(Myself,25942)
    ReallyForceSpell(Myself,WIZARD_IMPROVED_INVISIBILITY)
    SetGlobal("flContingency","LOCALS",1)
  RESPONSE #100
    DisplayStringHead(Myself,25942)
    ReallyForceSpell(Myself,WIZARD_MINOR_GLOBE_OF_INVULNERABILITY)
    SetGlobal("flContingency","LOCALS",2)
  RESPONSE #100
    DisplayStringHead(Myself,25942)
    ReallyForceSpell(Myself,WIZARD_STONE_SKIN)
    SetGlobal("flContingency","LOCALS",3)
  RESPONSE #100
    DisplayStringHead(Myself,25942)
    ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_COLD)
    SetGlobal("flContingency","LOCALS",4)
  RESPONSE #100
    DisplayStringHead(Myself,25942)
    ReallyForceSpell(Myself,WIZARD_MELF_METEOR)
    SetGlobal("flContingency","LOCALS",5)
  RESPONSE #100
    DisplayStringHead(Myself,25942)
    ReallyForceSpell(Myself,WIZARD_FIRE_SHIELD_BLUE)
    SetGlobal("flContingency","LOCALS",6)
END

INCLUDE FILE(fiends/include/teleport_away.ssl)

INCLUDE FILE(fiends/include/high_porting.ssl)

INCLUDE FILE(fiends/include/yugoloth_rampage.ssl)

IF TRIGGER
  TriggerBlock(CanCastMage)
  !CheckStatGT(Myself,0,STONESKINS)
THEN DO
  Action(SpellMyself,WIZARD_STONE_SKIN)
END

IF TRIGGER
  TriggerBlock(CanCastMage)
  !CheckStatGT(Myself,0,RESISTCOLD)
THEN DO
  Action(SpellMyself,WIZARD_PROTECTION_FROM_COLD|100|50)
END

IF TRIGGER
  TriggerBlock(CanCastMage)
  !HasItem("melfmet",Myself)
THEN DO
  Action(SpellMyself,WIZARD_MELF_METEOR|100|50)
END

IF TRIGGER
  Target(NearestEnemyOf(Myself))
  !CheckStatGT(NearestEnemyOf(Myself),0,TRUE_SIGHT)
  !CheckStatGT(SecondNearestEnemyOf(Myself),0,TRUE_SIGHT)
  !CheckStatGT(ThirdNearestEnemyOf(Myself),0,TRUE_SIGHT)
  !CheckStatGT(FourthNearestEnemyOf(Myself),0,TRUE_SIGHT)
  !CheckStatGT(FifthNearestEnemyOf(Myself),0,TRUE_SIGHT)
  !CheckStatGT(SixthNearestEnemyOf(Myself),0,TRUE_SIGHT)
  TriggerBlock(CanCastMage)
  !StateCheck(Myself,STATE_MIRRORIMAGE)
THEN DO
  Action(SpellMyself,WIZARD_MIRROR_IMAGE|100|100)
END

IF TRIGGER
  TriggerBlock(CanCastMage)
THEN DO
  Action(SpellMyself,WIZARD_SHIELD|100|100)
END

//Invisibility
IF TRIGGER
  !StateCheck(Myself,STATE_INVISIBLE)
  TriggerBlock(OKForInvis)
THEN DO
  Action(ForceSpellRESMyself,"RR#WI206"|100|400)
END  

IF TRIGGER
  TargetBlock(EnemyMagesInOrder)
  TriggerBlock(CanCastMage|Disabled)
  !CheckStatGT(scstarget,0,WIZARD_SPELL_TRAP)
  OR(6)
    CheckStatGT(scstarget,0,WIZARD_SPELL_IMMUNITY)
    CheckStatGT(scstarget,0,WIZARD_SPELL_DEFLECTION)
    CheckStatGT(scstarget,0,WIZARD_SPELL_TURNING)
    CheckStat(scstarget,1,CLERIC_SHIELD_OF_THE_ARCHONS)
    CheckStatGT(scstarget,0,MINORGLOBE)
  !Race(scstarget,RAKSHASA)
  !Race(scstarget,LICH)
THEN DO
  Combine()
  Action(Spell,WIZARD_SECRET_WORD|100|50)
END

IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(CanCastMage|SpellTurning|SIAbj|Disabled)
  OR(5)
    CheckStatGT(scstarget,0,TRUE_SIGHT)
    CheckStatGT(scstarget,0,STONESKINS)
    CheckStatGT(scstarget,0,CLERIC_CHAOTIC_COMMANDS)
    CheckStatGT(scstarget,0,CLERIC_FREE_ACTION)
    CheckStatGT(scstarget,0,WIZARD_RESIST_FEAR)
  !Race(scstarget,RAKSHASA)
  !Race(scstarget,LICH)
THEN DO
  Combine()
  Action(Spell,WIZARD_BREACH|100|50)
END 

IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(CanCastMage|Disabled|MR50|SIEnc)
  !Race(scstarget,RAKSHASA)
  !Race(scstarget,LICH)
THEN DO
  Action(Spell,WIZARD_GREATER_MALISON|100|25)
END

IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(CanCastMage|Disabled|MR50|Confusion|SIEnc)
  !Race(scstarget,RAKSHASA)
THEN DO
  Action(Spell,WIZARD_CHAOS|100|100)
END  

IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(CanCastMage|Disabled|MR50|ElecRes50|SpellTurning|SIEvo)
  !HasItemEquiped("clck24",scstarget)
  !HasItemEquiped("clck26",scstarget)
  !Race(scstarget,RAKSHASA)
THEN DO
  Combine()
  Action(Spell,WIZARD_CHAIN_LIGHTNING|100|100)
END

IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(CanCastMage|Disabled|MR50|SpellTurning|SIEnc|Charm)
  LevelGT(scstarget,8)
  !Race(scstarget,RAKSHASA)
THEN DO
  Combine()
  Action(Spell,WIZARD_DOMINATION|100|100)
END

IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(CanCastMage|Disabled|MR50|SpellTurning|MinorGlobe|SIEnc|Hold)
  LevelGT(scstarget,8)
  !Race(scstarget,RAKSHASA)
THEN DO
  Combine()
  Action(Spell,WIZARD_HOLD_MONSTER|100|100)
END

IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(Disabled|MR75|SpellTurning|SIAlt)
  Range(scstarget,10)
  !Race(scstarget,GIANT)
  !Race(scstarget,DRAGON)
  !Race(scstarget,RAKSHASA)
  !Race(scstarget,LICH)
THEN DO
  Action(ForceSpellRES,"fl#telki"|100|50)
END

IF TRIGGER
  Global("flDecideMinorSequencer","LOCALS",1)
  TargetBlock(EnemiesInOrder)
  TriggerBlock(Disabled|MR50|SpellTurning|MinorGlobe|SICon|CanCastMage|AcidRes75)
  !HasItemEquiped("clck26",scstarget)
  !Race(scstarget,RAKSHASA)
  !Race(scstarget,LICH)
THEN DO
  Action(MinorSequencer,WIZARD_MELF_ACID_ARROW,WIZARD_MELF_ACID_ARROW|100|50)
END

IF TRIGGER
  Global("flDecideMinorSequencer","LOCALS",2)
  TargetBlock(EnemiesInOrder)
  TriggerBlock(Disabled|MR50|SpellTurning|MinorGlobe|SIEvo|CanCastMage|FireRes75)
  !HasItemEquiped("clck26",scstarget)
  !Race(scstarget,RAKSHASA)
  !Race(scstarget,LICH)
THEN DO
  Action(MinorSequencer,WIZARD_AGANNAZAR_SCORCHER,WIZARD_AGANNAZAR_SCOCHER|100|50)
END 

IF TRIGGER
  Global("flDecideMinorSequencer","LOCALS",3)
  TargetBlock(EnemiesInOrder)
  TriggerBlock(Disabled|MR50|SpellTurning|MinorGlobe|SIEvo|CanCastMage|FireRes75|AcidRes75)
  !HasItemEquiped("clck26",scstarget)
  !Race(scstarget,RAKSHASA)
  !Race(scstarget,LICH)
THEN DO
  Action(MinorSequencer,WIZARD_AGANNAZAR_SCORCHER,WIZARD_MELF_ACID_ARROW|100|50)
END 

IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(CanCastMage|Disabled|MR50|SIEvo|MinorGlobe|ElecRes50)
  !HasItemEquiped("clck24",scstarget)
  !HasItemEquiped("clck26",scstarget)
  !Race(scstarget,RAKSHASA)
  !Race(scstarget,LICH)
THEN DO
  Combine()
  Action(Spell,WIZARD_LIGHTNING_BOLT|100|100)
END

IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(CanCastMage|Disabled|MR50|SICon|MinorGlobe|SpellTurning|FireRes50)
  !HasItemEquiped("clck26",scstarget)
  !Race(scstarget,RAKSHASA)
  !Race(scstarget,LICH)
THEN DO
  Combine()
  Action(Spell,WIZARD_FLAME_ARROW|100|100)
END

//Animate Dead
IF TRIGGER
  TargetBlock(EnemiesInOrderShort)
  !Range(scstarget,10)
THEN DO
  Action(ForceSpellRES,"RR#PR301"|50|100)
END

IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(CanCastMage|Disabled|MR50|SINec|MinorGlobe|Fear)
  !Race(scstarget,RAKSHASA)
THEN DO
  Action(Spell,WIZARD_HORROR|100|100)
END

IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(CanCastMage|Disabled|MR50|SICon|MinorGlobe|SpellTurning|AcidRes50)
  !HasItemEquiped("clck26",scstarget)
  !Race(scstarget,RAKSHASA)
  !Race(scstarget,LICH)
THEN DO
  Combine()
  Action(Spell,WIZARD_MELF_ACID_ARROW|100|100)
END

//Fear
IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(Disabled|MR50|SIIll|Fear)
  Range(scstarget,20)
  //Morale break?
  Global("fl#FearCone","LOCALS",0)
  !Race(scstarget,RAKSHASA)
THEN DO
  Action(ForceSpellRES,"fl#cnefr"|100|100)
  IncrementGlobal("fl#FearCone","LOCALS",1)
END

//Produce Flame
IF TRIGGER
  TargetBlock(EnemyTroll)
  TriggerBlock(FireResFull|SpellTurning|MinorGlobe)
  !StateCheck(scstarget,STATE_NOT_TARGETABLE)
  HPLT(scstarget,15)
THEN DO
  Action(ForceSpellRES,"RR#PFLAM"|100|50)
END  

IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(CanCastMage|Disabled|MR50|SIEvo|MinorGlobe|SpellTurning|Slayable)
  CheckStat(scstarget,1,24) //Greater Malison
  !HasItemEquiped("clck26",scstarget)
  !Race(scstarget,RAKSHASA)
  !Race(scstarget,LICH)
THEN DO
  Combine()
  Action(Spell,WIZARD_CHROMATIC_ORB|100|100)
END

IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(CanCastMage|Disabled|MR50|SIIll|MinorGlobe|SpellTurning|Blindness)
  !StateCheck(scstarget,STATE_BLIND)
  !Race(scstarget,RAKSHASA)
  !Race(scstarget,LICH)
THEN DO
  Combine()
  Action(Spell,WIZARD_BLINDNESS|100|100)
END

IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(CanCastMage|Disabled|MR50|SIIll|MinorGlobe|SpellTurning|Fear)
  !Race(scstarget,RAKSHASA)
THEN DO
  Combine()
  Action(Spell,WIZARD_SPOOK|100|100)
END

//Cause Disease
IF TRIGGER
  TargetBlock(EnemiesInOrderShort)
  TriggerBlock(Disabled|MR50|SpellTurning|MinorGlobe|SIAbj|Disease)
  Range(scstarget,5)
  !Race(scstarget,RAKSHASA)
THEN DO
  Combine()
  Action(ForceSpellRES,"fl#csdis"|40|100)
END

//Magic Missile
IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(Disabled|MR75|SpellTurning|MinorGlobe|SIEvo)
  !Race(scstarget,LICH)
  !Race(scstarget,RAKSHASA)
  !HasItemEquiped("clck26",scstarget)
THEN DO
  Action(ForceSpellRES,"RR#WI112"|100|100)
END

//Charm Person
IF TRIGGER
  TargetBlock(EnemiesInOrder)
  General(scstarget,HUMANOID)
  !Race(scstarget,RAKSHASA)
  CheckStatGT(scstarget,0,SAVEVSSPELL)
  TriggerBlock(Disabled|MR50|SpellTurning|MinorGlobe|SIEnc|Charm)
THEN DO
  Combine()
  Action(ForceSpellRES,"RR#WI104"|100|100)
END

//Gun for susceptible spellcasters primarily
IF TRIGGER
  HasItem("melfmet",Myself)
  TargetBlock(EnemiesInOrder)
  TriggerBlock(Disabled|MR50|SpellTurning|MinorGlobe|PierceRes75|WeaponImmunity)
  OR(2)
    CheckStatLT(scstarget,100,RESISTFIRE)
    !CheckStatGT(scstarget,0,STONESKINS)
  OR(5)
    Class(scstarget,MAGE_ALL)
    Class(scstarget,BARD_ALL)
    Class(scstarget,CLERIC_ALL)
    Class(scstarget,CLERIC_RANGER)
    Class(scstarget,DRUID_ALL)
THEN DO
  AttackOneRound(scstarget)
END

//Then anyone else
IF TRIGGER
  HasItem("melfmet",Myself)
  TargetBlock(EnemiesInOrder)
  TriggerBlock(Disabled|PierceRes75|WeaponImmunity)
THEN DO
  AttackOneRound(scstarget)
END 

IF TRIGGER
  HasItem("melfmet",Myself)
  TargetBlock(EnemiesInOrder)
  TriggerBlock(PierceResFull|WeaponImmunity)
THEN DO
  AttackOneRound(scstarget)
END  

IF TRIGGER
  TargetBlock(EnemiesInOrderShort)
  TriggerBlock(WeaponImmunity|SlashResFull)
  StateCheck(scstarget,STATE_IMMOBILE)
  Range(scstarget,5)
THEN DO
  AttackOneRound(scstarget)
END

IF TRIGGER
  TargetBlock(EnemiesInOrderShort)
  TriggerBlock(WeaponImmunity|SlashResFull)
  Range(scstarget,5)
THEN DO
  AttackOneRound(scstarget)
END

IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(WeaponImmunity|SlashResFull)
  !Range(scstarget,6)
THEN DO
  Action(ForceSpellRES,"RR#TELWE")
END

//Inviso-up if there's nothing better to do
IF TRIGGER
  !StateCheck(Myself,STATE_INVISIBLE)
  TriggerBlock(OKForInvis)
THEN DO
  Action(ForceSpellRESMyself,"RR#WI206"|100|25)
END

INCLUDE FILE(fiends/include/end_attack.ssl)

IF
  ActionListEmpty()
THEN
  RESPONSE #100
    RandomWalkContinuous()
END
  
  
  




















  
  
  










