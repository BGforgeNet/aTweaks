//Command, 3x/day
//detect invisibility, always active
//dispel magic (not implemented)
//fear, by touch
//invisibility
//mirror image, 3x/day
//They all have this:
//animate dead
//cause disease
//charm person
//produce flame
//teleport without error

INCLUDE FILE(fiends/definitions.ssl)

IF
  HasItem("dw#sumfi",Myself)
THEN
  RESPONSE #100
    DestroyItem("dw#sumfi")
    Continue()
END

IF
  Die()
  Global("fl#EACheck","LOCALS",1) // Fiend was summoned by an enemy
  Global("fl#XP","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("fl#XP","LOCALS",1)
    AddexperienceParty(25000) //Nycaloth XP value
END

INCLUDE FILE(fiends/include/summoned_check.ssl)

IF
  StateCheck(Myself,STATE_REALLY_DEAD)
THEN
  RESPONSE #100
    NoAction()
END

IF 
  !InActiveArea(Myself)
  !See([ANYONE])
THEN
  RESPONSE #100
    NoAction()
END

IF
  !GlobalTimerNotExpired("cast","LOCALS")
  OR(2)
    AreaCheck("AR3004")
    AreaCheck("AR3008")
THEN
  RESPONSE #100
    SetGlobalTimer("cast","LOCALS",600) //No casting in dead-magic areas
END

INCLUDE FILE(fiends/include/teleport_away.ssl)
//Do we need behaviour for hostile demons being attacked by someone under ProEvil?
//Do we need behaviour for demons being attacked by EVILCUTOFF under ProEvil?

INCLUDE FILE(fiends/include/high_porting.ssl)

INCLUDE FILE(fiends/include/yugoloth_rampage.ssl)

//Animate Dead if the enemy isn't too close
IF TRIGGER
  TargetBlock(EnemiesInOrderShort)
  !Range(scstarget,10)
THEN DO
  Action(ForceSpellRES,"RR#PR301"|50|150)
END

//Mirror Image if there's no True Sight
IF TRIGGER
  OR(6)
    Detect(NearestEnemyOf(Myself))
    CheckStatGT(NearestEnemyOf(Myself),0,PROTECTION_FROM_EVIL)
    HasItemEquipedReal("staf11",NearestEnemyOf(Myself))
    HasItemEquiped("wa2amu",NearestEnemyOf(Myself))
    HasItemEquiped("lich",NearestEnemyOf(Myself))
    HasItemEquiped("jonhp1",NearestEnemyOf(Myself))
  !CheckStatGT(NearestEnemyOf(Myself),0,TRUE_SIGHT)
  !CheckStatGT(SecondNearestEnemyOf(Myself),0,TRUE_SIGHT)
  !CheckStatGT(ThirdNearestEnemyOf(Myself),0,TRUE_SIGHT)
  !CheckStatGT(FourthNearestEnemyOf(Myself),0,TRUE_SIGHT)
  !CheckStatGT(FifthNearestEnemyOf(Myself),0,TRUE_SIGHT)
  !CheckStatGT(SixthNearestEnemyOf(Myself),0,TRUE_SIGHT)
  !StateCheck(Myself,STATE_MIRRORIMAGE)
  GlobalLT("flMirrorImage","LOCALS",3)
THEN DO
  Action(ForceSpellRESMyself,"RR#WI212")
  IncrementGlobal("flMirrorImage","LOCALS",1)
END

//Fear, by touch
IF TRIGGER
  TargetBlock(EnemiesInOrderShort)
  Range(scstarget,5)
  TriggerBlock(Disabled|MinorGlobe|SpellTurning|Fear|MR75|SIIll)
THEN DO
  Combine()
  Action(ForceSpellRES,"fl#tchfr"|30|100)
END

//Cause disease, also by touch
IF TRIGGER
  TargetBlock(EnemiesInOrderShort)
  Range(scstarget,5)
  TriggerBlock(Disabled|MinorGlobe|SpellTurning|Disease|MR75|SIAbj)
THEN DO
  Combine()
  Action(ForceSpellRES,"fl#csdis"|30|100)
END 

//Charm person
IF TRIGGER
  TargetBlock(EnemiesInOrder)
  General(scstarget,HUMANOID)
  !Race(scstarget,RAKSHASA)
  TriggerBlock(Disabled|MinorGlobe|SpellTurning|Charm|SIEnc|MR50)
THEN DO
  Combine()
  Action(ForceSpellRES,"RR#WI104"|50|100)
END  
  
//Toss fire at targets low on HP
IF TRIGGER
  TargetBlock(EnemiesInOrder)
  HPLT(scstarget,15)
  !Race(scstarget,LICH)
  !Race(scstarget,RAKSHASA)
  TriggerBlock(MinorGlobe|SpellTurning|FireResFull|MR75|SIEvo)
  !HasItemEquiped("clck26",scstarget)
THEN DO
  Combine()
  Action(ForceSpellRES,"RR#PFLAM"|100|100)
END

//Command
IF TRIGGER
  TargetBlock(EnemiesInOrderShort)
  Range(scstarget,6)
  LevelLT(scstarget,6)
  TriggerBlock(Disabled|MinorGlobe|SpellTurning|SIEnc|Sleep|MR50|Soneskin|MirrorImage)
  GlobalLT("flCommand","LOCALS",3)
THEN DO
  Combine()
  Action(ForceSpellRES,"RR#PR102"|100|100)
  IncrementGlobal("flCommand","LOCALS",1)
END

IF TRIGGER
  TargetBlock(EnemiesInOrderShort)
  Range(scstarget,6)
  TriggerBlock(Disabled|MinorGlobe|SpellTurning|SIEnc|Sleep|MR50|Stoneskin|MirrorImage)
  GlobalLT("flCommand","LOCALS",3)
THEN DO
  Combine()
  Action(ForceSpellRES,"RR#PR102"|100|200)
  IncrementGlobal("flCommand","LOCALS",1)
END

IF TRIGGER
  Global("fl#Summoner","LOCALS",0)
  Target(LastSummonerOf(Myself))
  Exists(scstarget)
  TriggerBlock(ProEvil)
  //!HasItemEquiped("lich",scstarget)
  //!HasItemEquiped("jonhp1",scstarget)
  !StateCheck(scstarget,STATE_REALLY_DEAD)
  !StateCheck(Myself,STATE_INVISIBLE)
  !Race(scstarget,DEMONIC)
THEN DO
  SetInterrupt(FALSE)
  SetGlobal("fl#Summoner","LOCALS",1)
  FaceObject(scstarget)
  DisplayStringHead(Myself,@681) // Foolish mortal! You have unleashed powers far beyond your comprehension by summoning me here.
  SetInterrupt(TRUE)
END

IF TRIGGER
  TargetBlock(EnemiesInOrder)
  !Race(scstarget,LICH)
  !Race(scstarget,RAKSHASA)
  TriggerBlock(MRFull|FireResFull|SpellTurning|MinorGlobe|SIEvo|ProEvil)
  //!HasItemEquiped("lich",scstarget)
  //!HasItemEquiped("jonhp1",scstarget)
  !HasItemEquiped("clck26",scstarget)
  Range(NearestEnemyOf(Myself),5)
THEN DO
  Combine()
  Action(ForceSpellRES,"RR#PFLAM"|100|100)
END 

IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(WeaponImmunity|SlashResFull|ProEvil|Stoneskin|MirrorImage|Disabled)
  //!HasItemEquiped("lich",scstarget)
  //!HasItemEquiped("jonhp1",scstarget)
  Range(scstarget,5)
THEN DO
  AttackOneRound(scstarget)
END 

IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(WeaponImmunity|SlashResFull|ProEvil|Stoneskin|MirrorImage|Disabled)
  //!HasItemEquiped("lich",scstarget)
  //!HasItemEquiped("jonhp1",scstarget)
  !Range(scstarget,5)
  InMyArea(scstarget)
THEN DO
  Combine()
  Action(ForceSpellRES,"RR#TELWE")
END

IF TRIGGER)
  TargetBlock(EnemiesInOrder)
  TriggerBlock(WeaponImmunity|SlashResFull|ProEvil)
  //!HasItemEquiped("lich",scstarget)
  //!HasItemEquiped("jonhp1",scstarget)
  Range(scstarget,5)
THEN DO
  AttackOneRound(scstarget)
END

IF TRIGGER
  TargetBlock(EnemiesInOrder)
  TriggerBlock(WeaponImmunity|SlashResFull|ProEvil)
  //!HasItemEquiped("lich",scstarget)
  //!HasItemEquiped("jonhp1",scstarget)
  !Range(scstarget,5)
  InMyArea(scstarget)
THEN DO
  Combine()
  Action(ForceSpellRES,"RR#TELWE")
END

//Inviso-up if there's nothing better to do
IF TRIGGER
  !StateCheck(Myself,STATE_INVISIBLE)
  TriggerBlock(OKForInvis)
THEN DO
  Action(ForceSpellRESMyself,"RR#WI206"|100|25)
END

//IF
//  See([ANYONE])
//  Allegiance(Myself,EVILCUTOFF)
//  Race(LastSeenBY(Myself),DEMONIC)
//  Alignment(LastSeenBy(Myself),MASK_EVIL)
//  !Allegiance(LastSeenBy(Myself),GOODCUTOFF)
//THEN
//  RESPONSE #100
//    RandomWalkContinuous()
//END

//IF
//  See([ANYONE])
//  Allegiance(Myself,GOODCUTOFF)
//  Race(LastSeenBy(Myself),DEMONIC)
//  Alignment(LastSeenBy(Myself),MASK_EVIL)
//  !Allegiance(LastSeenBy(Myself),EVILCUTOFF)
//THEN
//  RESPONSE #100
//    RandomWalkContinuous()
//END

//We need something for not attacking other summoned demons that also works

IF TRIGGER
  Target([ANYONE])
  Triggerblock(WeaponImmunity|SlashResFull|ProEvil)
  //!HasItemEquiped("lich",scstarget)
  //!HasItemEquiped("jonhp1",scstarget)
  Range(scstarget,5)
THEN DO
  AttackOneRound(scstarget)
END

IF TRIGGER
  Target([ANYONE])
  Triggerblock(WeaponImmunity|SlashResFull|ProEvil)
  //!HasItemEquiped("lich",scstarget)
  //!HasItemEquiped("jonhp1",scstarget)
  !Range(scstarget,5)
  InMyArea(scstarget)
THEN DO
  Action(ForceSpellRES,"RR#TELWE")
END

IF
  ActionListEmpty()
THEN
  RESPONSE #100
    RandomWalkContinous()
END
    
